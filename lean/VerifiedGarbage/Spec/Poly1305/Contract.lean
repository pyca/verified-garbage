import VerifiedGarbage.Spec.Poly1305
import VerifiedGarbage.TCB.Artifact

/-!
# Poly1305: the contracts, on every target

**Trusted** (as every file in `Spec/`). The contracts of streaming Poly1305
(`init`/`update`/`finalize` and `blocks`, on the representations `Buffered`
and `Repr`), in terms of `Spec/Poly1305.lean`, for any target: `A` is the
target's calling convention. The signatures fix where the arguments are, the
memory each function may access, disjointness, and that the pointers and
lengths are public (see `TCB/Sig.lean`); the contracts add the rest. The key,
the accumulator and the message are secret; the message's length so far
(`count`) is public, as the lengths of its pieces are.

`vg_poly1305_init` stores the key and a zero accumulator (the empty message,
which `Repr` and `Buffered` both describe). `vg_poly1305_update` absorbs
bytes of any length, buffering in the state the ones that do not fill a
block, and `vg_poly1305_finalize` absorbs the buffered bytes and computes the
tag, as the hashes' `update` and `finalize` do; like theirs, they keep 128
bytes of working space on the stack, since the state's own working space
(bytes 72–127) is too small for some targets. `vg_poly1305_finalize_scratch`
is `finalize` with its working space passed in `scratch`, for functions that
call it with theirs (ChaCha20-Poly1305's). `vg_poly1305_blocks` absorbs
whole blocks into the state of a message of whole blocks, for callers that
pad their message themselves (e.g. ChaCha20-Poly1305).

Each contract takes the number of bytes of stack below the stack pointer
that an implementation's calls and frames use (`stack`, see
`Sig.contract`), 0 for one that uses none.
-/

namespace VG.Spec.Poly1305

/-- `vg_poly1305_init(state: *mut [u64; 16], key: *const [u8; 32])`. -/
def initSig : Sig where
  params := [("state", .array true .u64 16), ("key", .array false .u8 32)]

/-- Makes the state at `state` represent the empty message under the 32-byte
one-time key at `key`. -/
def initContract {M : ISA} (A : Abi M) (stack : Nat := 0) : Contract M :=
  initSig.contract A (post := fun state key m m' _ => Repr m' state (bytesAt m key 32) [])
    (stack := stack)

/-- `vg_poly1305_init` on every target. -/
def initApi : Api where
  module := "poly1305"
  name := "vg_poly1305_init"
  sig := initSig
  contracts := some fun A stack => initContract A stack
  summary := "Starts a Poly1305 computation (RFC 8439 §2.5): makes the streaming state `*state` \
    represent the empty message under the 32-byte one-time key `*key`.\n\n\
    Contract: `VG.Spec.Poly1305.initContract`. The streaming state is the accumulator followed by \
    the key (`VG.Spec.Poly1305.Repr`). Constant time: only the pointers may affect timing, not the \
    key."
  safety := []

/-- `vg_poly1305_blocks(state: *mut [u64; 16], blocks: *const [u8; 16], n: usize)`. -/
def blocksSig : Sig where
  params := [("state", .array true .u64 16), ("blocks", .slice false (.array .u8 16) "n")]

/-- If the state at `state` represents a message `msg` under a key, then
afterwards it represents `msg` followed by the `n` 16-byte blocks at
`blocks`, under the same key. -/
def blocksContract {M : ISA} (A : Abi M) (stack : Nat := 0) : Contract M :=
  blocksSig.contract A (post := fun state blocks n m m' _ =>
    ∀ key msg, Repr m state key msg → Repr m' state key (msg ++ bytesAt m blocks (16 * n.toNat)))
    (stack := stack)

/-- `vg_poly1305_blocks` on every target. -/
def blocksApi : Api where
  module := "poly1305"
  name := "vg_poly1305_blocks"
  sig := blocksSig
  contracts := some fun A stack => blocksContract A stack
  summary := "Absorbs whole blocks into a Poly1305 computation: if the streaming state `*state` \
    represents a message under a key, it then represents that message followed by the `n` 16-byte \
    blocks at `blocks`, under the same key.\n\n\
    Contract: `VG.Spec.Poly1305.blocksContract`. Constant time: only the pointers and `n` may \
    affect timing, not the state or the data."
  safety := []

/-- `vg_poly1305_update(state: *mut [u64; 16], count: u64, data: *const u8, len: usize)`.
`count` is public. -/
def updateSig : Sig where
  params := [("state", .array true .u64 16), ("count", .int .u64 true),
    ("data", .slice false .u8 "len")]

/-- If the state at `state` represents a message `msg` of `count` bytes
(modulo 2⁶⁴) under a key, with its last bytes buffered, then afterwards it
represents `msg` followed by the `len` bytes at `data`, under the same key. -/
def updatePost (pb : Nat) : updateSig.Post pb := fun state count data len m m' _ =>
  ∀ key msg, Buffered m state key msg → count = BitVec.ofNat 64 msg.length →
    Buffered m' state key (msg ++ bytesAt m data len.toNat)

/-- `updatePost`. -/
def updateContract {M : ISA} (A : Abi M) (stack : Nat := 0) : Contract M :=
  updateSig.contract A (post := updatePost A.ptrBits) (stack := stack)

/-- `vg_poly1305_update` on every target. -/
def updateApi : Api where
  module := "poly1305"
  name := "vg_poly1305_update"
  sig := updateSig
  contracts := some fun A stack => updateContract A stack
  summary := "Absorbs data into a Poly1305 computation: if the streaming state `*state` represents \
    a message of `count` bytes (modulo 2⁶⁴) under a key, it then represents that message followed \
    by the `len` bytes at `data`, under the same key.\n\n\
    Contract: `VG.Spec.Poly1305.updateContract`. The streaming state is the accumulator, the key \
    and the message's last bytes that do not fill a block (`VG.Spec.Poly1305.Buffered`). \
    Constant time: only the pointers, `count` and `len` may affect timing, not the state or the \
    data."
  safety := []

/-- `vg_poly1305_finalize(state: *mut [u64; 16], count: u64, out: *mut [u8; 16])`.
`count` is public; `state` is left unspecified. -/
def finalizeSig : Sig where
  params := [("state", .array true .u64 16), ("count", .int .u64 true),
    ("out", .array true .u8 16)]

/-- If the state at `state` represents a message `msg` of `count` bytes
(modulo 2⁶⁴) under a key, with its last bytes buffered, writes the Poly1305
tag of `msg` under that key to `out`. -/
def finalizePost (pb : Nat) : finalizeSig.Post pb := fun state count out m m' _ =>
  ∀ key msg, Buffered m state key msg → count = BitVec.ofNat 64 msg.length →
    bytesAt m' out 16 = mac key msg

/-- `finalizePost`. -/
def finalizeContract {M : ISA} (A : Abi M) (stack : Nat := 0) : Contract M :=
  finalizeSig.contract A (post := finalizePost A.ptrBits) (stack := stack)

/-- `vg_poly1305_finalize_scratch(state: *mut [u64; 16], count: u64, out: *mut [u8; 16], scratch: *mut [u64; 16])`:
`vg_poly1305_finalize` with its working space passed in `scratch`, for
functions that call it with theirs (ChaCha20-Poly1305's). -/
def finalizeScratchSig : Sig where
  params := [("state", .array true .u64 16), ("count", .int .u64 true),
    ("out", .array true .u8 16), ("scratch", .array true .u64 16)]

/-- `finalizePost`, whatever `scratch` is. -/
def finalizeScratchContract {M : ISA} (A : Abi M) (stack : Nat := 0) : Contract M :=
  finalizeScratchSig.contract A
    (post := fun state count out _scratch => finalizePost A.ptrBits state count out)
    (stack := stack)

/-- `vg_poly1305_finalize` on every target. -/
def finalizeApi : Api where
  module := "poly1305"
  name := "vg_poly1305_finalize"
  sig := finalizeSig
  contracts := some fun A stack => finalizeContract A stack
  summary := "Finishes a Poly1305 computation: if the streaming state `*state` represents a \
    message of `count` bytes (modulo 2⁶⁴) under a key, writes the tag of that message, under that \
    key, to `*out`.\n\n\
    Contract: `VG.Spec.Poly1305.finalizeContract`. Constant time: only the pointers and `count` \
    may affect timing, not the state."
  safety := ["The contents of `state` on return are unspecified."]

/-- `vg_poly1305_finalize_scratch` on every target. -/
def finalizeScratchApi : Api where
  module := "poly1305"
  name := "vg_poly1305_finalize_scratch"
  sig := finalizeScratchSig
  contracts := some fun A stack => finalizeScratchContract A stack
  summary := "`vg_poly1305_finalize`, with its working space in `*scratch`.\n\n\
    Contract: `VG.Spec.Poly1305.finalizeScratchContract`. Constant time: only the pointers and \
    `count` may affect timing, not the state."
  safety := [
    "The contents of `state` on return are unspecified.",
    "The contents of `scratch` on return are unspecified."]

end VG.Spec.Poly1305
