import VerifiedGarbage.Proof.MlDsa.X86_64.KeyGen.Samp4
import VerifiedGarbage.Proof.MlDsa.KeyGen.Rest

/-!
# ML-DSA key generation on x86-64: after the samplers

Once the samplers are done, with `Â` and `s₁ ‖ s₂` in memory as `A` and `S`
and `r15` as `R` (`Good`), the rest of the function computes the keys from
them, whatever they are (`KR`): after the copies of `ρ` and `K`
(`copies_piece`), the first `np` entries of `s₁ ‖ s₂` packed to `sk`, the
first `nj` of `s₁` in the NTT domain, and the first `nr` rows of `t` packed to
`pk` and `sk`.
-/

namespace VG.Proof.MlDsa.X86_64.KeyGen

open VG VG.X86_64 VG.Proof.MlKem.X86_64
open VG.Impl.MlKem.X86_64 (Ptr sc copy seqR)
open VG.Impl.MlDsa.X86_64.KeyGen
open VG.Spec.MlDsa (Params Poly IPoly toRq ntt polyAt coeffAt Reduced PolyIs bitPack simpleBitPack)
open VG.Proof.MlDsa.KeyGen (t1K t0K)
open VG.Spec.Sha3 (bytesAt)

/-- After the samplers: the keys from `A`, `S` and `R`, so far. -/
structure KR (p : Params) (σ : State) (A : Nat → Poly) (S : Nat → IPoly) (R : BitVec 64) (np nj nr : Nat)
    (s : State) : Prop where
  kc : KC p σ s
  r15 : s.gpr .r15 = R
  good : Good p σ (p.k * p.ℓ) (p.ℓ + p.k) A S R
  small : ∀ r < p.ℓ + p.k, Small p.η (S r)
  aS : ∀ e < p.k * p.ℓ, PolyIs s.mem (pa s (aP e)) (A e)
  s2 : ∀ i < p.k, PolyIs s.mem (pa s (sP p (p.ℓ + i))) (toRq (S (p.ℓ + i)))
  s1 : ∀ j < p.ℓ, PolyIs s.mem (pa s (sP p j)) (if j < nj then ntt (toRq (S j)) else toRq (S j))
  pk0 : bytesAt s.mem (pa s (.r12, 0)) 32 = rhoOf p σ
  sk0 : bytesAt s.mem (pa s (.r13, 0)) 32 = rhoOf p σ
  sk1 : bytesAt s.mem (pa s (.r13, 32)) 32 = kOf p σ
  packs : ∀ r < np, bytesAt s.mem (pa s (.r13, 128 + lenS p * r)) (lenS p) = bitPack (S r) p.η p.η
  rows : ∀ i < nr, bytesAt s.mem (pa s (.r12, 32 + 320 * i)) 320 = simpleBitPack (t1K p A S i) 1023 ∧
    bytesAt s.mem (pa s (.r13, oT0 p + 416 * i)) 416 = bitPack (t0K p A S i) 4095 4096

/-- A piece that writes `ws` keeps what `KR` says. -/
structure KRChk (p : Params) (np nj nr : Nat) (ws : List (Ptr × Nat)) : Prop where
  kc : kcChk p ws = true
  aS : ∀ e < p.k * p.ℓ, keepB (kgB p) ws (aP e) 1024 = true
  s2 : ∀ i < p.k, keepB (kgB p) ws (sP p (p.ℓ + i)) 1024 = true
  s1 : ∀ j < p.ℓ, keepB (kgB p) ws (sP p j) 1024 = true
  pk0 : keepB (kgB p) ws (.r12, 0) 32 = true
  sk0 : keepB (kgB p) ws (.r13, 0) 32 = true
  sk1 : keepB (kgB p) ws (.r13, 32) 32 = true
  packs : ∀ r < np, keepB (kgB p) ws (.r13, 128 + lenS p * r) (lenS p) = true
  rows : ∀ i < nr, keepB (kgB p) ws (.r12, 32 + 320 * i) 320 = true ∧ keepB (kgB p) ws (.r13, oT0 p + 416 * i) 416 = true

/-- The checks of two pieces of writes, for both. -/
theorem KRChk.append {p : Params} {np nj nr : Nat} {ws₁ ws₂ : List (Ptr × Nat)} (h₁ : KRChk p np nj nr ws₁)
    (h₂ : KRChk p np nj nr ws₂) : KRChk p np nj nr (ws₁ ++ ws₂) :=
  ⟨kcChk_append h₁.kc h₂.kc, fun e he => keepB_append (h₁.aS e he) (h₂.aS e he),
    fun i hi => keepB_append (h₁.s2 i hi) (h₂.s2 i hi), fun j hj => keepB_append (h₁.s1 j hj) (h₂.s1 j hj),
    keepB_append h₁.pk0 h₂.pk0, keepB_append h₁.sk0 h₂.sk0, keepB_append h₁.sk1 h₂.sk1,
    fun r hr => keepB_append (h₁.packs r hr) (h₂.packs r hr),
    fun i hi => ⟨keepB_append (h₁.rows i hi).1 (h₂.rows i hi).1, keepB_append (h₁.rows i hi).2 (h₂.rows i hi).2⟩⟩

/-! The checks of a write to one region, proved once for any region: each check
is about the write and one region, apart from it or in another buffer. -/

/-- A write keeps a region of another register, both in the layout, the write's register written. -/
theorem keepB_one_diff {bs : List (Reg × Nat)} {r r' : Reg} {o n o' l : Nat} (hb : r' ∈ bases) (hne : r' ≠ r)
    (hw : r ∈ wRegs) (hq : inB bs (r', o') l = true) (hi : inB bs (r, o) n = true) :
    keepB bs [((r, o), n)] (r', o') l = true := by
  simp only [keepB, List.all_cons, List.all_nil, sepB_diff bs hne (Or.inr hw), hq, hi, decide_eq_true hb,
    Bool.and_self]

/-- A write keeps a region of its register apart from it, both in the layout. -/
theorem keepB_one_same {bs : List (Reg × Nat)} {r : Reg} {o n o' l : Nat} (hb : r ∈ bases)
    (hq : inB bs (r, o') l = true) (hi : inB bs (r, o) n = true) (h : o' + l ≤ o ∨ o + n ≤ o') :
    keepB bs [((r, o), n)] (r, o') l = true := by
  simp only [keepB, List.all_cons, List.all_nil, sepB_same, hq, hi, decide_eq_true hb, Bool.and_true,
    Bool.true_and, Bool.or_eq_true, decide_eq_true_eq]
  exact h

/-- A write to `pk` or `sk` keeps what `KC` says. -/
theorem kcChk_one {p : Params} {r : Reg} {o n : Nat} (hr : r = .r12 ∨ r = .r13)
    (hi : inB (kgB p) (r, o) n = true) : kcChk p [((r, o), n)] = true := by
  have hw : r ∈ wRegs := by rcases hr with rfl | rfl <;> decide
  have hs : 840 + 48 ≤ scrLen p := by simp only [scrLen, Spec.MlDsa.scratchWords]; omega
  simp only [kcChk, topChk, Bool.and_eq_true, List.all_eq_true, List.mem_range, List.all_cons, List.all_nil,
    Bool.and_true]
  refine ⟨⟨fun k hk => keepB_one_diff (by decide) (by rcases hr with rfl | rfl <;> decide) hw
    (by rw [inB_rbx]; exact decide_eq_true (by simp only [VG.Impl.MlKem.X86_64.oSV]; omega)) hi, hi⟩,
    keepB_one_diff (by decide) (by rcases hr with rfl | rfl <;> decide) hw (by rw [inB_rbp]; rfl) hi⟩

/-- A write to `scratch` outside the saved registers and the polynomials. -/
theorem KRChk.rbx {p : Params} (hF : PFacts p) {np nj nr : Nat} (hnp : np ≤ p.ℓ + p.k) (hnr : nr ≤ p.k) {o n : Nat}
    (h1 : o + n ≤ VG.Impl.MlKem.X86_64.oSV ∨ VG.Impl.MlKem.X86_64.oSV + 48 ≤ o)
    (h2 : o + n ≤ oP 0 ∨ oP (p.k * p.ℓ + p.ℓ + p.k) ≤ o) (h3 : o + n ≤ scrLen p) :
    KRChk p np nj nr [((.rbx, o), n)] := by
  have hk := hF.k; have hl := hF.l
  have hi : inB (kgB p) (.rbx, o) n = true := by rw [inB_rbx]; exact decide_eq_true h3
  have hs : scrLen p = 1024 * (p.k * p.ℓ + 4 * p.k + 3 * p.ℓ + 32) := by
    simp only [scrLen, Spec.MlDsa.scratchWords]; omega
  have hsk := hF.sk
  have hpk := hF.pk
  simp only [VG.Impl.MlKem.X86_64.oSV] at h1
  simp only [oP] at h2
  simp only [oT0] at hsk
  have bx : ∀ {o' l : Nat}, o' + l ≤ scrLen p → (o' + l ≤ o ∨ o + n ≤ o') →
      keepB (kgB p) [((.rbx, o), n)] (.rbx, o') l = true :=
    fun h => keepB_one_same (by decide) (by rw [inB_rbx]; exact decide_eq_true h) hi
  have b12 : ∀ {o' l : Nat}, o' + l ≤ p.pkLen → keepB (kgB p) [((.rbx, o), n)] (.r12, o') l = true :=
    fun h => keepB_one_diff (by decide) (by decide) (by decide) (by rw [inB_r12]; exact decide_eq_true h) hi
  have b13 : ∀ {o' l : Nat}, o' + l ≤ p.skLen → keepB (kgB p) [((.rbx, o), n)] (.r13, o') l = true :=
    fun h => keepB_one_diff (by decide) (by decide) (by decide) (by rw [inB_r13]; exact decide_eq_true h) hi
  have kc : kcChk p [((.rbx, o), n)] = true := by
    simp only [kcChk, topChk, Bool.and_eq_true, List.all_eq_true, List.mem_range, List.all_cons, List.all_nil,
      Bool.and_true]
    exact ⟨⟨fun k hk' => bx (by rw [hs]; simp only [VG.Impl.MlKem.X86_64.oSV]; omega)
      (by simp only [VG.Impl.MlKem.X86_64.oSV]; omega), hi⟩,
      keepB_one_diff (by decide) (by decide) (by decide) (by rw [inB_rbp]; rfl) hi⟩
  refine ⟨kc, fun e he => bx ?_ ?_, fun i hi' => bx ?_ ?_, fun j hj => bx ?_ ?_, b12 ?_, b13 ?_, b13 ?_,
    fun r hr' => b13 ?_, fun i hi' => ⟨b12 ?_, b13 ?_⟩⟩
  all_goals try simp only [oP, oT0]
  · rw [hs]; omega_arith
  · omega_arith
  · rw [hs]; omega_arith
  · omega_arith
  · rw [hs]; omega_arith
  · omega_arith
  · omega_arith
  · omega_arith
  · omega_arith
  · rcases hF.eta with ⟨_, hlen⟩ | ⟨_, hlen⟩ <;> simp only [hlen] at hsk ⊢ <;> omega_arith
  · omega_arith
  · omega_arith

/-- A write to `pk` after the rows so far. -/
theorem KRChk.r12 {p : Params} (hF : PFacts p) {np nj nr : Nat} (hnp : np ≤ p.ℓ + p.k) (hnr : nr ≤ p.k) {o n : Nat}
    (h1 : 32 + 320 * nr ≤ o) (h2 : o + n ≤ p.pkLen) : KRChk p np nj nr [((.r12, o), n)] := by
  have hk := hF.k; have hl := hF.l
  have hi : inB (kgB p) (.r12, o) n = true := by rw [inB_r12]; exact decide_eq_true h2
  have hs : scrLen p = 1024 * (p.k * p.ℓ + 4 * p.k + 3 * p.ℓ + 32) := by
    simp only [scrLen, Spec.MlDsa.scratchWords]; omega
  have hsk := hF.sk
  have hpk := hF.pk
  simp only [oT0] at hsk
  have bx : ∀ {o' l : Nat}, o' + l ≤ scrLen p → keepB (kgB p) [((.r12, o), n)] (.rbx, o') l = true :=
    fun h => keepB_one_diff (by decide) (by decide) (by decide) (by rw [inB_rbx]; exact decide_eq_true h) hi
  have b12 : ∀ {o' l : Nat}, o' + l ≤ p.pkLen → (o' + l ≤ o ∨ o + n ≤ o') →
      keepB (kgB p) [((.r12, o), n)] (.r12, o') l = true :=
    fun h => keepB_one_same (by decide) (by rw [inB_r12]; exact decide_eq_true h) hi
  have b13 : ∀ {o' l : Nat}, o' + l ≤ p.skLen → keepB (kgB p) [((.r12, o), n)] (.r13, o') l = true :=
    fun h => keepB_one_diff (by decide) (by decide) (by decide) (by rw [inB_r13]; exact decide_eq_true h) hi
  refine ⟨kcChk_one (.inl rfl) hi, fun e he => bx ?_, fun i hi' => bx ?_, fun j hj => bx ?_, b12 ?_ ?_, b13 ?_,
    b13 ?_, fun r hr' => b13 ?_, fun i hi' => ⟨b12 ?_ ?_, b13 ?_⟩⟩
  all_goals try simp only [oP, oT0]
  · rw [hs]; omega_arith
  · rw [hs]; omega_arith
  · rw [hs]; omega_arith
  · omega_arith
  · omega_arith
  · omega_arith
  · omega_arith
  · rcases hF.eta with ⟨_, hlen⟩ | ⟨_, hlen⟩ <;> simp only [hlen] at hsk ⊢ <;> omega_arith
  · omega_arith
  · omega_arith
  · omega_arith

/-- A write to `sk` after `ρ` and `K`, outside the entries packed and the rows so far. -/
theorem KRChk.r13 {p : Params} (hF : PFacts p) {np nj nr : Nat} (hnp : np ≤ p.ℓ + p.k) (hnr : nr ≤ p.k) {o n : Nat}
    (h0 : 64 ≤ o) (hp : o + n ≤ 128 ∨ 128 + lenS p * np ≤ o) (hr : o + n ≤ oT0 p ∨ oT0 p + 416 * nr ≤ o)
    (h2 : o + n ≤ p.skLen) : KRChk p np nj nr [((.r13, o), n)] := by
  have hk := hF.k; have hl := hF.l
  have hi : inB (kgB p) (.r13, o) n = true := by rw [inB_r13]; exact decide_eq_true h2
  have hs : scrLen p = 1024 * (p.k * p.ℓ + 4 * p.k + 3 * p.ℓ + 32) := by
    simp only [scrLen, Spec.MlDsa.scratchWords]; omega
  have hsk := hF.sk
  have hpk := hF.pk
  have bx : ∀ {o' l : Nat}, o' + l ≤ scrLen p → keepB (kgB p) [((.r13, o), n)] (.rbx, o') l = true :=
    fun h => keepB_one_diff (by decide) (by decide) (by decide) (by rw [inB_rbx]; exact decide_eq_true h) hi
  have b12 : ∀ {o' l : Nat}, o' + l ≤ p.pkLen → keepB (kgB p) [((.r13, o), n)] (.r12, o') l = true :=
    fun h => keepB_one_diff (by decide) (by decide) (by decide) (by rw [inB_r12]; exact decide_eq_true h) hi
  have b13 : ∀ {o' l : Nat}, o' + l ≤ p.skLen → (o' + l ≤ o ∨ o + n ≤ o') →
      keepB (kgB p) [((.r13, o), n)] (.r13, o') l = true :=
    fun h => keepB_one_same (by decide) (by rw [inB_r13]; exact decide_eq_true h) hi
  refine ⟨kcChk_one (.inr rfl) hi, fun e he => bx ?_, fun i hi' => bx ?_, fun j hj => bx ?_, b12 ?_,
    b13 ?_ ?_, b13 ?_ ?_, fun r hr' => b13 ?_ ?_, fun i hi' => ⟨b12 ?_, b13 ?_ ?_⟩⟩
  all_goals try simp only [oP]
  · rw [hs]; omega_arith
  · rw [hs]; omega_arith
  · rw [hs]; omega_arith
  · omega_arith
  · omega_arith
  · omega_arith
  · omega_arith
  · omega_arith
  · rcases hF.eta with ⟨_, hlen⟩ | ⟨_, hlen⟩ <;> simp only [oT0, hlen] at hsk ⊢ <;> omega_arith
  · rcases hF.eta with ⟨_, hlen⟩ | ⟨_, hlen⟩ <;> simp only [hlen] at hp ⊢ <;> omega_arith
  · omega_arith
  · simp only [oT0] at hsk hr ⊢; omega_arith
  · simp only [oT0] at hr ⊢; omega_arith

theorem KR.keep {p : Params} (hF : PFacts p) {σ : State} (hp : (kgK p).pre σ) {A : Nat → Poly} {S : Nat → IPoly}
    {R : BitVec 64} {np nj nr : Nat} {s s' : State} (h : KR p σ A S R np nj nr s) {ws : List (Ptr × Nat)}
    (hP : PPostB s s' ws) (hx : MX s' = MX s) (h15 : s'.gpr .r15 = s.gpr .r15) (hc : KRChk p np nj nr ws) :
    KR p σ A S R np nj nr s' := by
  have L := h.kc.lay hF hp
  exact ⟨h.kc.step hF hp hP hx hc.kc, h15.trans h.r15, h.good, h.small,
    fun e he => polyIs_frame' L hP (hc.aS e he) (h.aS e he), fun i hi => polyIs_frame' L hP (hc.s2 i hi) (h.s2 i hi),
    fun j hj => polyIs_frame' L hP (hc.s1 j hj) (h.s1 j hj), by rw [L.keepBytes hP hc.pk0]; exact h.pk0,
    by rw [L.keepBytes hP hc.sk0]; exact h.sk0, by rw [L.keepBytes hP hc.sk1]; exact h.sk1,
    fun r hr => by rw [L.keepBytes hP (hc.packs r hr)]; exact h.packs r hr,
    fun i hi => ⟨by rw [L.keepBytes hP (hc.rows i hi).1]; exact (h.rows i hi).1,
      by rw [L.keepBytes hP (hc.rows i hi).2]; exact (h.rows i hi).2⟩⟩

/-! ## `ρ` and `K` to the keys -/

theorem r12_bases (o n : Nat) : ∀ w ∈ [(((.r12, o) : Ptr), n)], w.1.1 ∈ bases := fun w hw => by
  rw [List.mem_singleton] at hw; subst hw; show Reg.r12 ∈ bases; decide

theorem r13_bases (o n : Nat) : ∀ w ∈ [(((.r13, o) : Ptr), n)], w.1.1 ∈ bases := fun w hw => by
  rw [List.mem_singleton] at hw; subst hw; show Reg.r13 ∈ bases; decide

/-- After the copies, for some `A`, `S` and `R`. -/
abbrev KR0 (p : Params) (σ s : State) : Prop := ∃ A S R, KR p σ A S R 0 0 0 s

theorem copies_ok {p : Params} (hF : PFacts p) {σ : State} (hp : (kgK p).pre σ) {s : State}
    (h : KSamp p σ (p.k * p.ℓ) (p.ℓ + p.k) s) : WP isa copies s (KR0 p σ) := by
  have hkl := hF.kl; have hl := hF.l; have hk := hF.k
  have L := h.k1.kc.lay hF hp
  unfold copies
  refine WP.seq (WP.mono (copy_okM L (dst := (.r12, 0)) (src := sc oHX) (n := 32) (by decide)
    (by layd)) fun s₁ ⟨⟨hP₁, hb₁⟩, hx₁⟩ => ?_)
  have L₁ := L.post hP₁.b (kgB_bases p)
  refine WP.seq (WP.mono (copy_okM L₁ (dst := (.r13, 0)) (src := sc oHX) (n := 32) (by decide)
    (by layd)) fun s₂ ⟨⟨hP₂, hb₂⟩, hx₂⟩ => ?_)
  have L₂ := L₁.post hP₂.b (kgB_bases p)
  refine WP.mono (copy_okM L₂ (dst := (.r13, 32)) (src := sc (oHX + 96)) (n := 32) (by decide)
    (by layd)) fun s₃ ⟨⟨hP₃, hb₃⟩, hx₃⟩ => ?_
  have hP := PPostB.app (PPostB.app hP₁.b hP₂.b (r13_bases _ _)) hP₃.b (r13_bases _ _)
  have hx : MX s₃ = MX s := hx₃.trans (hx₂.trans hx₁)
  have h15 : s₃.gpr .r15 = s.gpr .r15 := by
    rw [hP₃.cs .r15 (by decide), hP₂.cs .r15 (by decide), hP₁.cs .r15 (by decide)]
  obtain ⟨A, S, hA, hS, hG⟩ := h.ex
  have hc : kcChk p ([((.r12, 0), 32)] ++ [((.r13, 0), 32)] ++ [((.r13, 32), 32)]) = true ∧
      (∀ e < p.k * p.ℓ, keepB (kgB p) ([((.r12, 0), 32)] ++ [((.r13, 0), 32)] ++ [((.r13, 32), 32)]) (aP e) 1024 = true) ∧
      (∀ r < p.ℓ + p.k, keepB (kgB p) ([((.r12, 0), 32)] ++ [((.r13, 0), 32)] ++ [((.r13, 32), 32)]) (sP p r) 1024
        = true) := by
    exact ⟨by layd, fun _ _ => by layd, fun _ _ => by layd⟩
  -- The bytes of `HX`, and `ρ`, `K`.
  have hHX₁ : bytesAt s₁.mem (pa s₁ (sc oHX)) 128 = hxOf p σ := by rw [L.keepBytes hP₁.b (by layd)]; exact h.k1.hx
  have hHX₂ : bytesAt s₂.mem (pa s₂ (sc oHX)) 128 = hxOf p σ := by
    rw [L₁.keepBytes hP₂.b (by layd)]; exact hHX₁
  have e1 : bytesAt s.mem (pa s (sc oHX)) 32 = rhoOf p σ := by
    rw [rho_eq, ← h.k1.hx, Proof.MlKem.bytesAt_take _ _ (show 32 ≤ 128 by decide)]
  have e1' : bytesAt s₁.mem (pa s₁ (sc oHX)) 32 = rhoOf p σ := by
    rw [rho_eq, ← hHX₁, Proof.MlKem.bytesAt_take _ _ (show 32 ≤ 128 by decide)]
  have e2 : bytesAt s₂.mem (pa s₂ (sc (oHX + 96))) 32 = kOf p σ := by
    rw [kOf_eq, ← hHX₂, Proof.MlKem.bytesAt_slice _ _ (show 96 + 32 ≤ 128 by decide), pa, pa, off_add]
  refine ⟨A, S, s.gpr .r15, ⟨h.k1.kc.step hF hp hP hx hc.1, h15, hG, fun r hr => (hS r hr).2,
    fun e he => polyIs_frame' L hP (hc.2.1 e he) (hA e he),
    fun i hi => polyIs_frame' L hP (hc.2.2 _ (by omega)) (hS (p.ℓ + i) (by omega)).1,
    fun j hj => by rw [ifn (Nat.not_lt_zero j)]; exact polyIs_frame' L hP (hc.2.2 j (by omega)) (hS j (by omega)).1,
    ?_, ?_, ?_, fun _ h => absurd h (Nat.not_lt_zero _), fun _ h => absurd h (Nat.not_lt_zero _)⟩⟩
  · rw [L₂.keepBytes hP₃.b (by layd),
      L₁.keepBytes hP₂.b (by layd),
      hP₁.pa (by decide), hb₁, e1]
  · rw [L₂.keepBytes hP₃.b (by layd),
      hP₂.pa (by decide), hb₂, e1']
  · rw [hP₃.pa (by decide), hb₃, e2]

theorem copies_piece {p : Params} (hF : PFacts p) :
    Piece p (fun σ s => KSamp p σ (p.k * p.ℓ) (p.ℓ + p.k) s) (KR0 p) copies :=
  ⟨fun _ _ hp h => copies_ok hF hp h,
    rel_of (Q := Two p) (taintRel [.rbx, .r12, .r13] (fun x y h r hr => h.regs r (by
        simp only [List.mem_cons, List.not_mem_nil, or_false] at hr
        rcases hr with rfl | rfl | rfl <;> decide)) (by taint_decide))
      fun _ _ _ _ p₁ p₂ pub h₁ h₂ => kc_two hF p₁ p₂ pub h₁.k1.kc h₂.k1.kc⟩

end VG.Proof.MlDsa.X86_64.KeyGen
