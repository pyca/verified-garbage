import VerifiedGarbage.Proof.Ed25519.X86_64.SignCached.Layout

/-! Short symbolic executions for the complete signer's call arguments. -/
namespace VG.Proof.Ed25519.X86_64.SignCached
open VG VG.X86_64
open VG.Impl.Ed25519.X86_64 (stk shaScratch)
open VG.Impl.Ed25519.X86_64.SignCached
open VG.Proof.Ed25519.X86_64.PublicKey (ea_stk add_add sx32 zx32 ne_cs)
variable {L : Lay} {g : Reg → BitVec 64} {mx : BitVec 32} {m₀ : Mem}

def InitArgs (L : Lay) (t : State) : Prop := t.gpr .rdi = L.scr

theorem initArgs_ok {t : State} (hc : Ctx L g mx m₀ t) :
    WP isa (.block initArgs) t fun t' => Ctx L g mx m₀ t' ∧ t'.mem = t.mem ∧ InitArgs L t' := by
  have hin := hc.inFr (d := 216) (by omega) (by omega)
  refine WP.of_runBlock ⟨t.setReg .rdi L.scr, ?_, ?_⟩
  · simp only [initArgs, fScratch, runBlock_cons, runStep_some, runBlock_nil, exec, readSrc,
      State.load64, hc.ea_fr, hin, ite_true, Option.map_some, hc.pScr]
  exact ⟨hc.regs rfl rfl rfl rfl rfl fun r hr => RegUpd.gpr_setReg_of_ne _ _ (ne_cs hr (by decide)), rfl,
    RegUpd.gpr_setReg_self _ _ _⟩

def UpdArgs (L : Lay) (count : BitVec 64) (p : Addr) (n : BitVec 64) (t : State) : Prop :=
  t.gpr .rdi = L.scr ∧ t.gpr .rsi = count ∧ t.gpr .rdx = p ∧ t.gpr .rcx = n ∧
    t.gpr .r8 = L.scr + BitVec.ofNat 64 192

theorem inputArgs_ok {t : State} (hc : Ctx L g mx m₀ t) (source count : Nat)
    (hs : source + 8 ≤ 248) (hcount : count < 2 ^ 32) (p : Addr)
    (hp : t.mem.readW (L.B + BitVec.ofNat 64 (16 + source)) 64 = p) :
    WP isa (.block (inputArgs source count)) t fun t' => Ctx L g mx m₀ t' ∧ t'.mem = t.mem ∧
      UpdArgs L (BitVec.ofNat 64 count) p 32 t' := by
  have h216 := hc.inFr (d := 216) (by omega) (by omega)
  have hsrc := hc.inFr (d := 16 + source) (by omega) (by omega)
  apply WP.of_runBlock
  simp only [inputArgs, scrPtr, shaScratch, fScratch, List.cons_append, List.nil_append,
    runBlock_cons, runStep_some, runBlock_nil, exec, execAlu, readSrc, readSrc32, State.load64,
    State.setReg32, ea_stk, RegUpd.gpr_setReg, RegUpd.rd_setReg, RegUpd.wr_setReg, RegUpd.mem_setReg,
    RegUpd.gpr_arithFlags, RegUpd.mem_arithFlags,
    Option.map_some, Option.bind_some, reduceCtorEq, ite_false, ite_true, hc.rsp, add_add,
    Nat.reduceAdd, h216, hsrc, Option.some.injEq, exists_eq_left', hc.pScr, hp, UpdArgs]
  exact ⟨hc.regs rfl rfl rfl rfl rfl (by cs_tac), trivial, trivial, zx32 hcount,
    trivial, rfl, by rw [sx32 (by omega)]⟩

theorem messageArgs_ok {t : State} (hc : Ctx L g mx m₀ t) (count : Nat)
    (hcount : count < 2 ^ 32) :
    WP isa (.block (messageArgs count)) t fun t' => Ctx L g mx m₀ t' ∧ t'.mem = t.mem ∧
      UpdArgs L (BitVec.ofNat 64 count) L.msg L.len t' := by
  have h216 := hc.inFr (d := 216) (by omega) (by omega)
  have h224 := hc.inFr (d := 224) (by omega) (by omega)
  have h232 := hc.inFr (d := 232) (by omega) (by omega)
  apply WP.of_runBlock
  simp only [messageArgs, scrPtr, shaScratch, fScratch, fMessage, fLength,
    List.cons_append, List.nil_append, runBlock_cons, runStep_some, runBlock_nil,
    exec, execAlu, readSrc, readSrc32, State.load64, State.setReg32, ea_stk,
    RegUpd.gpr_setReg, RegUpd.rd_setReg, RegUpd.wr_setReg, RegUpd.mem_setReg,
    RegUpd.gpr_arithFlags, RegUpd.mem_arithFlags, Option.map_some, Option.bind_some,
    reduceCtorEq, ite_false, ite_true, hc.rsp, add_add, Nat.reduceAdd, h216, h224, h232,
    Option.some.injEq, exists_eq_left', hc.pScr, hc.pMsg, hc.pLen, UpdArgs]
  exact ⟨hc.regs rfl rfl rfl rfl rfl (by cs_tac), trivial, trivial, zx32 hcount,
    trivial, trivial, by rw [sx32 (by omega)]⟩


theorem prefixArgs_ok {t : State} (hc : Ctx L g mx m₀ t) :
    WP isa (.block prefixArgs) t fun t' => Ctx L g mx m₀ t' ∧ t'.mem = t.mem ∧
      UpdArgs L 0 (L.B + BitVec.ofNat 64 48) 32 t' := by
  have hin := hc.inFr (d := 216) (by omega) (by omega)
  apply WP.of_runBlock
  simp only [prefixArgs, framePtr, scrPtr, shaScratch, fScratch, List.cons_append, List.nil_append,
    runBlock_cons, runStep_some, runBlock_nil, exec, execAlu, readSrc, readSrc32, State.load64,
    State.setReg32, ea_stk, RegUpd.gpr_setReg, RegUpd.rd_setReg, RegUpd.wr_setReg, RegUpd.mem_setReg,
    RegUpd.gpr_arithFlags, RegUpd.mem_arithFlags, RegUpd.rd_arithFlags, RegUpd.wr_arithFlags,
    Option.map_some, Option.bind_some, reduceCtorEq, ite_false, ite_true, hc.rsp, add_add,
    Nat.reduceAdd, hin, Option.some.injEq, exists_eq_left', hc.pScr, UpdArgs]
  exact ⟨hc.regs rfl rfl rfl rfl rfl (by cs_tac), trivial, trivial, rfl,
    by rw [sx32 (by decide : 32 < 2 ^ 31), add_add],
    rfl, by rw [sx32 (by omega)]⟩

def FinArgs (L : Lay) (count : BitVec 64) (t : State) : Prop :=
  t.gpr .rdi = L.scr ∧ t.gpr .rsi = count ∧ t.gpr .rdx = L.B + BitVec.ofNat 64 144 ∧
    t.gpr .rcx = L.scr + BitVec.ofNat 64 192

theorem finalizeArgs_ok {t : State} (hc : Ctx L g mx m₀ t) (prefixLen : Nat)
    (hp : prefixLen < 2 ^ 31) (withMessage : Bool) :
    WP isa (.block (finalizeArgs prefixLen withMessage)) t fun t' => Ctx L g mx m₀ t' ∧
      t'.mem = t.mem ∧ FinArgs L
        (if withMessage then L.len + BitVec.ofNat 64 prefixLen else BitVec.ofNat 64 prefixLen) t' := by
  have hs := hc.inFr (d := 216) (by omega) (by omega)
  have hl := hc.inFr (d := 224) (by omega) (by omega)
  cases withMessage <;> apply WP.of_runBlock <;>
    simp only [finalizeArgs, framePtr, scrPtr, shaScratch, fScratch, fLength, Bool.false_eq_true,
      ite_false, ite_true, List.cons_append, List.nil_append, runBlock_cons, runStep_some, runBlock_nil,
      exec, execAlu, readSrc, readSrc32, State.load64, State.setReg32, ea_stk,
      RegUpd.gpr_setReg, RegUpd.rd_setReg, RegUpd.wr_setReg, RegUpd.mem_setReg,
      RegUpd.gpr_arithFlags, RegUpd.mem_arithFlags, RegUpd.rd_arithFlags, RegUpd.wr_arithFlags,
      Option.map_some, Option.bind_some, reduceCtorEq, ite_false, ite_true, hc.rsp, add_add,
      Nat.reduceAdd, hs, hl, Option.some.injEq, exists_eq_left', hc.pScr, hc.pLen, FinArgs]
  · exact ⟨hc.regs rfl rfl rfl rfl rfl (by cs_tac), trivial, trivial, zx32 (by omega),
      by rw [sx32 (by decide : 128 < 2 ^ 31), add_add],
      by rw [sx32 (by omega)]⟩
  · exact ⟨hc.regs rfl rfl rfl rfl rfl (by cs_tac), trivial, trivial, by rw [sx32 hp],
      by rw [sx32 (by decide : 128 < 2 ^ 31), add_add],
      by rw [sx32 (by omega)]⟩

end VG.Proof.Ed25519.X86_64.SignCached
