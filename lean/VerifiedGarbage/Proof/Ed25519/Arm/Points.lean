import VerifiedGarbage.Impl.Ed25519.Arm.FieldMemory
import VerifiedGarbage.Proof.Ed25519.Arm.FnCall

/-! Exact extended-coordinate operations, by calls of the functions of point
arithmetic, and the slots they preserve. -/

namespace VG.Proof.Ed25519.Arm

open VG VG.Arm VG.Impl.Ed25519.Arm

def fieldDest : FieldOp → Slot
  | .copy o _ | .const o _ | .mul o _ _ | .add o _ _ | .sub o _ _ => o

theorem evalOp_unchanged (op : FieldOp) (e : Env) (i : Slot) (hi : i ≠ fieldDest op) :
    evalOp op e i = e i := by
  cases op <;> exact Function.update_of_ne hi _ _

theorem evalOps_unchanged (ops : List FieldOp) (e : Env) (i : Slot)
    (hi : ∀ op ∈ ops, i ≠ fieldDest op) : evalOps ops e i = e i := by
  induction ops generalizing e with
  | nil => rfl
  | cons op ops ih =>
    change evalOps ops (evalOp op e) i = e i
    rw [ih (evalOp op e) (fun p hp => hi p (List.mem_cons_of_mem _ hp)), evalOp_unchanged op e i (hi op (by simp))]

theorem point_ops_high (ops : List FieldOp) (hops : ∀ op ∈ ops, (fieldDest op).val < 16)
    (e : Env) (i : Slot) (hi : 16 ≤ i.val) : evalOps ops e i = e i := by
  apply evalOps_unchanged
  intro op hop heq
  have h := hops op hop
  have := congrArg Fin.val heq
  omega

theorem pointAdd_high (e : Env) (i : Slot) (hi : 16 ≤ i.val) :
    evalOps pointAddOps e i = e i :=
  point_ops_high _ (by decide) e i hi

theorem pointDouble_high (e : Env) (i : Slot) (hi : 16 ≤ i.val) :
    evalOps pointDoubleOps e i = e i :=
  point_ops_high _ (by decide) e i hi

theorem constPoint_eval (p : Spec.Ed25519.Point) (e : Env) :
    point (evalOps (constPointOps p) e) 0 1 2 3 = p := by cases p; rfl

theorem savePoint_eval (e : Env) :
    point (evalOps savePointOps e) 17 18 19 20 = point e 0 1 2 3 := rfl

theorem restorePoint_eval (e : Env) :
    point (evalOps restorePointOps e) 0 1 2 3 = point e 17 18 19 20 := rfl

theorem copyPointToQ_eval (e : Env) :
    point (evalOps copyPointToQOps e) 4 5 6 7 = point e 0 1 2 3 := rfl

theorem pointDouble_ok {s : State} {base : BitVec 32} (hs : Ctx base s) (hl : AllLim s.mem base)
    (hd : env s.mem base 16 = Spec.Ed25519.d) :
    WP isa Point16.doubleCall s fun t =>
      Keep base s t ∧ AllLim t.mem base ∧ point (env t.mem base) 0 1 2 3 =
        Spec.Ed25519.pointAdd (point (env s.mem base) 0 1 2 3) (point (env s.mem base) 0 1 2 3) ∧
      ∀ i : Slot, 16 ≤ i.val → env t.mem base i = env s.mem base i := by
  refine WP.mono (doubleCall_ok hs hl) fun t ⟨hk, hlt, hv⟩ => ?_
  rw [hv]
  exact ⟨hk, hlt, pointDouble_eval _ hd, pointDouble_high _⟩

theorem pointAdd_ok {s : State} {base : BitVec 32} (hs : Ctx base s) (hl : AllLim s.mem base)
    (hd : env s.mem base 16 = Spec.Ed25519.d) :
    WP isa Point16.addCall s fun t =>
      Keep base s t ∧ AllLim t.mem base ∧ point (env t.mem base) 0 1 2 3 =
        Spec.Ed25519.pointAdd (point (env s.mem base) 0 1 2 3) (point (env s.mem base) 4 5 6 7) ∧
      ∀ i : Slot, 16 ≤ i.val → env t.mem base i = env s.mem base i := by
  refine WP.mono (addCall_ok hs hl) fun t ⟨hk, hlt, hv⟩ => ?_
  rw [hv]
  exact ⟨hk, hlt, pointAdd_eval _ hd, pointAdd_high _⟩

end VG.Proof.Ed25519.Arm
