import VerifiedGarbage.Proof.Framework.PowLit
import Mathlib.NumberTheory.LucasPrimality

/-!
# Pratt certificates

Primality by Lucas's theorem (`lucas_primality`): for a prime `p` of a
certificate tree, a witness `a` of order `p - 1` modulo `p` and the prime
factors of `p - 1` (with multiplicity), each proven prime in turn. The kernel
evaluates the modular powers with `powMod`, a binary exponentiation on `Nat`
(`decide +kernel`).

This imports `Mathlib.NumberTheory.LucasPrimality`; the curves' prime proofs
import it (Ed25519's and Ed448's in the bridges from their specifications to
the Edwards group, which much of the build imports: it is on
`ci/check_lean_speed.py`'s allow-list for that).
-/

namespace VG.Proof.Pratt

/-- `b^e % m` for `e < 2^n`, by binary exponentiation. -/
def powMod (m : Nat) : Nat → Nat → Nat → Nat
  | 0, _, _ => 1
  | n + 1, b, e => if e = 0 then 1 else
      if e % 2 = 0 then powMod m n (b * b % m) (e / 2) else b * powMod m n (b * b % m) (e / 2) % m

theorem powMod_cast (m : Nat) (n b e : Nat) (he : e < 2 ^ n) :
    (powMod m n b e : ZMod m) = (b : ZMod m) ^ e := by
  induction n generalizing b e with
  | zero =>
    have : e = 0 := by simpa using he
    subst this; simp [powMod]
  | succ n ih =>
    by_cases h0 : e = 0
    · subst h0; simp [powMod]
    have he2 : e / 2 < 2 ^ n := by rw [Nat.pow_succ] at he; omega
    have hb : ((b * b % m : Nat) : ZMod m) = (b : ZMod m) ^ 2 := by
      rw [ZMod.natCast_mod, Nat.cast_mul, sq]
    have hsplit : (b : ZMod m) ^ e = ((b : ZMod m) ^ 2) ^ (e / 2) * (b : ZMod m) ^ (e % 2) := by
      rw [← pow_mul, ← pow_add]; congr 1; omega
    by_cases h2 : e % 2 = 0
    · simp only [powMod, h0, h2, ite_false, ite_true]
      rw [ih _ _ he2, hb, hsplit, h2, pow_zero, mul_one]
    · simp only [powMod, h0, h2, ite_false]
      have h1 : e % 2 = 1 := by omega
      rw [ZMod.natCast_mod, Nat.cast_mul, ih _ _ he2, hb, hsplit, h1, pow_one, mul_comm]

theorem prime_of_dvd_prod {q : Nat} (hq : q.Prime) {fs : List Nat} (hfs : ∀ f ∈ fs, f.Prime)
    (h : q ∣ fs.prod) : q ∈ fs := by
  induction fs with
  | nil => exact absurd (Nat.eq_one_of_dvd_one (by simpa using h)) hq.ne_one
  | cons f fs ih =>
    rw [List.prod_cons] at h
    rcases (Nat.Prime.dvd_mul hq).mp h with h | h
    · rw [(Nat.prime_dvd_prime_iff_eq hq (hfs f (by simp))).mp h]; simp
    · exact List.mem_cons_of_mem _ (ih (fun g hg => hfs g (List.mem_cons_of_mem _ hg)) h)

theorem powMod_lt (m n b e : Nat) (hm : 0 < m) : powMod m n b e < m ∨ powMod m n b e = 1 := by
  induction n generalizing b e with
  | zero => right; rfl
  | succ n ih =>
    simp only [powMod]
    split_ifs
    · right; rfl
    · exact ih _ _
    · left; exact Nat.mod_lt _ hm

/-- Lucas's test, with the prime factors of `p - 1` listed (with multiplicity)
and every power computed by `powMod` with `n`-bit exponents. -/
theorem prime_of_cert (p a n : Nat) (fs : List Nat) (hp : 2 ≤ p) (hn : p - 1 < 2 ^ n)
    (hfs : ∀ f ∈ fs, f.Prime) (hprod : fs.prod = p - 1)
    (ha : powMod p n a (p - 1) = 1)
    (hq : ∀ f ∈ fs, powMod p n a ((p - 1) / f) ≠ 1) : p.Prime := by
  have h1 : ((1 : Nat) : ZMod p) = 1 := Nat.cast_one
  refine lucas_primality p (a : ZMod p) ?_ ?_
  · rw [← powMod_cast p n a _ hn, ha, h1]
  · intro q hq' hd
    have hm := prime_of_dvd_prod hq' hfs (hprod ▸ hd)
    rw [← powMod_cast p n a _ (lt_of_le_of_lt (Nat.div_le_self _ _) hn)]
    intro he
    have hlt := powMod_lt p n a ((p - 1) / q) (by omega)
    rcases hlt with hlt | hlt
    · have := (ZMod.natCast_eq_natCast_iff' _ _ p).mp (he.trans h1.symm)
      rw [Nat.mod_eq_of_lt hlt, Nat.mod_eq_of_lt (by omega)] at this
      exact hq q hm this
    · exact hq q hm hlt

end VG.Proof.Pratt
