import VerifiedGarbage.Proof.Sha512.X86.Steps
import VerifiedGarbage.Proof.Framework.Omega
import VerifiedGarbage.Proof.Framework.X86.RegUpd
import VerifiedGarbage.Proof.Framework.X86.Spill
import VerifiedGarbage.Proof.Framework.Contract
import VerifiedGarbage.Spec.Sha512
import VerifiedGarbage.TCB.X86.Target
import VerifiedGarbage.Proof.Sha512.X86.Lit
import VerifiedGarbage.Proof.Framework.Offset
import VerifiedGarbage.Proof.Framework.X86.SseTaint

/-!
# SHA-512 compression function on x86 (32-bit): the whole function
-/

/-!
## SHA-512: the x86 (32-bit) contracts

The contracts the proofs are written against; the artifacts are emitted with
the shared contracts of `Spec/`, which imply these (`Contract.Implies`). The
contracts of the x86 (32-bit) implementations of the compression function and
of the streaming functions (`init`, `update`, `finalize`; see
`VG.Spec.Sha512.Repr`), in terms of `Spec/Sha512.lean`, with the arguments on
the stack (cdecl).
-/

namespace VG.Proof.Sha512

open Spec.Sha512

open VG.X86 in
/-- x86 (32-bit) contract for
`vg_sha512_compress(state: *mut [u64; 8], blocks: *const [u8; 128], n: usize, scratch: *mut [u64; 28])`:
updates the hash value at `state` with the `n` 128-byte blocks at `blocks`.

The code may read the arguments (16 bytes above the return address) and
`blocks` (`128 * n` bytes), and read and write `state` (64 bytes) and
`scratch` (224 bytes, whose contents on exit are unspecified). The writable
buffers may not overlap each other, the blocks, the arguments or the return
address, and nothing may wrap around the end of the (32-bit) address space.
`esp` and the arguments (the pointers and `n`) are public; the hash value
and the blocks are secret. -/
def compressX86 : Contract X86.isa where
  pre s :=
    let state : Region := ⟨(arg s 0).setWidth 64, 64⟩
    let blocks : Region := ⟨(arg s 1).setWidth 64, 128 * (arg s 2).toNat⟩
    let scratch : Region := ⟨(arg s 3).setWidth 64, 224⟩
    let args : Region := ⟨argAddr s 0, 16⟩
    let ret : Region := ⟨(s.gpr .esp).setWidth 64, 4⟩
    s.rd = [blocks, args] ∧ s.wr = [state, scratch] ∧
    state.Disjoint scratch ∧ blocks.Disjoint state ∧ blocks.Disjoint scratch ∧
    args.Disjoint state ∧ args.Disjoint scratch ∧ ret.Disjoint state ∧ ret.Disjoint scratch ∧
    (arg s 0).toNat + 64 ≤ 2 ^ 32 ∧ (arg s 1).toNat + 128 * (arg s 2).toNat ≤ 2 ^ 32 ∧
    (arg s 3).toNat + 224 ≤ 2 ^ 32 ∧ (s.gpr .esp).toNat + 20 ≤ 2 ^ 32
  post s s' :=
    stateAt s'.mem ((arg s 0).setWidth 64) =
      compressBlocks (stateAt s.mem ((arg s 0).setWidth 64)) s.mem ((arg s 1).setWidth 64)
        (arg s 2).toNat
  pub s₁ s₂ :=
    s₁.gpr .esp = s₂.gpr .esp ∧
    arg s₁ 0 = arg s₂ 0 ∧ arg s₁ 1 = arg s₂ 1 ∧ arg s₁ 2 = arg s₂ 2 ∧ arg s₁ 3 = arg s₂ 3

open VG.X86 in
/-- The 64-bit `count` argument of `update`/`finalize`, in argument slots 1
and 2 (cdecl: the low word first). -/
def countX86 (s : X86.State) : BitVec 64 := arg s 2 ++ arg s 1

open VG.X86 in
/-- x86 (32-bit) contract for `vg_<alg>_init(state: *mut [u8; 192])`, where
`iv` is the initial hash value of `<alg>`: makes the streaming state at
`state` represent the empty message, hashed from `iv`.

The code may read the argument (4 bytes above the return address) and write
`state` (192 bytes), which may not overlap the argument or the return
address; nothing may wrap around the end of the (32-bit) address space.
`esp` and the pointer are public. -/
def initX86 (iv : HashValue) : Contract X86.isa where
  pre s :=
    let state : Region := ⟨(arg s 0).setWidth 64, 192⟩
    let args : Region := ⟨argAddr s 0, 4⟩
    let ret : Region := ⟨(s.gpr .esp).setWidth 64, 4⟩
    s.rd = [args] ∧ s.wr = [state] ∧ args.Disjoint state ∧ ret.Disjoint state ∧
    (arg s 0).toNat + 192 ≤ 2 ^ 32 ∧ (s.gpr .esp).toNat + 8 ≤ 2 ^ 32
  post s s' := Repr iv s'.mem ((arg s 0).setWidth 64) []
  pub s₁ s₂ := s₁.gpr .esp = s₂.gpr .esp ∧ arg s₁ 0 = arg s₂ 0

open VG.X86 in
/-- x86 (32-bit) contract for
`vg_sha512_update(state: *mut [u8; 192], count: u64, data: *const u8, len: usize, scratch: *mut [u64; 34])`,
whose arguments are on the stack (cdecl: `state`, the low and high words of
`count`, `data`, `len`, `scratch`): if the streaming state at `state`
represents a message `m` of `count` bytes (modulo 2⁶⁴), hashed from any
initial hash value, then afterwards it represents `m` followed by the `len`
bytes at `data`, from the same one.

The code may read the arguments (24 bytes above the return address) and
`data` (`len` bytes), and read and write `state` (192 bytes) and `scratch`
(272 bytes, whose contents on exit are unspecified). The writable buffers
may not overlap each other, the data or the arguments; none of the buffers
may overlap the return address or the 20 bytes of stack below it, where the
calls of the compression function store their arguments and return address;
nothing may wrap around the end of the (32-bit) address space. `esp`, the
pointers, `count` and `len` are public; the state and the data are secret. -/
def updateX86 : Contract X86.isa where
  pre s :=
    let state : Region := ⟨(arg s 0).setWidth 64, 192⟩
    let data : Region := ⟨(arg s 3).setWidth 64, (arg s 4).toNat⟩
    let scratch : Region := ⟨(arg s 5).setWidth 64, 272⟩
    let args : Region := ⟨argAddr s 0, 24⟩
    let ret : Region := ⟨(s.gpr .esp).setWidth 64, 4⟩
    let stack : Region := ⟨(s.gpr .esp).setWidth 64 - 20, 20⟩
    s.rd = [data, args] ∧ s.wr = [state, scratch] ∧
    state.Disjoint scratch ∧ data.Disjoint state ∧ data.Disjoint scratch ∧
    args.Disjoint state ∧ args.Disjoint scratch ∧
    ret.Disjoint state ∧ ret.Disjoint scratch ∧ stack.Disjoint state ∧ stack.Disjoint scratch ∧
    stack.Disjoint data ∧
    (arg s 0).toNat + 192 ≤ 2 ^ 32 ∧ (arg s 3).toNat + (arg s 4).toNat ≤ 2 ^ 32 ∧
    (arg s 5).toNat + 272 ≤ 2 ^ 32 ∧ 20 ≤ (s.gpr .esp).toNat ∧ (s.gpr .esp).toNat + 28 ≤ 2 ^ 32
  post s s' := ∀ iv m, Repr iv s.mem ((arg s 0).setWidth 64) m → countX86 s = BitVec.ofNat 64 m.length →
    Repr iv s'.mem ((arg s 0).setWidth 64)
      (m ++ bytesAt s.mem ((arg s 3).setWidth 64) (arg s 4).toNat)
  pub s₁ s₂ :=
    s₁.gpr .esp = s₂.gpr .esp ∧ ∀ i < 6, arg s₁ i = arg s₂ i

open VG.X86 in
/-- x86 (32-bit) contract for
`vg_sha512_finalize(state: *mut [u8; 192], count: u64, out: *mut [u8; 64], scratch: *mut [u64; 34])`,
whose arguments are on the stack (cdecl: `state`, the low and high words of
`count`, `out`, `scratch`): if the streaming state at `state` represents a
message `m` of `count` bytes, fewer than 2⁶⁴, hashed from the initial hash
value `iv`, writes the final hash value `H⁽ᴺ⁾` of `m` from `iv` (64 bytes;
`finalHash iv m`) to `out`.

The code may read the arguments (20 bytes above the return address), and
read and write `state` (192 bytes, whose contents on exit are unspecified),
`out` (64 bytes) and `scratch` (272 bytes, whose contents on exit are
unspecified). These may not overlap each other or the arguments; none of
them may overlap the return address or the 20 bytes of stack below it; and
nothing may wrap around the end of the (32-bit) address space. `esp`, the
pointers and `count` are public; the state is secret. -/
def finalizeX86 : Contract X86.isa where
  pre s :=
    let state : Region := ⟨(arg s 0).setWidth 64, 192⟩
    let out : Region := ⟨(arg s 3).setWidth 64, 64⟩
    let scratch : Region := ⟨(arg s 4).setWidth 64, 272⟩
    let args : Region := ⟨argAddr s 0, 20⟩
    let ret : Region := ⟨(s.gpr .esp).setWidth 64, 4⟩
    let stack : Region := ⟨(s.gpr .esp).setWidth 64 - 20, 20⟩
    s.rd = [args] ∧ s.wr = [state, out, scratch] ∧
    state.Disjoint out ∧ state.Disjoint scratch ∧ out.Disjoint scratch ∧
    args.Disjoint state ∧ args.Disjoint out ∧ args.Disjoint scratch ∧
    ret.Disjoint state ∧ ret.Disjoint out ∧ ret.Disjoint scratch ∧
    stack.Disjoint state ∧ stack.Disjoint out ∧ stack.Disjoint scratch ∧
    (arg s 0).toNat + 192 ≤ 2 ^ 32 ∧ (arg s 3).toNat + 64 ≤ 2 ^ 32 ∧
    (arg s 4).toNat + 272 ≤ 2 ^ 32 ∧ 20 ≤ (s.gpr .esp).toNat ∧ (s.gpr .esp).toNat + 24 ≤ 2 ^ 32
  post s s' := ∀ iv m, Repr iv s.mem ((arg s 0).setWidth 64) m → m.length < 2 ^ 64 →
    countX86 s = BitVec.ofNat 64 m.length →
    bytesAt s'.mem ((arg s 3).setWidth 64) 64 = finalHash iv m
  pub s₁ s₂ :=
    s₁.gpr .esp = s₂.gpr .esp ∧ ∀ i < 5, arg s₁ i = arg s₂ i

end VG.Proof.Sha512


namespace VG.Proof.Sha512.X86

open VG VG.X86 VG.Impl.Sha512.X86
open VG.Spec.Sha512 (HashValue Word Block W stateAt blockAt compress compressBlocks parseBlock)
open VG.Proof.Sha256.X86.Stream (contains_addr sub_offset Upd Mupd wp_store wp_addi wp_subi)
open VG.Proof.Sha512.Word64 (readW64 lo_append hi_append)

/-! ## Memory -/

theorem cat44 (b0 b1 b2 b3 b4 b5 b6 b7 : BitVec 8) :
    ((b0 ++ b1 ++ b2 ++ b3 : BitVec 32) ++ (b4 ++ b5 ++ b6 ++ b7 : BitVec 32) : BitVec 64) =
      (b0 ++ b1 ++ b2 ++ b3 ++ b4 ++ b5 ++ b6 ++ b7 : BitVec 64) := by
  simp only [BitVec.append_assoc, BitVec.cast_eq]

theorem add_one' (p : Addr) (a : Nat) : p + BitVec.ofNat 64 a + 1 = p + BitVec.ofNat 64 (a + 1) := by
  rw [BitVec.add_assoc, BitVec.ofNat_add]; rfl

/-- A store, which leaves the registers and the flags alone. -/
theorem wp_storeF {s : State} {is : List Instr} {Q : State → Prop} {m : MemOp} {r : Reg} {a : Addr}
    (ha : s.ea m = a) (hout : InRegions s.wr a 4)
    (k : WP isa (.block is) { s with mem := s.mem.writeW a (s.gpr r) } Q) :
    WP isa (.block (.store m r :: is)) s Q := by
  refine Proof.Sha256.X86.Stream.WP.cons ?_ k
  simp [exec, State.store32, ha, hout]

theorem stateAt_get {st : BitVec 32} (hfit : st.toNat + 64 ≤ 2 ^ 32) (m : Mem) {k : Nat} (hk : k < 8) :
    (stateAt m (st.setWidth 64))[k] = rd64 m st (8 * k) := by
  simp only [stateAt, Vector.getElem_ofFn, rd64]
  rw [readW64, show st.setWidth 64 + BitVec.ofNat 64 (8 * k) + 4 =
      st.setWidth 64 + BitVec.ofNat 64 (8 * k + 4) from Offset.add_ofNat_add_ofNat _ _ 4,
    ← addr_eq (by bdd_omega), ← addr_eq (by bdd_omega)]

theorem stateAt_ext {st : BitVec 32} (hfit : st.toNat + 64 ≤ 2 ^ 32) {m : Mem} {H : HashValue}
    (h : ∀ k (hk : k < 8), rd64 m st (8 * k) = H[k]) : stateAt m (st.setWidth 64) = H := by
  ext k hk
  rw [stateAt_get hfit m hk, h k hk]

/-- The block at `bk`'s words, as `loadW` makes them from its bytes. -/
def Raw (bk : BitVec 32) (M : Block) (m : Mem) : Prop :=
  ∀ j < 16, bswap (m.readW (addr bk (8 * j)) 32) ++ bswap (m.readW (addr bk (8 * j + 4)) 32) = W M j

/-- `loadW` makes the block's words from its bytes. -/
theorem raw_block {bk : BitVec 32} (hfit : bk.toNat + 128 ≤ 2 ^ 32) (m : Mem) :
    Raw bk (blockAt m (bk.setWidth 64)) m := by
  intro j hj
  rw [W_lt _ hj]
  simp only [blockAt, parseBlock]
  rw [addr_eq (by bdd_omega), addr_eq (by bdd_omega), bswap_readW, bswap_readW]
  simp only [add_one', Nat.add_assoc]
  exact cat44 _ _ _ _ _ _ _ _

theorem compressBlocks_succ (H : HashValue) (m : Mem) (p : Addr) (i : Nat) :
    compressBlocks H m p (i + 1) =
      compress (compressBlocks H m p i) (blockAt m (p + BitVec.ofNat 64 (128 * i))) := by
  simp [compressBlocks, List.range_succ, List.foldl_append]

namespace Compress

/-! ## The precondition -/

section
variable (s₀ : State)

abbrev esp₀ : BitVec 32 := s₀.gpr .esp
abbrev st : BitVec 32 := arg s₀ 0
abbrev bp : BitVec 32 := arg s₀ 1
abbrev nb : Nat := (arg s₀ 2).toNat
abbrev scr : BitVec 32 := arg s₀ 3
abbrev stR : Region := ⟨(st s₀).setWidth 64, 64⟩
abbrev blR : Region := ⟨(bp s₀).setWidth 64, 128 * nb s₀⟩
abbrev scrR : Region := ⟨(scr s₀).setWidth 64, 224⟩
abbrev argR : Region := ⟨argAddr s₀ 0, 16⟩
abbrev retR : Region := ⟨(esp₀ s₀).setWidth 64, 4⟩
abbrev H₀ : HashValue := stateAt s₀.mem ((st s₀).setWidth 64)

/-- Where block `i` starts. -/
abbrev blkAddr (i : Nat) : BitVec 32 := bp s₀ + BitVec.ofNat 32 (128 * i)

end

structure Pre (s₀ : State) : Prop where
  rd : s₀.rd = [blR s₀, argR s₀]
  wr : s₀.wr = [stR s₀, scrR s₀]
  st_scr : (stR s₀).Disjoint (scrR s₀)
  blk_st : (blR s₀).Disjoint (stR s₀)
  blk_scr : (blR s₀).Disjoint (scrR s₀)
  arg_st : (argR s₀).Disjoint (stR s₀)
  arg_scr : (argR s₀).Disjoint (scrR s₀)
  ret_st : (retR s₀).Disjoint (stR s₀)
  ret_scr : (retR s₀).Disjoint (scrR s₀)
  st_fits : (st s₀).toNat + 64 ≤ 2 ^ 32
  blk_fits : (bp s₀).toNat + 128 * nb s₀ ≤ 2 ^ 32
  scr_fits : (scr s₀).toNat + 224 ≤ 2 ^ 32
  esp_fits : (esp₀ s₀).toNat + 20 ≤ 2 ^ 32

theorem pre_of (s₀ : State) (h : Proof.Sha512.compressX86.pre s₀) : Pre s₀ := by
  obtain ⟨h1, h2, h3, h4, h5, h6, h7, h8, h9, h10, h11, h12, h13⟩ := h
  exact ⟨h1, h2, h3, h4, h5, h6, h7, h8, h9, h10, h11, h12, h13⟩

namespace Pre
variable {s₀ : State} (h : Pre s₀)
include h

theorem ctx {s : State} (hesi : s.gpr .esi = scr s₀) (hw : s.wr = s₀.wr) : Ctx (scr s₀) s :=
  ⟨hesi, h.scr_fits, by rw [hw, h.wr]; simp⟩

theorem accS {s : State} (hw : s.wr = s₀.wr) : Acc s.wr (st s₀) 64 :=
  Acc.of_mem (by rw [hw, h.wr]; simp) h.st_fits

theorem accV {s : State} (hw : s.wr = s₀.wr) : Acc s.wr (scr s₀) 224 :=
  Acc.of_mem (by rw [hw, h.wr]; simp) h.scr_fits

theorem in_st {s : State} (hw : s.wr = s₀.wr) {d : Nat} (hd : d + 4 ≤ 64) :
    InRegions (s.rd ++ s.wr) (addr (st s₀) d) 4 :=
  mem_rd (h.accS hw d hd)

theorem in_scr {s : State} (hw : s.wr = s₀.wr) {d : Nat} (hd : d + 4 ≤ 224) :
    InRegions (s.rd ++ s.wr) (addr (scr s₀) d) 4 :=
  mem_rd (h.accV hw d hd)

theorem argAddr_eq {d : Nat} (hd : d < 20) :
    addr (esp₀ s₀) d = (esp₀ s₀).setWidth 64 + BitVec.ofNat 64 d :=
  addr_eq (by have := h.esp_fits; omega)

theorem arg_contains {d : Nat} (hd : 4 ≤ d) (hd' : d + 4 ≤ 20) :
    (argR s₀).Contains (addr (esp₀ s₀) d) 4 := by
  show (⟨addr (esp₀ s₀) 4, 16⟩ : Region).Contains _ _
  rw [h.argAddr_eq (by bdd_omega), h.argAddr_eq (by bdd_omega)]
  exact Offset.contains _ hd (by bdd_omega) (by bdd_omega)

theorem in_arg {s : State} (hrd : s.rd = s₀.rd) {d : Nat} (hd : 4 ≤ d)
    (hd' : d + 4 ≤ 20) : InRegions (s.rd ++ s.wr) (addr (esp₀ s₀) d) 4 :=
  ⟨argR s₀, by simp [hrd, h.rd], h.arg_contains hd hd'⟩

/-- An argument slot is inside the argument region. -/
theorem arg_sub {i : Nat} (hi : i < 4) : Region.Sub ⟨argAddr s₀ i, 4⟩ (argR s₀) := by
  show Region.Sub ⟨addr (esp₀ s₀) (4 + 4 * i), 4⟩ ⟨addr (esp₀ s₀) 4, 16⟩
  rw [h.argAddr_eq (by bdd_omega), h.argAddr_eq (by bdd_omega)]
  exact Offset.sub _ (by bdd_omega) (by bdd_omega)

/-- The arguments are unchanged while only the state and the scratch buffer are written. -/
theorem arg_frame {m : Mem} (hf : Frame [stR s₀, scrR s₀] s₀.mem m) {i : Nat} (hi : i < 4) :
    m.readW (addr (esp₀ s₀) (4 + 4 * i)) 32 = arg s₀ i := by
  refine (hf.readW (r := ⟨argAddr s₀ i, 4⟩) (Region.contains_self _ _) ?_ (by decide))
  simp only [List.mem_cons, List.not_mem_nil, or_false, forall_eq_or_imp, forall_eq]
  exact ⟨h.arg_st.sub_left (h.arg_sub hi), h.arg_scr.sub_left (h.arg_sub hi)⟩

theorem blk_toNat {i : Nat} (hi : i < nb s₀) : (blkAddr s₀ i).toNat = (bp s₀).toNat + 128 * i := by
  have := h.blk_fits
  simp only [blkAddr, BitVec.toNat_add, BitVec.toNat_ofNat]
  rw [Nat.mod_eq_of_lt (a := 128 * i) (by bdd_omega), Nat.mod_eq_of_lt (by bdd_omega)]

theorem blk_fit {i : Nat} (hi : i < nb s₀) : (blkAddr s₀ i).toNat + 128 ≤ 2 ^ 32 := by
  have := h.blk_fits; rw [h.blk_toNat hi]; omega

theorem blk_addr {i : Nat} (hi : i < nb s₀) :
    (blkAddr s₀ i).setWidth 64 = (bp s₀).setWidth 64 + BitVec.ofNat 64 (128 * i) :=
  addr_eq (x := bp s₀) (k := 128 * i) (by have := h.blk_fits; omega)

theorem blk_sub {i : Nat} (hi : i < nb s₀) : Region.Sub ⟨(blkAddr s₀ i).setWidth 64, 128⟩ (blR s₀) := by
  have := h.blk_fits
  rw [h.blk_addr hi]; exact sub_offset (by bdd_omega) (by bdd_omega)

theorem blk_rd {i : Nat} (hi : i < nb s₀) {o : Nat} (ho : o + 4 ≤ 128) :
    InRegions (s₀.rd ++ s₀.wr) (addr (blkAddr s₀ i) o) 4 := by
  refine ⟨blR s₀, by simp [h.rd], ?_⟩
  rw [show addr (blkAddr s₀ i) o = addr (bp s₀) (128 * i + o) by
    simp only [addr, blkAddr, BitVec.add_assoc, BitVec.ofNat_add]]
  exact contains_addr (by have : i + 1 ≤ nb s₀ := hi; omega) (by bdd_omega) h.blk_fits

theorem blk_disj {i : Nat} (hi : i < nb s₀) :
    ∀ r ∈ [stR s₀, scrR s₀], Region.Disjoint ⟨(blkAddr s₀ i).setWidth 64, 128⟩ r := by
  simp only [List.mem_cons, List.not_mem_nil, or_false, forall_eq_or_imp, forall_eq]
  exact ⟨h.blk_st.sub_left (h.blk_sub hi), h.blk_scr.sub_left (h.blk_sub hi)⟩

theorem st_work : (stR s₀).Disjoint (workR (scr s₀)) :=
  h.st_scr.sub_right (Region.sub_prefix (by bdd_omega))

/-- The saved registers and the block count (at offsets `200 … 220` of the
scratch buffer) are unchanged while only the working variables and the
message schedule, or the hash value, are written. -/
theorem high_frame {m m' : Mem} (hf : Frame [workR (scr s₀)] m m' ∨ Frame [stR s₀] m m') {d : Nat}
    (hd : 200 ≤ d) (hd' : d + 4 ≤ 224) : m'.readW (addr (scr s₀) d) 32 = m.readW (addr (scr s₀) d) 32 := by
  have hc : (⟨addr (scr s₀) d, 4⟩ : Region).Contains (addr (scr s₀) d) (32 / 8) :=
    Region.contains_self _ _
  have := h.scr_fits
  rcases hf with hf | hf
  · refine hf.readW hc ?_ (by decide)
    simp only [List.mem_singleton, forall_eq]
    rw [addr_eq (by bdd_omega)]
    exact Offset.disjoint_base _ hd (by bdd_omega)
  · refine hf.readW hc ?_ (by decide)
    simp only [List.mem_singleton, forall_eq]
    refine Region.Disjoint.sub_left h.st_scr.symm ?_
    rw [addr_eq (by bdd_omega)]
    exact Offset.sub_base _ (by bdd_omega)
end Pre

/-! ## The loop invariant -/

/-- The callee-saved registers are saved in the scratch buffer. -/
def compressSaved : Spill.Slots := [(.ebx, 200), (.esi, 204), (.edi, 208), (.ebp, 212)]

theorem compressSaved_fits : Spill.Fits 216 compressSaved := by decide

abbrev Saved (s₀ : State) (m : Mem) : Prop := Spill.Saved m (addr (scr s₀)) s₀.gpr compressSaved

/-- What holds between blocks, after `i` of them. -/
structure Common (s₀ : State) (i : Nat) (s : State) : Prop where
  esi : s.gpr .esi = scr s₀
  esp : s.gpr .esp = esp₀ s₀
  rd : s.rd = s₀.rd
  wr : s.wr = s₀.wr
  frame : Frame [stR s₀, scrR s₀] s₀.mem s.mem
  state : stateAt s.mem ((st s₀).setWidth 64) =
    compressBlocks (H₀ s₀) s₀.mem ((bp s₀).setWidth 64) i
  saved : Saved s₀ s.mem

/-- The loop invariant, at the start of block `i`. -/
structure LInv (s₀ : State) (i : Nat) (s : State) : Prop extends Common s₀ i s where
  edi : s.gpr .edi = blkAddr s₀ i
  cnt : s.mem.readW (addr (scr s₀) cntOff) 32 = BitVec.ofNat 32 (nb s₀ - i)

theorem saved_frame {s₀ : State} (hp : Pre s₀) {m m' : Mem} (h : Saved s₀ m)
    (hf : Frame [workR (scr s₀)] m m' ∨ Frame [stR s₀] m m') : Saved s₀ m' :=
  h.of_readW fun p hp' => hp.high_frame hf (by revert p hp'; decide) (by have := compressSaved_fits.1 p hp'; omega)

/-! ## Loading the working variables and the message -/

theorem xr_0_0 : xr 0 0 = .xmm0 := rfl
theorem xr_0_1 : xr 0 1 = .xmm1 := rfl
theorem xr_0_2 : xr 0 2 = .xmm2 := rfl
theorem xr_80_0 : xr 80 0 = .xmm0 := rfl
theorem xr_80_1 : xr 80 1 = .xmm1 := rfl

section
variable {s₀ : State} (hp : Pre s₀)
include hp

theorem in_st8 {s : State} (hw : s.wr = s₀.wr) {d : Nat} (hd : d + 8 ≤ 64) :
    InRegions s.wr (addr (st s₀) d) 8 :=
  ⟨stR s₀, by simp [hw, hp.wr], contains_addr hd (by decide) hp.st_fits⟩

theorem in_scr8 {s : State} (hw : s.wr = s₀.wr) {d : Nat} (hd : d + 8 ≤ 224) :
    InRegions s.wr (addr (scr s₀) d) 8 :=
  ⟨scrR s₀, by simp [hw, hp.wr], contains_addr hd (by decide) hp.scr_fits⟩

theorem loadH_ok (k : Nat) (hk : k < 8) {s : State} (hecx : s.gpr .ecx = st s₀)
    (hesi : s.gpr .esi = scr s₀) (hwr : s.wr = s₀.wr) :
    WP isa (.block (loadH k)) s fun s' => s'.gpr = s.gpr ∧ s'.rd = s.rd ∧ s'.wr = s.wr ∧
      s'.mem = s.mem.writeW (addr (scr s₀) (8 * k)) (s.mem.readW (addr (st s₀) (8 * k)) 64) := by
  have hi := mem_rd (in_st8 hp hwr (d := 8 * k) (by omega))
  have ho := in_scr8 hp hwr (d := 8 * k) (by omega)
  apply WP.of_runBlock
  simp only [loadH, ldq, stq, runBlock_cons, runStep_some, runBlock_nil, exec, isa, State.load64,
    State.store64, ea_at, hecx, hesi, RegUpd.gpr_setXmm, RegUpd.rd_setXmm, RegUpd.wr_setXmm,
    RegUpd.mem_setXmm, RegUpd.xmm_setXmm_self, extractLsb'_qword, qword_append_0, hi, ho, ↓reduceIte,
    Option.map_some]
  exact ⟨_, rfl, rfl, rfl, rfl, rfl⟩

/-- After copying words `0 … n-1` of the hash value to the working variables. -/
structure LdInv (s : State) (n : Nat) (s' : State) : Prop where
  gpr : s'.gpr = s.gpr
  rd : s'.rd = s.rd
  wr : s'.wr = s.wr
  vars : ∀ k < n, s'.mem.readW (addr (scr s₀) (8 * k)) 64 = s.mem.readW (addr (st s₀) (8 * k)) 64
  frame : Frame [workR (scr s₀)] s.mem s'.mem

theorem loadHs_ok {s : State} (hecx : s.gpr .ecx = st s₀) (hesi : s.gpr .esi = scr s₀)
    (hwr : s.wr = s₀.wr) :
    ∀ n ≤ 8, WP isa (.block ((List.range n).flatMap loadH)) s (LdInv (s₀ := s₀) s n) := by
  intro n hn
  have fS := hp.st_fits
  have fV := hp.scr_fits
  induction n with
  | zero => exact WP.block_nil ⟨rfl, rfl, rfl, fun _ h => absurd h (by bdd_omega), Frame.refl _ _⟩
  | succ n ih =>
    rw [List.range_succ, List.flatMap_append, WP.block_append_iff]
    refine WP.mono (ih (by bdd_omega)) fun s₁ h₁ => ?_
    simp only [List.flatMap_cons, List.flatMap_nil, List.append_nil]
    refine WP.mono (loadH_ok hp n (by bdd_omega) (by rw [h₁.gpr, hecx]) (by rw [h₁.gpr, hesi])
      (by rw [h₁.wr, hwr])) fun s₂ ⟨g₂, rd₂, wr₂, m₂⟩ =>
      ⟨by rw [g₂, h₁.gpr], by rw [rd₂, h₁.rd], by rw [wr₂, h₁.wr], fun k hk => ?_, ?_⟩
    · have eS : s₁.mem.readW (addr (st s₀) (8 * n)) 64 = s.mem.readW (addr (st s₀) (8 * n)) 64 :=
        h₁.frame.readW (contains_addr (by bdd_omega) (by decide) fS)
          (fun r hr => by simp at hr; subst hr; exact hp.st_work) (by decide)
      rw [m₂]
      by_cases hkn : k = n
      · subst hkn
        rw [Mem.readW_writeW_self64, eS]
      · rw [readW64_write_ne _ _ (by bdd_omega) (by bdd_omega) (by bdd_omega)]
        exact h₁.vars k (by bdd_omega)
    · rw [m₂]
      exact frame_writeW h₁.frame fV (by bdd_omega) _

/-- After making words `0 … n-1` of the message schedule. -/
structure WInv (s : State) (M : Block) (n : Nat) (s' : State) : Prop where
  gpr : ∀ r, r ≠ .ecx → r ≠ .edx → s'.gpr r = s.gpr r
  rd : s'.rd = s.rd
  wr : s'.wr = s.wr
  win : ∀ j < n, s'.mem.readW (addr (scr s₀) (wOff j)) 64 = W M j
  low : ∀ o, o + 8 ≤ 64 → s'.mem.readW (addr (scr s₀) o) 64 = s.mem.readW (addr (scr s₀) o) 64
  frame : Frame [workR (scr s₀)] s.mem s'.mem

theorem loadWs_ok {s : State} {bk : BitVec 32} {M : Block} (hesi : s.gpr .esi = scr s₀)
    (hwr : s.wr = s₀.wr) (hedi : s.gpr .edi = bk) (fitB : bk.toNat + 128 ≤ 2 ^ 32)
    (disj : Region.Disjoint ⟨bk.setWidth 64, 128⟩ (workR (scr s₀)))
    (hrd : ∀ o, o + 4 ≤ 128 → InRegions (s.rd ++ s.wr) (addr bk o) 4) (hM : Raw bk M s.mem) :
    ∀ n ≤ 16, WP isa (.block ((List.range n).flatMap fun t => loadW (8 * t) (wOff t))) s
      (WInv (s₀ := s₀) s M n) := by
  intro n hn
  have fV := hp.scr_fits
  induction n with
  | zero => exact WP.block_nil ⟨fun _ _ _ => rfl, rfl, rfl, fun _ h => absurd h (by bdd_omega),
      fun _ _ => rfl, Frame.refl _ _⟩
  | succ n ih =>
    rw [List.range_succ, List.flatMap_append, WP.block_append_iff]
    refine WP.mono (ih (by bdd_omega)) fun s₁ h₁ => ?_
    simp only [List.flatMap_cons, List.flatMap_nil, List.append_nil]
    rw [← List.append_nil (loadW _ _)]
    have rdB : ∀ o, o + 4 ≤ 128 → s₁.mem.readW (addr bk o) 32 = s.mem.readW (addr bk o) 32 :=
      fun o ho => h₁.frame.readW (contains_addr ho (by decide) fitB)
        (fun r hr => by simp at hr; subst hr; exact disj) (by decide)
    have wl := wOff_lt n
    refine wp_loadW (N := 224) (by bdd_omega) (by rw [h₁.wr, hwr]; exact hp.accV rfl)
      (by rw [h₁.gpr _ (by decide) (by decide), hesi]) (by rw [h₁.gpr _ (by decide) (by decide), hedi])
      (by rw [h₁.rd, h₁.wr]; exact hrd _ (by bdd_omega)) (by rw [h₁.rd, h₁.wr]; exact hrd _ (by bdd_omega))
      fun s₂ w₂ => WP.block_nil ?_
    rw [rdB _ (by bdd_omega), rdB _ (by bdd_omega), hM n (by bdd_omega)] at w₂
    refine ⟨fun r h1 h2 => ?_, by rw [w₂.rd, h₁.rd], by rw [w₂.wr, h₁.wr], fun j hj => ?_,
      fun o ho => ?_, ?_⟩
    · rw [w₂.gpr r (by simp [Z0, Z1, h1, h2]), h₁.gpr r h1 h2]
    · have := wOff_lt j
      rw [w₂.mem, ← rd64_eq_readW _ (by bdd_omega)]
      by_cases hjn : j = n
      · subst hjn; rw [rd64_write64_self _ _ (by bdd_omega)]
      · rw [rd64_write64_ne _ _ (by bdd_omega) (by bdd_omega) (wOff_sep (by bdd_omega)),
          rd64_eq_readW _ (by bdd_omega)]
        exact h₁.win j (by bdd_omega)
    · rw [w₂.mem, ← rd64_eq_readW _ (by bdd_omega), rd64_write64_ne _ _ (by bdd_omega) (by bdd_omega)
        (.inr (by simp only [wOff]; omega)), rd64_eq_readW _ (by bdd_omega)]
      exact h₁.low o ho
    · rw [w₂.mem]
      exact frame_write64 (N := 200) h₁.frame (by simp) (by bdd_omega) (by omega) _

/-- The block's sixteen words, and the copy of `W₀` after the window. -/
theorem loadWsMir_ok {s : State} {bk : BitVec 32} {M : Block} (hesi : s.gpr .esi = scr s₀)
    (hwr : s.wr = s₀.wr) (hedi : s.gpr .edi = bk) (fitB : bk.toNat + 128 ≤ 2 ^ 32)
    (disj : Region.Disjoint ⟨bk.setWidth 64, 128⟩ (workR (scr s₀)))
    (hrd : ∀ o, o + 4 ≤ 128 → InRegions (s.rd ++ s.wr) (addr bk o) 4) (hM : Raw bk M s.mem) :
    WP isa (.block loadWs) s fun s' =>
      WInv (s₀ := s₀) s M 16 s' ∧ s'.mem.readW (addr (scr s₀) mirOff) 64 = W M 0 := by
  have fV := hp.scr_fits
  rw [loadWs, WP.block_append_iff]
  refine WP.mono (loadWs_ok hp hesi hwr hedi fitB disj hrd hM 16 (Nat.le_refl _)) fun s₁ h₁ => ?_
  rw [← List.append_nil (loadW _ _)]
  have rdB : ∀ o, o + 4 ≤ 128 → s₁.mem.readW (addr bk o) 32 = s.mem.readW (addr bk o) 32 :=
    fun o ho => h₁.frame.readW (contains_addr ho (by decide) fitB)
      (fun r hr => by simp at hr; subst hr; exact disj) (by decide)
  refine wp_loadW (N := 224) (o := mirOff) (by decide) (by rw [h₁.wr, hwr]; exact hp.accV rfl)
    (by rw [h₁.gpr _ (by decide) (by decide), hesi]) (by rw [h₁.gpr _ (by decide) (by decide), hedi])
    (by rw [h₁.rd, h₁.wr]; exact hrd _ (by bdd_omega)) (by rw [h₁.rd, h₁.wr]; exact hrd _ (by bdd_omega))
    fun s₂ w₂ => WP.block_nil ?_
  rw [rdB _ (by bdd_omega), rdB _ (by bdd_omega), show (0 : Nat) + 4 = 8 * 0 + 4 from rfl,
    show (0 : Nat) = 8 * 0 from rfl, hM 0 (by decide)] at w₂
  have mw : ∀ j, wOff j + 8 ≤ mirOff := fun j => by simp only [wOff, mirOff]; omega
  refine ⟨⟨fun r h1 h2 => ?_, by rw [w₂.rd, h₁.rd], by rw [w₂.wr, h₁.wr], fun j hj => ?_,
    fun o ho => ?_, ?_⟩, ?_⟩
  · rw [w₂.gpr r (by simp [Z0, Z1, h1, h2]), h₁.gpr r h1 h2]
  · have := wOff_lt j
    rw [w₂.mem, ← rd64_eq_readW _ (by bdd_omega), rd64_write64_ne _ _ (by simp only [mirOff]; omega)
      (by bdd_omega) (.inr (mw j)), rd64_eq_readW _ (by bdd_omega)]
    exact h₁.win j hj
  · rw [w₂.mem, ← rd64_eq_readW _ (by bdd_omega), rd64_write64_ne _ _ (by simp only [mirOff]; omega)
      (by bdd_omega) (.inr (by simp only [mirOff]; omega)), rd64_eq_readW _ (by bdd_omega)]
    exact h₁.low o ho
  · rw [w₂.mem]
    exact h₁.frame.trans (frame_write64 (N := 200) (Frame.refl _ _) (by simp) (by bdd_omega) (by decide) _)
  · rw [w₂.mem, ← rd64_eq_readW _ (by simp only [mirOff]; omega), rd64_write64_self _ _ (by simp only [mirOff]; omega)]

theorem enter_ok {s : State} {H : HashValue} (hesi : s.gpr .esi = scr s₀) (hwr : s.wr = s₀.wr)
    (hv : ∀ k (hk : k < 8), s.mem.readW (addr (scr s₀) (8 * k)) 64 = H[k]) :
    WP isa (.block enter) s fun s' =>
      qword (s'.xmm (xr 0 0)) 0 = H[0] ∧ qword (s'.xmm (xr 0 1)) 0 = H[4] ∧
      qword (s'.xmm (xr 0 2)) 0 = H[1] ^^^ H[2] ∧ s'.gpr = s.gpr ∧ s'.mem = s.mem ∧ s'.rd = s.rd ∧
      s'.wr = s.wr := by
  have i : ∀ k < 8, InRegions (s.rd ++ s.wr) (addr (s.gpr .esi) (8 * k)) 8 := fun k hk => by
    rw [hesi]; exact mem_rd (in_scr8 hp hwr (by omega))
  have i0 := i 0 (by decide); have i1 := i 1 (by decide); have i2 := i 2 (by decide)
  have i4 := i 4 (by decide)
  have v0 := hv 0 (by decide); have v1 := hv 1 (by decide); have v2 := hv 2 (by decide)
  have v4 := hv 4 (by decide)
  simp only [Nat.mul_zero, Nat.reduceMul] at i0 i1 i2 i4 v0 v1 v2 v4
  rw [← hesi] at v0 v1 v2 v4
  apply WP.of_runBlock
  simp only [enter, xr_0_0, xr_0_1, xr_0_2, ldq, xb, X, runBlock_cons, runStep_some, runBlock_nil, exec,
    XOp.exec, isa, State.load64, ea_at, RegUpd.gpr_setXmm, RegUpd.rd_setXmm, RegUpd.wr_setXmm,
    RegUpd.mem_setXmm, RegUpd.xmm_setXmm, qword_append_0, q_pxor, i0, i1, i2,
    i4, v0, v1, v2, v4, reduceCtorEq, ↓reduceIte, Option.map_some, and_self, Option.some.injEq,
    exists_eq_left']


/-! ## Adding them into the hash value -/

theorem exit_ok {s : State} (hesi : s.gpr .esi = scr s₀) (hwr : s.wr = s₀.wr) :
    WP isa (.block exit) s fun s' => s'.gpr = s.gpr ∧ s'.rd = s.rd ∧ s'.wr = s.wr ∧
      s'.mem = (s.mem.writeW (addr (scr s₀) 0) (qword (s.xmm (xr 80 0)) 0)).writeW (addr (scr s₀) 32)
        (qword (s.xmm (xr 80 1)) 0) := by
  have o0 := in_scr8 hp hwr (d := 0) (by decide)
  have o4 := in_scr8 hp hwr (d := 32) (by decide)
  rw [← hesi] at o0 o4
  apply WP.of_runBlock
  simp only [exit, xr_80_0, xr_80_1, stq, runBlock_cons, runStep_some, runBlock_nil, exec, isa,
    State.store64, ea_at, extractLsb'_qword, o0, o4, ↓reduceIte]
  rw [hesi]
  exact ⟨_, rfl, rfl, rfl, rfl, rfl⟩

theorem addH_ok (k : Nat) (hk : k < 8) {s : State} (hecx : s.gpr .ecx = st s₀)
    (hesi : s.gpr .esi = scr s₀) (hwr : s.wr = s₀.wr) :
    WP isa (.block (addH k)) s fun s' => s'.gpr = s.gpr ∧ s'.rd = s.rd ∧ s'.wr = s.wr ∧
      s'.mem = s.mem.writeW (addr (st s₀) (8 * k))
        (s.mem.readW (addr (scr s₀) (8 * k)) 64 + s.mem.readW (addr (st s₀) (8 * k)) 64) := by
  have hi := mem_rd (in_scr8 hp hwr (d := 8 * k) (by omega))
  have hs := in_st8 hp hwr (d := 8 * k) (by omega)
  have hs' := mem_rd hs
  apply WP.of_runBlock
  simp only [addH, ldq, stq, xb, X, Y, runBlock_cons, runStep_some, runBlock_nil, exec, XOp.exec, isa,
    State.load64, State.store64, ea_at, hecx, hesi, RegUpd.gpr_setXmm, RegUpd.rd_setXmm, RegUpd.wr_setXmm,
    RegUpd.mem_setXmm, RegUpd.xmm_setXmm_self, RegUpd.xmm_setXmm_of_ne, extractLsb'_qword, qword_append_0,
    q_paddq, hi, hs, hs', reduceCtorEq, not_false_eq_true, ↓reduceIte, Option.map_some]
  exact ⟨_, rfl, rfl, rfl, rfl, rfl⟩

structure UInv (s : State) (n : Nat) (s' : State) : Prop where
  gpr : s'.gpr = s.gpr
  rd : s'.rd = s.rd
  wr : s'.wr = s.wr
  done : ∀ k < n, s'.mem.readW (addr (st s₀) (8 * k)) 64 =
    s.mem.readW (addr (scr s₀) (8 * k)) 64 + s.mem.readW (addr (st s₀) (8 * k)) 64
  todo : ∀ k, n ≤ k → k < 8 →
    s'.mem.readW (addr (st s₀) (8 * k)) 64 = s.mem.readW (addr (st s₀) (8 * k)) 64
  frame : Frame [stR s₀] s.mem s'.mem

theorem addHs_ok {s : State} (hecx : s.gpr .ecx = st s₀) (hesi : s.gpr .esi = scr s₀)
    (hwr : s.wr = s₀.wr) :
    ∀ n ≤ 8, WP isa (.block ((List.range n).flatMap addH)) s (UInv (s₀ := s₀) s n) := by
  intro n hn
  have fS := hp.st_fits
  induction n with
  | zero => exact WP.block_nil ⟨rfl, rfl, rfl, fun _ h => absurd h (by bdd_omega),
      fun _ _ _ => rfl, Frame.refl _ _⟩
  | succ n ih =>
    rw [List.range_succ, List.flatMap_append, WP.block_append_iff]
    refine WP.mono (ih (by bdd_omega)) fun s₁ h₁ => ?_
    simp only [List.flatMap_cons, List.flatMap_nil, List.append_nil]
    refine WP.mono (addH_ok hp n (by bdd_omega) (by rw [h₁.gpr, hecx]) (by rw [h₁.gpr, hesi])
      (by rw [h₁.wr, hwr])) fun s₂ ⟨g₂, rd₂, wr₂, m₂⟩ =>
      ⟨by rw [g₂, h₁.gpr], by rw [rd₂, h₁.rd], by rw [wr₂, h₁.wr], fun k hk => ?_, fun k hk hk' => ?_, ?_⟩
    · rw [m₂]
      by_cases hkn : k = n
      · subst hkn
        rw [Mem.readW_writeW_self64, h₁.todo k (by bdd_omega) (by bdd_omega),
          h₁.frame.readW (contains_addr (by bdd_omega) (by decide) hp.scr_fits)
            (fun r hr => by simp at hr; subst hr; exact hp.st_scr.symm) (by decide)]
      · rw [readW64_write_ne _ _ (by bdd_omega) (by bdd_omega) (by bdd_omega)]
        exact h₁.done k (by bdd_omega)
    · rw [m₂, readW64_write_ne _ _ (by bdd_omega) (by bdd_omega) (by bdd_omega)]
      exact h₁.todo k (by bdd_omega) hk'
    · rw [m₂]
      exact h₁.frame.writeW (by simp) _ (contains_addr (by bdd_omega) (by decide) fS)

end

/-! ## One block -/

theorem first_eq : load ++ loadWs ++ enter =
    .mov .ecx (.mem ⟨.esp, 4⟩) :: ((List.range 8).flatMap loadH ++ (loadWs ++ enter)) := rfl

theorem last_eq : exit ++ update ++ advance =
    exit ++ .mov .ecx (.mem ⟨.esp, 4⟩) :: ((List.range 8).flatMap addH ++ advance) := rfl

theorem harg_of {s₀ : State} (hp : Pre s₀) {m : Mem} (hf : Frame [stR s₀, scrR s₀] s₀.mem m) :
    m.readW (addr (esp₀ s₀) 4) 32 = st s₀ :=
  hp.arg_frame hf (i := 0) (by decide)

theorem movArg_ok {s₀ : State} (hp : Pre s₀) {s : State} {d : Reg} (hesp : s.gpr .esp = esp₀ s₀)
    (hrd : s.rd = s₀.rd) {is : List Instr} {Q : State → Prop}
    (k : ∀ s', Upd s s' d (s.mem.readW (addr (esp₀ s₀) 4) 32) → WP isa (.block is) s' Q) :
    WP isa (.block (.mov d (.mem ⟨.esp, 4⟩) :: is)) s Q :=
  wp_movS (readSrc_mem (b := .esp) (d := 4) hesp (hp.in_arg hrd (by bdd_omega) (by bdd_omega))) k

theorem advance_ok {s : State} {Q : State → Prop} {B : BitVec 32} (hesi : s.gpr .esi = B)
    (hA : Acc s.wr B 224)
    (k : ∀ s', s'.gpr .edi = s.gpr .edi + 128 →
      s'.zf = some (s.mem.readW (addr B cntOff) 32 - 1 == 0) →
      (∀ r, r ≠ .edi → r ≠ T → s'.gpr r = s.gpr r) →
      s'.mem = s.mem.writeW (addr B cntOff) (s.mem.readW (addr B cntOff) 32 - 1) → s'.rd = s.rd →
      s'.wr = s.wr → Q s') :
    WP isa (.block advance) s Q := by
  unfold advance
  refine wp_addi fun s₁ u₁ => ?_
  refine wp_movS (readSrc_mem (b := .esi) (d := cntOff) (by rw [u₁.other _ (by decide), hesi])
    (by rw [u₁.rd, u₁.wr]; exact mem_rd (hA _ (by decide)))) fun s₂ u₂ => wp_subi fun s₃ u₃ hz => ?_
  refine wp_storeF (ea_of (by rw [u₃.other _ (by decide), u₂.other _ (by decide), u₁.other _ (by decide),
    hesi]) _) (by rw [u₃.wr, u₂.wr, u₁.wr]; exact hA _ (by decide)) (WP.block_nil ?_)
  refine k _ ?_ ?_ (fun r h1 h2 => ?_) ?_ ?_ ?_
  · show s₃.gpr .edi = _
    rw [u₃.other _ (by decide), u₂.other _ (by decide), u₁.gpr]
  · show s₃.zf = _
    rw [hz, u₂.gpr, u₁.mem]
  · show s₃.gpr r = _
    rw [u₃.other _ h2, u₂.other _ h2, u₁.other _ h1]
  · show s₃.mem.writeW _ (s₃.gpr T) = _
    rw [u₃.gpr, u₃.mem, u₂.gpr, u₂.mem, u₁.mem]
  · show s₃.rd = _
    rw [u₃.rd, u₂.rd, u₁.rd]
  · show s₃.wr = _
    rw [u₃.wr, u₂.wr, u₁.wr]

theorem body_ok {s₀ : State} (hp : Pre s₀) {i : Nat} (hi : i < nb s₀) {s : State}
    (hL : LInv s₀ i s) :
    WP isa body s fun s' =>
      (eval .ne s' = some false ∧ Common s₀ (nb s₀) s') ∨
      (eval .ne s' = some true ∧ i + 1 < nb s₀ ∧ LInv s₀ (i + 1) s') := by
  have fitS := hp.st_fits
  have fitV := hp.scr_fits
  have fitB := hp.blk_fit hi
  set H := stateAt s.mem ((st s₀).setWidth 64) with hH
  set M := blockAt s₀.mem ((blkAddr s₀ i).setWidth 64) with hMdef
  have c₀ : Ctx (scr s₀) s := hp.ctx hL.esi hL.wr
  -- Load the working variables, the message and the registers.
  refine WP.seq ?_
  rw [first_eq]
  refine movArg_ok hp hL.esp hL.rd fun s₀' u₀ => ?_
  rw [harg_of hp hL.frame] at u₀
  have esi₀ : s₀'.gpr .esi = scr s₀ := by rw [u₀.other _ (by decide), hL.esi]
  have wr₀ : s₀'.wr = s₀.wr := by rw [u₀.wr, hL.wr]
  rw [WP.block_append_iff]
  refine WP.mono (loadHs_ok hp u₀.gpr esi₀ wr₀ 8 (Nat.le_refl _)) fun s₁ h₁ => ?_
  rw [WP.block_append_iff]
  have hM : Raw (blkAddr s₀ i) M s₁.mem := fun j hj => by
    have e : ∀ o, o + 4 ≤ 128 → s₁.mem.readW (addr (blkAddr s₀ i) o) 32 =
        s₀.mem.readW (addr (blkAddr s₀ i) o) 32 := fun o ho => by
      rw [h₁.frame.readW (contains_addr ho (by decide) fitB)
          (fun r hr => by
            simp at hr; subst hr
            exact (hp.blk_scr.sub_left (hp.blk_sub hi)).sub_right (Region.sub_prefix (by bdd_omega)))
          (by decide), u₀.mem,
        hL.frame.readW (contains_addr ho (by bdd_omega) fitB) (hp.blk_disj hi) (by decide)]
    rw [e _ (by bdd_omega), e _ (by bdd_omega)]
    exact raw_block fitB s₀.mem j hj
  refine WP.mono (loadWsMir_ok hp (by rw [h₁.gpr, esi₀]) (by rw [h₁.wr, wr₀])
    (by rw [h₁.gpr, u₀.other _ (by decide), hL.edi]) fitB
    ((hp.blk_scr.sub_left (hp.blk_sub hi)).sub_right (Region.sub_prefix (by bdd_omega)))
    (fun o ho => by rw [h₁.rd, h₁.wr, u₀.rd, u₀.wr, hL.rd, hL.wr]; exact hp.blk_rd hi ho) hM) fun s₂ ⟨h₂, mir₂⟩ => ?_
  have esi₂ : s₂.gpr .esi = scr s₀ := by rw [h₂.gpr _ (by decide) (by decide), h₁.gpr, esi₀]
  have wr₂ : s₂.wr = s₀.wr := by rw [h₂.wr, h₁.wr, wr₀]
  have hv : ∀ k (hk : k < 8), s₂.mem.readW (addr (scr s₀) (8 * k)) 64 = H[k] := fun k hk => by
    rw [h₂.low _ (by bdd_omega), h₁.vars k hk, u₀.mem, ← rd64_eq_readW _ (by bdd_omega),
      ← stateAt_get fitS _ hk]
  refine WP.mono (enter_ok hp esi₂ wr₂ hv) fun s₃ ⟨xa, xe, xbc, g₃, m₃, rd₃, wr₃⟩ => ?_
  have f₁ : Frame [workR (scr s₀)] s.mem s₁.mem := by rw [← u₀.mem]; exact h₁.frame
  have hI : RInv (scr s₀) H M s 0 0 s₃ := by
    refine ⟨fun k hk h0 h4 => ?_, by rw [Proof.Sha512.rounds_zero]; exact xa,
      by rw [Proof.Sha512.rounds_zero]; exact xe, by rw [Proof.Sha512.rounds_zero]; exact xbc,
      fun j hj _ => ?_, fun j hj _ hj0 => ?_, fun r h1 h2 h3 => ?_, ?_, ?_, ?_⟩
    · rw [m₃, show vOff 0 k = 8 * k by simp only [vOff]; omega, hv k hk, Proof.Sha512.rounds_zero]
    · rw [m₃]; exact h₂.win j (by omega)
    · rw [m₃, mir₂, show j = 0 by omega]
    · rw [g₃, h₂.gpr r h2 h3, h₁.gpr, u₀.other r h2]
    · rw [rd₃, h₂.rd, h₁.rd, u₀.rd]
    · rw [wr₃, h₂.wr, h₁.wr, u₀.wr]
    · rw [m₃]; exact f₁.trans h₂.frame
  -- The rounds.
  refine WP.seq (WP.mono (rounds_ok c₀ hI 80) fun s₄ h₄ => ?_)
  have c₄ := c₀.of_rinv h₄
  -- Store `a` and `e`, update the hash value, and advance.
  rw [last_eq, WP.block_append_iff]
  refine WP.mono (exit_ok hp c₄.esi (by rw [h₄.wr, hL.wr])) fun s₅ ⟨g₅, rd₅, wr₅, m₅⟩ => ?_
  have hrd₅ : s₅.rd = s₀.rd := by rw [rd₅, h₄.rd, hL.rd]
  have hwr₅ : s₅.wr = s₀.wr := by rw [wr₅, h₄.wr, hL.wr]
  have m₂ : Frame [workR (scr s₀)] s.mem s₅.mem := by
    rw [m₅]; exact frame_writeW (frame_writeW h₄.frame fitV (by decide) _) fitV (by decide) _
  have sw : ∀ r ∈ [workR (scr s₀)], ∃ r' ∈ [stR s₀, scrR s₀], Region.Sub r r' :=
    fun r hr => ⟨scrR s₀, by simp, by simp at hr; subst hr; exact Region.sub_prefix (by bdd_omega)⟩
  have hframe₂ : Frame [stR s₀, scrR s₀] s₀.mem s₅.mem := hL.frame.trans (m₂.sub sw)
  have esi₅ : s₅.gpr .esi = scr s₀ := by rw [g₅, c₄.esi]
  have hvars : ∀ k (hk : k < 8),
      s₅.mem.readW (addr (scr s₀) (8 * k)) 64 = (Spec.Sha512.rounds H M 80)[k] := fun k hk => by
    rw [m₅]
    by_cases h4 : k = 4
    · subst h4; rw [Mem.readW_writeW_self64, h₄.xe]
    rw [readW64_write_ne _ _ (by bdd_omega) (by bdd_omega) (by bdd_omega)]
    by_cases h0 : k = 0
    · subst h0; rw [Nat.mul_zero, Mem.readW_writeW_self64, h₄.xa]
    rw [readW64_write_ne _ _ (by bdd_omega) (by bdd_omega) (by bdd_omega),
      show 8 * k = vOff 80 k by simp only [vOff]; omega]
    exact h₄.vars k hk h0 h4
  refine movArg_ok hp (by rw [g₅, h₄.gpr _ (by decide) (by decide) (by decide), hL.esp]) hrd₅
    fun s₆ u₆ => ?_
  rw [harg_of hp hframe₂] at u₆
  rw [WP.block_append_iff]
  refine WP.mono (addHs_ok hp u₆.gpr (by rw [u₆.other _ (by decide), esi₅]) (by rw [u₆.wr, hwr₅]) 8
    (Nat.le_refl _)) fun s₇ h₇ => ?_
  have esi₇ : s₇.gpr .esi = scr s₀ := by rw [h₇.gpr, u₆.other _ (by decide), esi₅]
  refine advance_ok esi₇ (hp.accV (by rw [h₇.wr, u₆.wr, hwr₅])) fun s₈ edi₈ z₈ g₈ m₈ rd₈ wr₈ => ?_
  -- Registers
  have g : ∀ r, r ≠ .eax → r ≠ .ecx → r ≠ .edx → r ≠ .edi → s₈.gpr r = s.gpr r :=
    fun r h1 h2 h3 h4 => by
      rw [g₈ r h4 h1, h₇.gpr, u₆.other r h2, g₅, h₄.gpr r h1 h2 h3]
  have hrd : s₈.rd = s₀.rd := by rw [rd₈, h₇.rd, u₆.rd, hrd₅]
  have hwr : s₈.wr = s₀.wr := by rw [wr₈, h₇.wr, u₆.wr, hwr₅]
  -- Memory
  have hcnt₇ : s₇.mem.readW (addr (scr s₀) cntOff) 32 = BitVec.ofNat 32 (nb s₀ - i) := by
    rw [hp.high_frame (.inr h₇.frame) (by decide) (by decide), u₆.mem,
      hp.high_frame (.inl m₂) (by decide) (by decide), hL.cnt]
  have hframe : Frame [stR s₀, scrR s₀] s₀.mem s₈.mem := by
    rw [m₈]
    refine ((hframe₂.trans ?_).trans (h₇.frame.mono (by simp))).writeW (r := scrR s₀) (by simp) _
      (contains_addr (by decide) (by bdd_omega) fitV)
    rw [u₆.mem]; exact Frame.refl _ _
  have hstate : stateAt s₈.mem ((st s₀).setWidth 64) = compress H M := by
    have e8 : ∀ k < 8, rd64 s₈.mem (st s₀) (8 * k) = rd64 s₇.mem (st s₀) (8 * k) := fun k hk => by
      rw [m₈]
      simp only [rd64]
      rw [Mem.readW_writeW_sep (hp.st_scr.sep (contains_addr (by bdd_omega) (by bdd_omega) fitS)
          (contains_addr (by decide) (by bdd_omega) fitV)) (by decide),
        Mem.readW_writeW_sep (hp.st_scr.sep (contains_addr (by bdd_omega) (by bdd_omega) fitS)
          (contains_addr (by decide) (by bdd_omega) fitV)) (by decide)]
    refine stateAt_ext fitS fun k hk => ?_
    rw [e8 k hk, rd64_eq_readW _ (by bdd_omega), h₇.done k hk, u₆.mem, hvars k hk,
      m₂.readW (contains_addr (by bdd_omega) (by decide) fitS)
        (fun r hr => by simp at hr; subst hr; exact hp.st_work) (by decide),
      ← rd64_eq_readW _ (by bdd_omega), ← stateAt_get fitS _ hk]
    simp only [Spec.Sha512.compress, Vector.getElem_zipWith]
    rfl
  have hsaved : Saved s₀ s₈.mem := by
    have s₇' : Saved s₀ s₇.mem := by
      refine saved_frame hp ?_ (.inr h₇.frame)
      rw [u₆.mem]
      exact saved_frame hp hL.saved (.inl m₂)
    have e : ∀ d, 200 ≤ d → d + 4 ≤ 216 →
        s₈.mem.readW (addr (scr s₀) d) 32 = s₇.mem.readW (addr (scr s₀) d) 32 := fun d h1 h2 => by
      rw [m₈]
      exact Mem.readW_writeW_sep (Proof.Sha256.X86.Stream.addr_sep (by bdd_omega) (by simp [cntOff]; omega)
        (by simp [cntOff]; omega)) (by decide)
    exact s₇'.of_readW fun p h => e _ (by revert p h; decide) (compressSaved_fits.1 p h)
  have hnb : nb s₀ < 2 ^ 32 := (arg s₀ 2).isLt
  have hc1 : s₇.mem.readW (addr (scr s₀) cntOff) 32 - 1 = BitVec.ofNat 32 (nb s₀ - (i + 1)) := by
    rw [hcnt₇, show (1 : BitVec 32) = BitVec.ofNat 32 1 from rfl, Offset.ofNat_sub_ofNat (by bdd_omega),
      Nat.sub_sub]
  have hcommon : Common s₀ (i + 1) s₈ := by
    refine ⟨by rw [g _ (by decide) (by decide) (by decide) (by decide), hL.esi],
      by rw [g _ (by decide) (by decide) (by decide) (by decide), hL.esp], hrd, hwr, hframe, ?_, hsaved⟩
    rw [hstate, compressBlocks_succ, ← hL.state, hMdef, hp.blk_addr hi]
  have hev : eval .ne s₈ = some (!(BitVec.ofNat 32 (nb s₀ - (i + 1)) == 0)) := by
    rw [Proof.Sha256.X86.Stream.eval_ne, z₈, hc1]; rfl
  by_cases hlast : i + 1 = nb s₀
  · left
    refine ⟨by rw [hev, hlast]; simp, hlast ▸ hcommon⟩
  · right
    have hne : nb s₀ - (i + 1) ≠ 0 := by bdd_omega
    have h0 : BitVec.ofNat 32 (nb s₀ - (i + 1)) ≠ 0 := by
      intro h
      have h' := congrArg BitVec.toNat h
      rw [BitVec.toNat_ofNat, Nat.mod_eq_of_lt (by bdd_omega)] at h'
      exact hne h'
    refine ⟨by rw [hev]; simpa using h0, by bdd_omega, { hcommon with edi := ?_, cnt := ?_ }⟩
    · rw [edi₈, h₇.gpr, u₆.other _ (by decide), g₅, h₄.gpr _ (by decide) (by decide) (by decide), hL.edi]
      simp only [blkAddr]
      rw [BitVec.add_assoc, show (128 : BitVec _) = BitVec.ofNat _ 128 from rfl, BitVec.ofNat_add_ofNat]
      rfl
    · rw [m₈, Mem.readW_writeW_self32, hc1]

/-! ## Prologue and epilogue -/

theorem prologue_eq : prologue = .mov .eax (.mem ⟨.esp, 16⟩) :: (Spill.saveCode .eax compressSaved ++
    ([.mov .esi (.reg .eax), .mov .edi (.mem ⟨.esp, 8⟩), .mov .eax (.mem ⟨.esp, 12⟩),
      .store ⟨.esi, 216⟩ .eax, .alu .test .eax (.reg .eax)] : List Instr)) := rfl

theorem epilogue_eq :
    epilogue = Spill.restoreCode .esi ([(.ebx, 200), (.edi, 208), (.ebp, 212)] ++ [(.esi, 204)]) ++ [] :=
  rfl

/-- Reading an argument after writing the scratch buffer. -/
theorem readW_writeW_scr_arg {s₀ : State} (hp : Pre s₀) (m : Mem) (v : BitVec 32) {d e : Nat}
    (hd : d + 4 ≤ 224) (he : 4 ≤ e) (he' : e + 4 ≤ 20) :
    (m.writeW (addr (scr s₀) d) v).readW (addr (esp₀ s₀) e) 32 = m.readW (addr (esp₀ s₀) e) 32 :=
  Mem.readW_writeW_sep (hp.arg_scr.sep (hp.arg_contains he he')
    (contains_addr hd (by bdd_omega) hp.scr_fits)) (by decide)

theorem readW_writeW_scr {s₀ : State} (hp : Pre s₀) (m : Mem) (v : BitVec 32) {d e : Nat}
    (hd : d + 4 ≤ 224) (he : e + 4 ≤ 224) (h : d + 4 ≤ e ∨ e + 4 ≤ d) :
    (m.writeW (addr (scr s₀) e) v).readW (addr (scr s₀) d) 32 = m.readW (addr (scr s₀) d) 32 :=
  Proof.Sha256.X86.Stream.readW_writeW_addr m v (by have := hp.scr_fits; omega)
    (by have := hp.scr_fits; omega) h

/-- The memory after the prologue. -/
def saveMem (s₀ : State) : Mem :=
  (Spill.saveMem s₀.mem (addr (scr s₀)) s₀.gpr compressSaved).writeW (addr (scr s₀) 216) (arg s₀ 2)

theorem compressSaved_contains {s₀ : State} (hp : Pre s₀) : ∀ p ∈ compressSaved, (scrR s₀).Contains (addr (scr s₀) p.2) 4 :=
  fun p h => contains_addr (by have := compressSaved_fits.1 p h; omega) (by bdd_omega) hp.scr_fits

theorem save_ok {s₀ : State} (hp : Pre s₀) :
    WP isa (.block prologue) s₀ fun s₁ =>
      s₁.gpr .esi = scr s₀ ∧ s₁.gpr .edi = bp s₀ ∧ s₁.gpr .esp = esp₀ s₀ ∧ s₁.rd = s₀.rd ∧
      s₁.wr = s₀.wr ∧ s₁.mem = saveMem s₀ ∧ s₁.zf = some (arg s₀ 2 &&& arg s₀ 2 == 0) := by
  have harg : ∀ d, 4 ≤ d → d + 4 ≤ 20 → (Spill.saveMem s₀.mem (addr (scr s₀)) s₀.gpr compressSaved).readW
      (addr (esp₀ s₀) d) 32 = s₀.mem.readW (addr (esp₀ s₀) d) 32 := fun d hd hd' =>
    Spill.saveMem_readW_of_sep _ _ (by decide) _ _ fun p h =>
      hp.arg_scr.sep (hp.arg_contains hd hd') (compressSaved_contains hp p h)
  have hout : ∀ {s : State}, s.wr = s₀.wr → ∀ d, d + 4 ≤ 224 → InRegions s.wr (addr (scr s₀) d) 4 :=
    fun hw => hp.accV hw
  rw [prologue_eq]
  refine Wp.wp_ldm rfl (hp.in_arg (s := s₀) rfl (d := 16) (by bdd_omega) (by bdd_omega)) fun s₁ u₁ => ?_
  refine Spill.save_ok compressSaved (fun p h => by
    rw [u₁.gpr]; exact hout u₁.wr _ (by have := compressSaved_fits.1 p h; omega)) fun s₂ u₂ => ?_
  have hm : s₂.mem = Spill.saveMem s₀.mem (addr (scr s₀)) s₀.gpr compressSaved := by
    rw [u₂.mem, u₁.gpr, u₁.mem]
    exact Spill.saveMem_congr _ _ (fun _ _ => rfl) fun p h => u₁.other _ (by revert p h; decide)
  have hesp : s₂.gpr .esp = esp₀ s₀ := by rw [u₂.gpr, u₁.other _ (by decide)]
  have hrd : s₂.rd = s₀.rd := by rw [u₂.rd, u₁.rd]
  have hwr : s₂.wr = s₀.wr := by rw [u₂.wr, u₁.wr]
  refine Wp.wp_mov fun s₃ u₃ => ?_
  have hesi : s₃.gpr .esi = scr s₀ := by rw [u₃.gpr, u₂.gpr, u₁.gpr]; rfl
  refine Wp.wp_ldm (by rw [u₃.other _ (by decide), hesp])
    (hp.in_arg (by rw [u₃.rd, hrd]) (d := 8) (by bdd_omega) (by bdd_omega)) fun s₄ u₄ => ?_
  refine Wp.wp_ldm (by rw [u₄.other _ (by decide), u₃.other _ (by decide), hesp])
    (hp.in_arg (by rw [u₄.rd, u₃.rd, hrd]) (d := 12) (by bdd_omega) (by bdd_omega)) fun s₅ u₅ => ?_
  have h12 : s₅.gpr .eax = arg s₀ 2 := by
    rw [u₅.gpr, u₄.mem, u₃.mem, hm, harg _ (by bdd_omega) (by bdd_omega)]; rfl
  refine Wp.wp_stm (by rw [u₅.other _ (by decide), u₄.other _ (by decide), hesi])
    (hout (by rw [u₅.wr, u₄.wr, u₃.wr, hwr]) _ (by bdd_omega)) fun s₆ u₆ =>
      Wp.wp_test fun s₇ f₇ z₇ => WP.block_nil ⟨?_, ?_, ?_, ?_, ?_, ?_, ?_⟩
  · rw [f₇.gpr, u₆.gpr, u₅.other _ (by decide), u₄.other _ (by decide), hesi]
  · rw [f₇.gpr, u₆.gpr, u₅.other _ (by decide), u₄.gpr, u₃.mem, hm, harg _ (by bdd_omega) (by bdd_omega)]; rfl
  · rw [f₇.gpr, u₆.gpr, u₅.other _ (by decide), u₄.other _ (by decide), u₃.other _ (by decide), hesp]
  · rw [f₇.rd, u₆.rd, u₅.rd, u₄.rd, u₃.rd, hrd]
  · rw [f₇.wr, u₆.wr, u₅.wr, u₄.wr, u₃.wr, hwr]
  · rw [f₇.mem, u₆.mem, u₅.mem, u₄.mem, u₃.mem, hm, h12]; rfl
  · rw [z₇, u₆.gpr, h12]

theorem saveMem_saved {s₀ : State} (hp : Pre s₀) : Saved s₀ (saveMem s₀) :=
  (Spill.saveMem_saved_addr _ _ compressSaved_fits (by have := hp.scr_fits; omega)).of_readW fun p h => by
    have := compressSaved_fits.1 p h
    rw [saveMem, readW_writeW_scr hp _ _ (by bdd_omega) (by bdd_omega) (by bdd_omega)]

theorem saveMem_cnt {s₀ : State} :
    (saveMem s₀).readW (addr (scr s₀) cntOff) 32 = arg s₀ 2 := by
  simp only [saveMem, cntOff, Mem.readW_writeW_self32]

theorem saveMem_frame {s₀ : State} (hp : Pre s₀) : Frame [scrR s₀] s₀.mem (saveMem s₀) :=
  (Spill.saveMem_frame List.mem_cons_self _ _ _ _ (compressSaved_contains hp)).writeW List.mem_cons_self _
    (contains_addr (d := 216) (by bdd_omega) (by bdd_omega) hp.scr_fits)

theorem common_zero {s₀ : State} (hp : Pre s₀) {s₁ : State} (hesi : s₁.gpr .esi = scr s₀)
    (hesp : s₁.gpr .esp = esp₀ s₀) (hrd : s₁.rd = s₀.rd) (hwr : s₁.wr = s₀.wr)
    (hm : s₁.mem = saveMem s₀) : Common s₀ 0 s₁ := by
  refine ⟨hesi, hesp, hrd, hwr, ?_, ?_, by rw [hm]; exact saveMem_saved hp⟩
  · rw [hm]; exact (saveMem_frame hp).mono (by simp)
  · rw [hm]
    have hd : ∀ r ∈ [scrR s₀], Region.Disjoint (stR s₀) r := fun r hr => by
      simp at hr; subst hr; exact hp.st_scr
    refine stateAt_ext hp.st_fits fun k hk => ?_
    rw [rd64_frame (saveMem_frame hp) hd hp.st_fits (by bdd_omega), ← stateAt_get hp.st_fits _ hk]
    simp [compressBlocks]

theorem restore_ok {s₀ : State} (hp : Pre s₀) {s : State} (hc : Common s₀ (nb s₀) s) :
    WP isa (.block epilogue) s fun s' =>
      (∀ r ∈ calleeSaved, s'.gpr r = s₀.gpr r) ∧ s'.mem = s.mem := by
  rw [epilogue_eq]
  refine Spill.restoreBase_ok _ (by decide)
    (fun p h => by rw [hc.esi]; exact hp.in_scr hc.wr (by have := compressSaved_fits.1 p (by revert p h; decide); omega))
    (by rw [hc.esi]; exact hc.saved.sub (by decide)) fun s' u =>
      WP.block_nil ⟨u.abi (by decide) (by decide) hc.esp, u.mem⟩

/-! ## The whole function -/

theorem correct {s₀ : State} (hp : Pre s₀) :
    WP isa Impl.Sha512.X86.compress s₀ fun s' => abiPreserved s₀ s' ∧ Proof.Sha512.compressX86.post s₀ s' := by
  refine WP.seq (WP.mono (save_ok hp) fun s₁ ⟨hesi, hedi, hesp, hrd, hwr, hm, hz⟩ => ?_)
  refine WP.seq (WP.mono (Q := Common s₀ (nb s₀)) ?_ fun s₂ hc =>
    WP.mono (restore_ok hp hc) fun s' ⟨hr, hm'⟩ => ⟨⟨hr, ?_⟩, ?_⟩)
  rotate_left
  · rw [hm']
    refine hc.frame.readW (Region.contains_self _ _) ?_ (by decide)
    simpa using ⟨hp.ret_st, hp.ret_scr⟩
  · show stateAt s'.mem _ = _
    rw [hm']; exact hc.state
  have hc₀ := common_zero hp hesi hesp hrd hwr hm
  refine WP.ite (arg s₀ 2 &&& arg s₀ 2 == 0) (by simp [eval, hz]) (fun h => ?_) (fun h => ?_)
  · have h0 : nb s₀ = 0 := by simp only [BitVec.and_self, beq_iff_eq] at h; simp [nb, h]
    exact WP.block_nil (M := isa) (h0 ▸ hc₀)
  · have hpos : 0 < nb s₀ := by
      simp only [BitVec.and_self, beq_eq_false_iff_ne, ne_eq] at h
      exact Nat.pos_of_ne_zero fun h' => h (BitVec.eq_of_toNat_eq (by simpa using h'))
    let Inv : Nat → State → Prop := fun m s => ∃ i, m = nb s₀ - i ∧ i < nb s₀ ∧ LInv s₀ i s
    have hstep : ∀ m s, Inv m s → WP isa body s (fun s' =>
        (eval .ne s' = some false ∧ Common s₀ (nb s₀) s') ∨
        (eval .ne s' = some true ∧ ∃ m' < m, Inv m' s')) := by
      rintro m s ⟨i, rfl, hi, hL⟩
      refine WP.mono (body_ok hp hi hL) fun s' h => ?_
      rcases h with ⟨he, hc⟩ | ⟨he, hi', hL'⟩
      · exact .inl ⟨he, hc⟩
      · exact .inr ⟨he, nb s₀ - (i + 1), by bdd_omega, i + 1, rfl, hi', hL'⟩
    have hL₀ : LInv s₀ 0 s₁ :=
      { hc₀ with
        edi := by rw [hedi]; simp [blkAddr]
        cnt := by rw [hm, saveMem_cnt]; simp [nb] }
    exact WP.loop (M := isa) Inv hstep (nb s₀) s₁ ⟨0, rfl, hpos, hL₀⟩

/-- Memory holding the arguments `0x1000, 0x2000, 0, 0x3000` at `0x4004`. -/
def satMem : Mem := fun a =>
  if a = 0x4005 then 0x10 else if a = 0x4009 then 0x20 else if a = 0x4011 then 0x30 else 0

/-- A state satisfying the precondition (with no blocks). -/
def satState : State where
  gpr r := match r with
    | .esp => 0x4000 | _ => 0
  cf := none
  zf := none
  sf := none
  of := none
  mem := satMem
  rd := [⟨0x2000, 0⟩, ⟨0x4004, 16⟩]
  wr := [⟨0x1000, 64⟩, ⟨0x3000, 224⟩]

theorem sat_pre : Proof.Sha512.compressX86.pre satState := by
  have a0 : arg satState 0 = 0x1000 := by decide
  have a1 : arg satState 1 = 0x2000 := by decide
  have a2 : arg satState 2 = 0 := by decide
  have a3 : arg satState 3 = 0x3000 := by decide
  have e : argAddr satState 0 = 0x4004 := by decide
  simp only [Proof.Sha512.compressX86, a0, a1, a2, a3, e]
  refine ⟨by decide, rfl, ?_, ?_, ?_, ?_, ?_, ?_, ?_, by decide, by decide, by decide, by decide⟩ <;>
  exact Region.disjoint_of_sep (by decide)

/-- The taint analysis starts with the stack arguments public, and the words
holding `state` and `scratch` known to be the base addresses of the writable
regions. -/
def τ₀ : VG.X86.Taint.T :=
  { regs := .ofList [.esp], flags := false, lens := [64, 224], argLen := 20, argBases := [(4, 0), (16, 1)] }

theorem wf₀ {s : State} (hp : Pre s) : VG.X86.Taint.Wf τ₀ s := by
  have hst := hp.st_fits; have hsc := hp.scr_fits; have hs := hp.esp_fits
  refine VG.X86.Taint.Wf.entry rfl rfl ⟨fun _ => ⟨by simp [hp.wr, τ₀], by simpa [hp.wr] using hp.st_scr, ?_⟩,
    fun _ h => (List.not_mem_nil h).elim, fun _ h => (List.not_mem_nil h).elim,
    fun _ => ⟨hs, ?_⟩, ?_⟩
  · simp only [hp.wr, List.mem_cons, List.not_mem_nil, or_false]
    rintro r (rfl | rfl) <;> simp only [BitVec.toNat_setWidth] <;> omega
  · simp only [hp.wr, List.mem_cons, List.not_mem_nil, or_false]
    rintro r (rfl | rfl)
    · exact VG.X86.Taint.frame_disjoint (n := 16) (by bdd_omega) hp.ret_st hp.arg_st
    · exact VG.X86.Taint.frame_disjoint (n := 16) (by bdd_omega) hp.ret_scr hp.arg_scr
  · intro p hp'
    simp only [τ₀, List.mem_cons, List.not_mem_nil, or_false] at hp'
    rcases hp' with rfl | rfl <;> refine ⟨by decide, ?_⟩ <;>
      simp [VG.X86.Taint.region, hp.wr, addr, arg, argAddr]

theorem agree₀ {s₁ s₂ : State} (h₁ : Proof.Sha512.compressX86.pre s₁)
    (h₂ : Proof.Sha512.compressX86.pre s₂) (hpub : Proof.Sha512.compressX86.pub s₁ s₂) :
    VG.X86.Taint.Agree τ₀ s₁ s₂ := by
  obtain ⟨hesp, a0, a1, a2, a3⟩ := hpub
  have hp₁ := pre_of _ h₁; have hp₂ := pre_of _ h₂
  refine ⟨⟨fun r hr => ?_, fun h => nomatch h⟩, fun _ => ?_, wf₀ hp₁, wf₀ hp₂,
    fun _ h => (List.not_mem_nil h).elim, fun _ h => (List.not_mem_nil h).elim, fun _ => hesp,
    fun k h4 hk => ?_⟩
  · simp only [τ₀, RegSet.mem_ofList, List.mem_cons, List.not_mem_nil, or_false] at hr
    subst hr; exact hesp
  · rw [hp₁.wr, hp₂.wr]; simp only [stR, scrR, st, scr, a0, a3]
  · simp only [τ₀] at hk
    rw [show VG.X86.Taint.depth τ₀.stk = 0 from rfl, Nat.zero_add]
    rw [VG.X86.Taint.argByte_eq hp₁.esp_fits h4 hk, VG.X86.Taint.argByte_eq hp₂.esp_fits h4 hk,
      Mem.readW_byte s₁.mem _ (Nat.mod_lt _ (by bdd_omega)), Mem.readW_byte s₂.mem _ (Nat.mod_lt _ (by bdd_omega))]
    have : (k - 4) / 4 = 0 ∨ (k - 4) / 4 = 1 ∨ (k - 4) / 4 = 2 ∨ (k - 4) / 4 = 3 := by bdd_omega
    rcases this with h | h | h | h <;> rw [h]
    · exact congrArg _ a0
    · exact congrArg _ a1
    · exact congrArg _ a2
    · exact congrArg _ a3

theorem compress_verified :
    Verified X86.target Impl.Sha512.X86.compress Proof.Sha512.compressX86 :=
  ⟨fun s hs => correct (pre_of s hs),
    VG.Taint.constantTime (A := sseTaint) τ₀ (fun _ _ h₁ h₂ hpub => agree₀ h₁ h₂ hpub) (by taint_decide),
    ⟨satState, sat_pre⟩⟩

end Compress

end VG.Proof.Sha512.X86
