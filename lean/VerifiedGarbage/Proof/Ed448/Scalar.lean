import VerifiedGarbage.Spec.Ed448.Contract
import VerifiedGarbage.Proof.X25519.Bytes
import VerifiedGarbage.Proof.Framework.Omega

/-!
# Ed448 scalar arithmetic: the numbers

`L = 2^446 - c` with `c < 2^224`. Folding a word into a remainder below `L`
(`fold_nat`) leaves a value below `2L` and congruent to it; a conditional
subtraction (`csub_nat`) then leaves the remainder. The bytes of the
specification are X25519's little-endian numbers (`decodeLE_eq`).
-/

namespace VG.Proof.Ed448

open VG VG.Spec.Ed448

/-- `c = 2^446 - L`. -/
def cL : Nat := 13818066809895115352007386748515426880336692474882178609894547503885

theorem L_add : L + cL = 2 ^ 446 := by decide +kernel
theorem cL_lt : cL < 2 ^ 224 := by decide +kernel
theorem L_pos : 0 < L := by decide +kernel
theorem L_lt : L < 2 ^ 446 := by decide +kernel
theorem K_eq : 2 ^ 448 - L = 3 * 2 ^ 446 + cL := by decide +kernel

theorem L_lit : L = 181709681073901722637330951972001133588410340171829515070372549795146003961539585716195755291692375963310293709091662304773755859649779 := by
  decide +kernel

/-- One word folded in, on words (exponents above 256 stay unevaluated, so
the numbers are written with nested factors `2^64`): the remainder
`r = (r₀, …, r₆) < L` and the next word `w` give `v = w + 2^64 r =
h 2^446 + l`, with `l = (w, r₀, …, r₄, r₅ mod 2^62)` and
`h = r₅ / 2^62 + 4 r₆`; then `l + h c` is below `2L` and congruent to `v`
modulo `L`. -/
theorem fold_words (w r0 r1 r2 r3 r4 r5 r6 : Nat) (hw : w < 2 ^ 64) (h0 : r0 < 2 ^ 64)
    (h1 : r1 < 2 ^ 64) (h2 : r2 < 2 ^ 64) (h3 : r3 < 2 ^ 64) (h4 : r4 < 2 ^ 64)
    (h5 : r5 < 2 ^ 64) (h6 : r6 < 2 ^ 64)
    (hr : r0 + 2 ^ 64 * (r1 + 2 ^ 64 * (r2 + 2 ^ 64 * (r3 + 2 ^ 64 * (r4 + 2 ^ 64 *
      (r5 + 2 ^ 64 * r6))))) < L) :
    let l := w + 2 ^ 64 * (r0 + 2 ^ 64 * (r1 + 2 ^ 64 * (r2 + 2 ^ 64 * (r3 + 2 ^ 64 *
      (r4 + 2 ^ 64 * (r5 % 2 ^ 62))))))
    let h := r5 / 2 ^ 62 + 4 * r6
    r6 < 2 ^ 62 ∧ h < 2 ^ 64 ∧ l + h * cL < 2 * L ∧
      (l + h * cL) % L = (w + 2 ^ 64 * (r0 + 2 ^ 64 * (r1 + 2 ^ 64 * (r2 + 2 ^ 64 * (r3 +
        2 ^ 64 * (r4 + 2 ^ 64 * (r5 + 2 ^ 64 * r6))))))) % L := by
  obtain ⟨m, q, rfl, hm⟩ : ∃ m q, r5 = m + 2 ^ 62 * q ∧ m < 2 ^ 62 :=
    ⟨_, _, (Nat.mod_add_div r5 (2 ^ 62)).symm, Nat.mod_lt r5 (by decide)⟩
  rw [Nat.add_mul_mod_self_left, Nat.mod_eq_of_lt hm, Nat.add_mul_div_left _ _ (by decide),
    Nat.div_eq_of_lt hm, Nat.zero_add]
  dsimp only
  have h6' : r6 < 2 ^ 62 := by omega_using [hr, L_lt]
  have hh : q + 4 * r6 < 2 ^ 64 := by omega_using [h5, h6, h6']
  have hhc : (q + 4 * r6) * cL < 2 ^ 64 * 2 ^ 224 := Nat.mul_lt_mul'' hh cL_lt
  have e : w + 2 ^ 64 * (r0 + 2 ^ 64 * (r1 + 2 ^ 64 * (r2 + 2 ^ 64 * (r3 + 2 ^ 64 * (r4 +
      2 ^ 64 * (m + 2 ^ 62 * q + 2 ^ 64 * r6)))))) =
      (w + 2 ^ 64 * (r0 + 2 ^ 64 * (r1 + 2 ^ 64 * (r2 + 2 ^ 64 * (r3 + 2 ^ 64 * (r4 + 2 ^ 64 * m))))) +
        (q + 4 * r6) * cL) + (q + 4 * r6) * L := by
    have e' : w + 2 ^ 64 * (r0 + 2 ^ 64 * (r1 + 2 ^ 64 * (r2 + 2 ^ 64 * (r3 + 2 ^ 64 * (r4 +
        2 ^ 64 * (m + 2 ^ 62 * q + 2 ^ 64 * r6)))))) =
        w + 2 ^ 64 * (r0 + 2 ^ 64 * (r1 + 2 ^ 64 * (r2 + 2 ^ 64 * (r3 + 2 ^ 64 * (r4 + 2 ^ 64 * m))))) +
          (q + 4 * r6) * 2 ^ 446 := by omega_using []
    rw [e', ← L_add, Nat.mul_add (q + 4 * r6) L cL, ← Nat.add_assoc, Nat.add_right_comm]
  refine ⟨h6', hh, by omega_using [L_add, cL_lt, hhc, hm, hw, h0, h1, h2, h3, h4], ?_⟩
  rw [e, Nat.add_mul_mod_self_right]

/-- The conditional subtraction: `K = M - L` added to `x < 2L` carries out of
`M` (`2^448`) exactly when `x ≥ L`, and then the sum is `x - L`. -/
theorem csub_nat {M x y : Nat} {c : Bool} (hM : 2 * L ≤ M) (hx : x < 2 * L) (hy : y < M)
    (he : y + M * c.toNat = x + (M - L)) :
    (if c then y else x) = x % L := by
  have hL := L_pos
  cases c with
  | false =>
    simp only [Bool.toNat_false, Nat.mul_zero, Nat.add_zero] at he
    rw [ite_eq_right Bool.false_ne_true, Nat.mod_eq_of_lt (by omega)]
  | true =>
    simp only [Bool.toNat_true, Nat.mul_one] at he
    rw [ite_eq_left rfl, Nat.mod_eq_sub_mod (by omega), Nat.mod_eq_of_lt (by omega)]
    omega

/-- Folding a word into the remainder of the words above it. -/
theorem mod_step (a w : Nat) : (a % L * 2 ^ 64 + w) % L = (w + 2 ^ 64 * a) % L := by
  have hd := Nat.mod_add_div a L
  generalize a % L = r at hd ⊢
  generalize a / L = q at hd
  subst hd
  rw [← Nat.add_mul_mod_self_left (r * 2 ^ 64 + w) L (q * 2 ^ 64)]
  congr 1
  rw [Nat.mul_add, Nat.add_comm w, Nat.add_assoc, Nat.add_comm w, ← Nat.add_assoc,
    Nat.mul_comm (2 ^ 64) r, Nat.mul_left_comm (2 ^ 64) L q, Nat.mul_comm (2 ^ 64) q,
    ← Nat.mul_assoc]

theorem decodeLE_eq (xs : List Byte) : decodeLE xs = Proof.X25519.leNum xs := by
  induction xs with
  | nil => rfl
  | cons b bs ih => simp only [decodeLE, Proof.X25519.leNum, ih]

theorem decodeLE_append (xs ys : List Byte) :
    decodeLE (xs ++ ys) = decodeLE xs + 256 ^ xs.length * decodeLE ys := by
  simp only [decodeLE_eq, Proof.X25519.leNum_append]

theorem decodeLE_lt' (xs : List Byte) : decodeLE xs < 256 ^ xs.length := by
  rw [decodeLE_eq]; exact Proof.X25519.leNum_lt xs

theorem encodeLE_eq (n x : Nat) : encodeLE n x = Proof.X25519.leBytes n x := by
  simp only [encodeLE, Proof.X25519.leBytes, Nat.shiftRight_eq_div_pow, Nat.pow_mul]

theorem bytesAt_eq (m : Mem) (p : Addr) (n : Nat) : bytesAt m p n = Spec.X25519.bytesAt m p n := rfl

theorem leBytes_one (x : Nat) : Proof.X25519.leBytes 1 x = [BitVec.ofNat 8 x] := by
  simp [Proof.X25519.leBytes]

/-- A point's encoding (§5.2.2): the 56 bytes of `y < 2^448`, then the sign
bit `b` as the top bit of the 57th byte. Stated here, where `2 ^ 455` is the
specification's term. -/
theorem encodeLE_57 (y b : Nat) (hy : y < 256 ^ 56) :
    encodeLE 57 (y + b * 2 ^ 455) =
      Proof.X25519.leBytes 56 y ++ [BitVec.ofNat 8 (128 * b)] := by
  have h455 : (2 : Nat) ^ 455 = 256 ^ 56 * 128 := by decide +kernel
  rw [encodeLE_eq, show 57 = 56 + 1 from rfl, Proof.X25519.leBytes_add, h455, leBytes_one]
  generalize hM : (256 : Nat) ^ 56 = M at *
  have hM0 : 0 < M := by omega
  have e1 : (y + b * (M * 128)) % M = y := by
    rw [show y + b * (M * 128) = y + M * (b * 128) by grind, Nat.add_mul_mod_self_left, Nat.mod_eq_of_lt hy]
  have e2 : (y + b * (M * 128)) / M = 128 * b := by
    rw [show y + b * (M * 128) = y + M * (b * 128) by grind, Nat.add_mul_div_left _ _ hM0, Nat.div_eq_of_lt hy,
      Nat.zero_add, Nat.mul_comm]
  rw [e2, ← Proof.X25519.leBytes_mod 56, hM, e1]

/-- A 57-byte number has no bits from 456 up. -/
theorem decodeLE_shift_456 (xs : List Byte) (h : xs.length = 57) : decodeLE xs >>> 456 = 0 := by
  have hl := decodeLE_lt' xs
  rw [h] at hl
  have e : (2 : Nat) ^ 456 = 256 ^ 57 := by decide +kernel
  rw [Nat.shiftRight_eq_div_pow, e]
  exact Nat.div_eq_of_lt hl

/-- The check of `S < L` on its low 448 bits `y` and byte 56 `b`: `y + (2^448 - L)`
leaves `r` and the carry `c`. -/
theorem sCheck_nat {y b r c : Nat} (hy : y < 2 ^ 448) (hr : r < 2 ^ 448) (hc : c ≤ 1)
    (h : r + 2 ^ 448 * c = y + (2 ^ 448 - L)) : (c = 0 ∧ b = 0) ↔ y + 256 ^ 56 * b < L := by
  have e : (256 : Nat) ^ 56 = 2 ^ 448 := by decide +kernel
  have hL : L < 2 ^ 448 := by decide +kernel
  rw [e]
  generalize (2 : Nat) ^ 448 = M at *
  constructor
  · rintro ⟨rfl, rfl⟩; simp only [Nat.mul_zero, Nat.add_zero] at h ⊢; omega
  · intro h'
    have : b = 0 := by
      rcases Nat.eq_zero_or_pos b with hb | hb
      · exact hb
      · have : M ≤ M * b := Nat.le_mul_of_pos_right M hb
        omega
    subst this
    rcases (by omega : c = 0 ∨ c = 1) with rfl | rfl <;> omega

theorem bytesAt_getD (m : Mem) (p : Addr) {n i : Nat} (hi : i < n) :
    (bytesAt m p n).getD i 0 = m (p + BitVec.ofNat 64 i) := by
  rw [bytesAt_eq]
  simp [Spec.X25519.bytesAt, List.getD_eq_getElem?_getD, hi]

/-- Bit `t` of the scalar is bit `t % 8` of its byte `t / 8`. -/
theorem scalar_bit (m : Mem) (p : Addr) {t : Nat} (ht : t < 456) :
    ((m (p + BitVec.ofNat 64 (t / 8))).toNat >>> (t % 8)) &&& 1 =
      (decodeLE (bytesAt m p 57) >>> t) &&& 1 := by
  rw [decodeLE_eq, Proof.X25519.leNum_bit, bytesAt_getD m p (by omega)]

end VG.Proof.Ed448
