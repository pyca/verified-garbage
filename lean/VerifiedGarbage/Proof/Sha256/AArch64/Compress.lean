import VerifiedGarbage.Proof.Framework.AArch64.VecPreserved
import VerifiedGarbage.Proof.Framework.Block
import VerifiedGarbage.Proof.Framework.Mem
import VerifiedGarbage.Proof.Framework.AArch64.Taint
import VerifiedGarbage.Proof.Framework.AArch64.Exec
import VerifiedGarbage.Proof.Framework.AArch64.RegUpd
import VerifiedGarbage.Proof.Sha256.Spec
import VerifiedGarbage.Impl.Sha256.AArch64
import VerifiedGarbage.Proof.Framework.Offset
import VerifiedGarbage.Proof.Sha256.StateMem
import VerifiedGarbage.Spec.Sha256
import VerifiedGarbage.TCB.AArch64.Target
import VerifiedGarbage.Proof.Framework.AArch64.Lit
import VerifiedGarbage.Impl.Sha256.AArch64.Stream

/- Proofs formerly in `VerifiedGarbage.Proof.Sha256.AArch64.Contract`. -/
section

/-!
# SHA-256: the AArch64 contract

The contracts the proofs are written against; the artifacts are emitted with the
shared contracts of `Spec/`, which imply these (`Contract.Implies`). The
contracts of the AArch64 implementations of the compression function and the
streaming interface, in terms of `Spec/Sha256.lean`.

The return address is in the link register `x30`, which the target's
calling convention requires to be preserved (`VG.AArch64.abiPreserved`), not
on the stack, so unlike on x86-64 no region needs to be kept disjoint from it.
-/

namespace VG.Proof.Sha256

open Spec.Sha256

open AArch64 in
/-- AArch64 contract for
`vg_sha256_compress(state: *mut [u32; 8], blocks: *const [u8; 64], n: usize, scratch: *mut [u64; 14])`:
updates the hash value at `state` with the `n` 64-byte blocks at `blocks`.

The code may read `blocks` (`64 * n` bytes) and read and write `state`
(32 bytes) and `scratch` (112 bytes, whose contents on exit are unspecified).
These may not overlap each other. The pointers and `n` are public; the hash
value and the blocks are secret. -/
def compressAArch64 : Contract AArch64.isa where
  pre s :=
    let state : Region := ⟨s.gpr .x0, 32⟩
    let blocks : Region := ⟨s.gpr .x1, 64 * (s.gpr .x2).toNat⟩
    let scratch : Region := ⟨s.gpr .x3, 112⟩
    s.rd = [blocks] ∧ s.wr = [state, scratch] ∧
    state.Disjoint scratch ∧ blocks.Disjoint state ∧ blocks.Disjoint scratch
  post s s' :=
    stateAt s'.mem (s.gpr .x0) =
      compressBlocks (stateAt s.mem (s.gpr .x0)) s.mem (s.gpr .x1) (s.gpr .x2).toNat
  pub s₁ s₂ :=
    s₁.gpr .x0 = s₂.gpr .x0 ∧ s₁.gpr .x1 = s₂.gpr .x1 ∧
    s₁.gpr .x2 = s₂.gpr .x2 ∧ s₁.gpr .x3 = s₂.gpr .x3 ∧ s₁.sp = s₂.sp

open AArch64 in
/-- AArch64 contract for `vg_sha256_init(state: *mut [u8; 96])` and
`vg_sha224_init`, which store the initial hash value `iv`: makes the
streaming state at `state` represent the empty message, hashed from `iv`.

The code may write `state` (96 bytes). The pointer is public. -/
def initAArch64 (iv : HashValue) : Contract AArch64.isa where
  pre s :=
    let state : Region := ⟨s.gpr .x0, 96⟩
    s.rd = [] ∧ s.wr = [state]
  post s s' := ReprFrom iv s'.mem (s.gpr .x0) []
  pub s₁ s₂ := s₁.gpr .x0 = s₂.gpr .x0 ∧ s₁.sp = s₂.sp

open AArch64 in
/-- AArch64 contract for
`vg_sha256_update(state: *mut [u8; 96], count: u64, data: *const u8, len: usize, scratch: *mut [u64; 20])`:
if the streaming state at `state` represents a message `m` of `count` bytes
(modulo 2⁶⁴), hashed from any initial hash value `iv`, then afterwards it
represents `m` followed by the `len` bytes at `data`, from `iv`.

The code may read `data` (`len` bytes) and read and write `state` (96
bytes) and `scratch` (160 bytes, whose contents on exit are unspecified).
These may not overlap each other, nor the 16 bytes below the stack pointer
(the frame saving `x30`), which do not wrap around. The pointers, `count` and
`len` are public; the state and the data are secret. -/
def updateAArch64 : Contract AArch64.isa where
  pre s :=
    let state : Region := ⟨s.gpr .x0, 96⟩
    let data : Region := ⟨s.gpr .x2, (s.gpr .x3).toNat⟩
    let scratch : Region := ⟨s.gpr .x4, 160⟩
    let stack : Region := ⟨s.sp - 16, 16⟩
    s.rd = [data] ∧ s.wr = [state, scratch] ∧
    state.Disjoint scratch ∧ data.Disjoint state ∧ data.Disjoint scratch ∧
    16 ≤ s.sp.toNat ∧ stack.Disjoint state ∧ stack.Disjoint data ∧ stack.Disjoint scratch
  post s s' := ∀ iv m, ReprFrom iv s.mem (s.gpr .x0) m → s.gpr .x1 = BitVec.ofNat 64 m.length →
    ReprFrom iv s'.mem (s.gpr .x0) (m ++ bytesAt s.mem (s.gpr .x2) (s.gpr .x3).toNat)
  pub s₁ s₂ :=
    s₁.gpr .x0 = s₂.gpr .x0 ∧ s₁.gpr .x1 = s₂.gpr .x1 ∧ s₁.gpr .x2 = s₂.gpr .x2 ∧
    s₁.gpr .x3 = s₂.gpr .x3 ∧ s₁.gpr .x4 = s₂.gpr .x4 ∧ s₁.sp = s₂.sp

open AArch64 in
/-- AArch64 contract for
`vg_sha256_finalize(state: *mut [u8; 96], count: u64, out: *mut [u8; 32], scratch: *mut [u64; 20])`:
if the streaming state at `state` represents a message `m` of `count` bytes
(modulo 2⁶⁴), hashed from the initial hash value `iv`, writes the final hash
value of `m` from `iv` to `out` (the SHA-256 digest if `iv` is `H0`).

The code may read and write `state` (96 bytes, whose contents on exit are
unspecified), `out` (32 bytes) and `scratch` (160 bytes, whose contents on
exit are unspecified). These may not overlap each other, nor the 16 bytes
below the stack pointer (the frame saving `x30`), which do not wrap around.
The pointers and `count` are public; the state is secret. -/
def finalizeAArch64 : Contract AArch64.isa where
  pre s :=
    let state : Region := ⟨s.gpr .x0, 96⟩
    let out : Region := ⟨s.gpr .x2, 32⟩
    let scratch : Region := ⟨s.gpr .x3, 160⟩
    let stack : Region := ⟨s.sp - 16, 16⟩
    s.rd = [] ∧ s.wr = [state, out, scratch] ∧
    state.Disjoint out ∧ state.Disjoint scratch ∧ out.Disjoint scratch ∧
    16 ≤ s.sp.toNat ∧ stack.Disjoint state ∧ stack.Disjoint out ∧ stack.Disjoint scratch
  post s s' := ∀ iv m, ReprFrom iv s.mem (s.gpr .x0) m → s.gpr .x1 = BitVec.ofNat 64 m.length →
    bytesAt s'.mem (s.gpr .x2) 32 = Spec.Sha256.finalHash iv m
  pub s₁ s₂ :=
    s₁.gpr .x0 = s₂.gpr .x0 ∧ s₁.gpr .x1 = s₂.gpr .x1 ∧ s₁.gpr .x2 = s₂.gpr .x2 ∧
    s₁.gpr .x3 = s₂.gpr .x3 ∧ s₁.sp = s₂.sp

end VG.Proof.Sha256

end

/- Proofs formerly in `VerifiedGarbage.Proof.Sha256.AArch64.Lit`. -/
section

/-!
# SHA-256 on AArch64: the code as literals
-/

namespace VG

materialize_code Impl.Sha256.AArch64.compress
materialize_code Impl.Sha256.AArch64.Stream.update
materialize_code Impl.Sha256.AArch64.Stream.finalize

end VG

end

/- Proofs formerly in `VerifiedGarbage.Proof.Sha256.AArch64.Compress`. -/
section

/-!
# SHA-256 compression function on AArch64: the message schedule and the rounds
-/

namespace VG.Proof.Sha256.AArch64

open VG VG.AArch64 VG.Impl.Sha256.AArch64
open VG.Spec.Sha256 (HashValue Word Block K W bsig0 bsig1 ch maj ssig0 ssig1)

/-- The working variables `v` are in the registers of round `t`. -/
def Vars (t : Nat) (s : State) (v : HashValue) : Prop :=
  s.gpr (var t 0) = v[0].setWidth 64 ∧ s.gpr (var t 1) = v[1].setWidth 64 ∧
  s.gpr (var t 2) = v[2].setWidth 64 ∧ s.gpr (var t 3) = v[3].setWidth 64 ∧
  s.gpr (var t 4) = v[4].setWidth 64 ∧ s.gpr (var t 5) = v[5].setWidth 64 ∧
  s.gpr (var t 6) = v[6].setWidth 64 ∧ s.gpr (var t 7) = v[7].setWidth 64

/-- The pointers, the count and the registers the ABI requires us to
preserve: never written by the rounds. -/
def pubRegs : List Reg := [.x0, .x1, .x2, .x3, .x19, .x20, .x21, .x22, .x23, .x24, .x25,
  .x26, .x27, .x28, .x30]

/-- The working variables move one register along each round. -/
theorem var_succ (t k : Nat) (hk : k < 7) : var (t + 1) (k + 1) = var t k := by
  simp only [var]; congr 1; omega

theorem var_succ_zero (t : Nat) : var (t + 1) 0 = var t 7 := by
  simp only [var]; congr 1; omega

/-- The registers a round reads are not those it writes after them (`T1`,
`T2`, `d`, `h`). In parts: `decide` cannot synthesize the instance of one
long conjunction. -/
theorem round_ne₁ (t : Nat) :
    (¬var t 0 = .x13 ∧ ¬var t 0 = .x14 ∧ ¬var t 1 = .x13 ∧ ¬var t 1 = .x14 ∧
      ¬var t 2 = .x13 ∧ ¬var t 2 = .x14 ∧ ¬var t 3 = .x13 ∧ ¬var t 3 = .x14) ∧
    (¬var t 4 = .x13 ∧ ¬var t 4 = .x14 ∧ ¬var t 5 = .x13 ∧ ¬var t 5 = .x14 ∧
      ¬var t 6 = .x13 ∧ ¬var t 6 = .x14 ∧ ¬var t 7 = .x13 ∧ ¬var t 7 = .x14) := by
  simp only [var]
  have := Nat.mod_lt t (show 8 > 0 by omega)
  generalize t % 8 = c at *
  revert this; revert c; decide

theorem round_ne₂ (t : Nat) :
    (¬var t 0 = var t 3 ∧ ¬var t 1 = var t 3 ∧ ¬var t 2 = var t 3 ∧ ¬var t 4 = var t 3 ∧
      ¬var t 5 = var t 3 ∧ ¬var t 6 = var t 3 ∧ ¬var t 7 = var t 3) ∧
    (¬var t 0 = var t 7 ∧ ¬var t 1 = var t 7 ∧ ¬var t 2 = var t 7 ∧ ¬var t 3 = var t 7 ∧
      ¬var t 4 = var t 7 ∧ ¬var t 5 = var t 7 ∧ ¬var t 6 = var t 7) ∧
    (¬Reg.x12 = var t 3 ∧ ¬Reg.x12 = var t 7 ∧ ¬Reg.x13 = var t 3 ∧ ¬Reg.x13 = var t 7 ∧
      ¬Reg.x14 = var t 3 ∧ ¬Reg.x14 = var t 7) := by
  simp only [var]
  have := Nat.mod_lt t (show 8 > 0 by omega)
  generalize t % 8 = c at *
  revert this; revert c; decide

/-- The registers the rounds keep are none of those a round writes. -/
theorem pub_ne (t : Nat) :
    ∀ r ∈ VG.Proof.Sha256.AArch64.pubRegs, ¬r = .x13 ∧ ¬r = .x14 ∧ ¬r = var t 3 ∧ ¬r = var t 7 := by
  simp only [var]
  have := Nat.mod_lt t (show 8 > 0 by omega)
  generalize t % 8 = c at *
  revert this; revert c; decide

/-- `movz_movk'` with its shifts evaluated. -/
theorem movz_movk₀ (x : BitVec 32) :
    (x.extractLsb' 0 16).setWidth 32 <<< 0 &&& ~~~((65535 : BitVec 32) <<< 16) |||
      (x.extractLsb' 16 16).setWidth 32 <<< 16 = x :=
  movz_movk' x

/-- The round is symbolically executed once, for any registers `a … h`
(which `round_ne₁`, `round_ne₂` and `pub_ne` say are different where it
matters), with the register writes kept folded (`VG.AArch64.RegUpd`). -/
theorem round_ok (t : Nat) (s : State) (v : HashValue) (w : Word)
    (hv : VG.Proof.Sha256.AArch64.Vars t s v) (hw : s.gpr T0 = w.setWidth 64) :
    WP isa (.block (round t)) s fun s' =>
      VG.Proof.Sha256.AArch64.Vars (t + 1) s' (roundKW v (K t) w) ∧
      s'.mem = s.mem ∧ s'.rd = s.rd ∧ s'.wr = s.wr ∧ ∀ r ∈ VG.Proof.Sha256.AArch64.pubRegs, s'.gpr r = s.gpr r := by
  obtain ⟨n₁, n₂⟩ := VG.Proof.Sha256.AArch64.round_ne₁ t
  obtain ⟨n₃, n₄, n₅⟩ := VG.Proof.Sha256.AArch64.round_ne₂ t
  have hp := VG.Proof.Sha256.AArch64.pub_ne t
  simp only [VG.Proof.Sha256.AArch64.Vars, VG.Proof.Sha256.AArch64.var_succ_zero, VG.Proof.Sha256.AArch64.var_succ t _ (show 0 < 7 by omega),
    VG.Proof.Sha256.AArch64.var_succ t _ (show 1 < 7 by omega), VG.Proof.Sha256.AArch64.var_succ t _ (show 2 < 7 by omega),
    VG.Proof.Sha256.AArch64.var_succ t _ (show 3 < 7 by omega), VG.Proof.Sha256.AArch64.var_succ t _ (show 4 < 7 by omega),
    VG.Proof.Sha256.AArch64.var_succ t _ (show 5 < 7 by omega), VG.Proof.Sha256.AArch64.var_succ t _ (show 6 < 7 by omega)] at hv ⊢
  obtain ⟨h0, h1, h2, h3, h4, h5, h6, h7⟩ := hv
  apply WP.of_runBlock
  simp only [Impl.Sha256.AArch64.round]
  generalize var t 0 = a at *
  generalize var t 1 = b at *
  generalize var t 2 = c at *
  generalize var t 3 = d at *
  generalize var t 4 = e at *
  generalize var t 5 = f at *
  generalize var t 6 = g at *
  generalize var t 7 = h at *
  simp only [T0, T1, T2] at hw ⊢
  simp only [runBlock_cons, runStep_some, runBlock_nil, exec, isa, State.read, Size.bits,
    RegUpd.gpr_write_self, RegUpd.gpr_write_of_ne, RegUpd.mem_write, RegUpd.rd_write,
    RegUpd.wr_write, ite_true, Nat.reduceLT, Nat.reduceLeDiff, Nat.reduceMul, not_false_eq_true,
    reduceCtorEq, n₁, n₂, n₃, n₄, n₅,
    h0, h1, h2, h3, h4, h5, h6, h7, hw,
    BitVec.setWidth_setWidth_of_le, BitVec.setWidth_eq, Option.some.injEq, exists_eq_left']
  refine ⟨⟨?_, ?_, ?_, ?_, ?_, ?_, ?_, ?_⟩, trivial, trivial, trivial, fun r hr => ?_⟩
  rotate_right
  · obtain ⟨p₁, p₂, p₃, p₄⟩ := hp r hr
    simp only [RegUpd.gpr_write_of_ne, p₁, p₂, p₃, p₄, not_false_eq_true]
  all_goals
    refine congrArg (BitVec.setWidth 64) ?_
    simp only [roundKW_0, roundKW_1, roundKW_2, roundKW_3, roundKW_4, roundKW_5, roundKW_6, roundKW_7, bsig1, ch_eq, bsig0, maj_eq, VG.Proof.Sha256.AArch64.movz_movk₀, BitVec.add_assoc]

theorem slot_ok (j : Nat) : slot j % 4 = 0 ∧ slot j < 16384 := by
  simp only [slot]; omega

/-- The address of `W[j mod 16]`. -/
abbrev slotAddr (scr : Addr) (j : Nat) : Addr := scr + BitVec.ofNat 64 (slot j)

theorem schedule_ok (t : Nat) (s : State) (M : Block) (bp scr : Addr)
    (hx1 : s.gpr .x1 = bp) (hx3 : s.gpr .x3 = scr)
    (hin : ∀ j, InRegions (s.rd ++ s.wr) (VG.Proof.Sha256.AArch64.slotAddr scr j) 4)
    (hout : ∀ j, InRegions s.wr (VG.Proof.Sha256.AArch64.slotAddr scr j) 4)
    (hbin : t < 16 → InRegions (s.rd ++ s.wr) (bp + BitVec.ofNat 64 (4 * t)) 4)
    (hblk : t < 16 → rev32 (s.mem.readW (bp + BitVec.ofNat 64 (4 * t)) 32) = W M t)
    (hwin : 16 ≤ t → ∀ j, j < t → t ≤ j + 16 → s.mem.readW (VG.Proof.Sha256.AArch64.slotAddr scr j) 32 = W M j) :
    WP isa (.block (schedule t)) s fun s' =>
      s'.gpr T0 = (W M t).setWidth 64 ∧
      s'.mem = s.mem.writeW (VG.Proof.Sha256.AArch64.slotAddr scr t) (W M t) ∧
      s'.rd = s.rd ∧ s'.wr = s.wr ∧
      ∀ r, r ≠ T0 → r ≠ T1 → r ≠ T2 → r ≠ T3 → s'.gpr r = s.gpr r := by
  simp only [VG.Proof.Sha256.AArch64.slotAddr] at hin hout hwin ⊢
  apply WP.of_runBlock
  by_cases ht : t < 16
  · have hi := hbin ht
    have hb := hblk ht
    have ho : 4 * t % 4 = 0 ∧ 4 * t < 16384 := by omega
    simp only [Impl.Sha256.AArch64.schedule, ht, ite_true, T0, T1, T2, T3]
    simp only [runBlock_cons, runStep_some,
      runBlock_nil, exec_ldr_w ho, exec_str_w (VG.Proof.Sha256.AArch64.slot_ok _),
      exec_rev32, isa, State.read, Size.bits, RegUpd.gpr_write_self, RegUpd.gpr_write_of_ne,
      RegUpd.mem_write, RegUpd.rd_write,
      RegUpd.wr_write, not_false_eq_true, reduceCtorEq,
      Nat.reduceLeDiff, hx1, hx3, hi, hout,
      BitVec.setWidth_setWidth_of_le, BitVec.setWidth_eq, hb,
      Option.some.injEq, exists_eq_left']
    refine ⟨trivial, trivial, trivial, trivial, fun r h0 _ _ _ => ?_⟩
    simp only [RegUpd.gpr_write_of_ne, h0, not_false_eq_true]
  · have hw := hwin (by omega)
    have e2 := hw (t - 2) (by omega) (by omega)
    have e7 := hw (t - 7) (by omega) (by omega)
    have e15 := hw (t - 15) (by omega) (by omega)
    have e16 := hw (t - 16) (by omega) (by omega)
    rw [show slot (t - 2) = slot (t + 14) by simp only [slot]; omega] at e2
    rw [show slot (t - 7) = slot (t + 9) by simp only [slot]; omega] at e7
    rw [show slot (t - 15) = slot (t + 1) by simp only [slot]; omega] at e15
    rw [show slot (t - 16) = slot t by simp only [slot]; omega] at e16
    simp only [Impl.Sha256.AArch64.schedule, ht, ite_false, T0, T1, T2, T3]
    simp only [runBlock_cons, runStep_some,
      runBlock_nil, exec_ldr_w (VG.Proof.Sha256.AArch64.slot_ok _),
      exec_str_w (VG.Proof.Sha256.AArch64.slot_ok _), exec_add, exec_logic, exec_ror_w, exec_lsr_w, isa, State.read,
      Size.bits, RegUpd.gpr_write_self, RegUpd.gpr_write_of_ne, RegUpd.mem_write, RegUpd.rd_write,
      RegUpd.wr_write, Nat.reduceLT, Nat.reduceLeDiff, not_false_eq_true, reduceCtorEq, hx3, hin,
      hout, BitVec.setWidth_setWidth_of_le,
      BitVec.setWidth_eq, e2, e7, e15, e16, Option.some.injEq, exists_eq_left']
    have hW := W_ge M (t := t) (by omega)
    refine ⟨by rw [hW]; rfl, by rw [hW]; rfl, trivial, trivial, fun r h0 h1 h2 h3 => ?_⟩
    simp only [RegUpd.gpr_write_of_ne, h0, h1, h2, h3, not_false_eq_true]

/-! ## The 64 rounds -/

theorem var_mem (t k : Nat) : var t k ∈ work := by
  unfold var List.getD
  cases h : work[(k + 8 - t % 8) % 8]?
  · simp [work]
  · exact List.mem_of_getElem? h

theorem work_ne' : ∀ r ∈ work, r ≠ T0 ∧ r ≠ T1 ∧ r ≠ T2 ∧ r ≠ T3 := by decide

theorem work_ne {r : Reg} (h : r ∈ work) : r ≠ T0 ∧ r ≠ T1 ∧ r ≠ T2 ∧ r ≠ T3 := VG.Proof.Sha256.AArch64.work_ne' r h

theorem pubRegs_ne' : ∀ r ∈ VG.Proof.Sha256.AArch64.pubRegs, r ≠ T0 ∧ r ≠ T1 ∧ r ≠ T2 ∧ r ≠ T3 := by decide

theorem pubRegs_ne {r : Reg} (h : r ∈ VG.Proof.Sha256.AArch64.pubRegs) : r ≠ T0 ∧ r ≠ T1 ∧ r ≠ T2 ∧ r ≠ T3 :=
  VG.Proof.Sha256.AArch64.pubRegs_ne' r h

/-- The window `⟨scr, 64⟩`. -/
abbrev winRegion (scr : Addr) : Region := ⟨scr, 64⟩

theorem win_contains (scr : Addr) (j : Nat) : (VG.Proof.Sha256.AArch64.winRegion scr).Contains (VG.Proof.Sha256.AArch64.slotAddr scr j) 4 := by
  simp only [Region.Contains, VG.Proof.Sha256.AArch64.slotAddr, slot]
  have : j % 16 < 16 := Nat.mod_lt _ (by omega)
  generalize j % 16 = p at *
  rw [Offset.add_sub_cancel_left]
  simp only [BitVec.toNat_ofNat]
  omega

theorem slot_sep (scr : Addr) {i j : Nat} (h : i % 16 ≠ j % 16) :
    Mem.Sep (VG.Proof.Sha256.AArch64.slotAddr scr i) 4 (VG.Proof.Sha256.AArch64.slotAddr scr j) 4 := by
  simp only [VG.Proof.Sha256.AArch64.slotAddr, slot]
  exact Offset.sep _ (by omega) (by omega) (by omega)

/-- Rounds invariant, relative to the state `sB` at the start of the rounds. -/
structure RInv (H : HashValue) (M : Block) (scr : Addr) (sB : State) (t : Nat) (s : State) : Prop where
  vars : VG.Proof.Sha256.AArch64.Vars t s (VG.Spec.Sha256.rounds H M t)
  pub : ∀ r ∈ VG.Proof.Sha256.AArch64.pubRegs, s.gpr r = sB.gpr r
  rd : s.rd = sB.rd
  wr : s.wr = sB.wr
  frame : Frame [VG.Proof.Sha256.AArch64.winRegion scr] sB.mem s.mem
  win : ∀ j < t, t ≤ j + 16 → s.mem.readW (VG.Proof.Sha256.AArch64.slotAddr scr j) 32 = W M j

theorem rounds_ok (H : HashValue) (M : Block) (bp scr : Addr) (sB : State)
    (hrsi : sB.gpr .x1 = bp) (hrcx : sB.gpr .x3 = scr)
    (hin : ∀ j, InRegions (sB.rd ++ sB.wr) (VG.Proof.Sha256.AArch64.slotAddr scr j) 4)
    (hout : ∀ j, InRegions sB.wr (VG.Proof.Sha256.AArch64.slotAddr scr j) 4)
    (hbin : ∀ t : Nat, t < 16 → InRegions (sB.rd ++ sB.wr) (bp + BitVec.ofNat 64 (4 * t)) 4)
    (hblk : ∀ m, Frame [VG.Proof.Sha256.AArch64.winRegion scr] sB.mem m →
      ∀ t : Nat, t < 16 → rev32 (m.readW (bp + BitVec.ofNat 64 (4 * t)) 32) = W M t)
    (h0 : VG.Proof.Sha256.AArch64.Vars 0 sB H) :
    ∀ t ≤ 64, WP isa (rounds t) sB (VG.Proof.Sha256.AArch64.RInv H M scr sB t) := by
  intro t ht
  induction t with
  | zero =>
    refine WP.block_nil (M := isa) ⟨?_, fun _ _ => rfl, rfl, rfl, Frame.refl _ _, fun j hj => absurd hj (by omega)⟩
    rw [rounds_zero]; exact h0
  | succ t ih =>
    refine WP.seq (WP.mono (ih (by omega)) fun s hs => ?_)
    rw [WP.block_append_iff]
    have hs_rsi : s.gpr .x1 = bp := (hs.pub .x1 (by decide)).trans hrsi
    have hs_rcx : s.gpr .x3 = scr := (hs.pub .x3 (by decide)).trans hrcx
    refine WP.mono (VG.Proof.Sha256.AArch64.schedule_ok t s M bp scr hs_rsi hs_rcx
      (by rw [hs.rd, hs.wr]; exact hin) (by rw [hs.wr]; exact hout)
      (fun h => by rw [hs.rd, hs.wr]; exact hbin t h) (hblk _ hs.frame t)
      (fun _ => hs.win)) fun s₁ ⟨hT0, hm₁, hrd₁, hwr₁, hr₁⟩ => ?_
    have hv₁ : VG.Proof.Sha256.AArch64.Vars t s₁ (VG.Spec.Sha256.rounds H M t) := by
      have hv := hs.vars
      have e : ∀ k, s₁.gpr (var t k) = s.gpr (var t k) := fun k =>
        have := VG.Proof.Sha256.AArch64.work_ne (VG.Proof.Sha256.AArch64.var_mem t k); hr₁ _ this.1 this.2.1 this.2.2.1 this.2.2.2
      simp only [VG.Proof.Sha256.AArch64.Vars, e] at hv ⊢
      exact hv
    refine WP.mono (VG.Proof.Sha256.AArch64.round_ok t s₁ _ _ hv₁ hT0) fun s₂ ⟨hv₂, hm₂, hrd₂, hwr₂, hr₂⟩ => ?_
    refine ⟨?_, fun r hr => ?_, by rw [hrd₂, hrd₁, hs.rd], by rw [hwr₂, hwr₁, hs.wr], ?_, ?_⟩
    · have e : VG.Spec.Sha256.rounds H M (t + 1) =
          roundKW (VG.Spec.Sha256.rounds H M t) (K t) (W M t) := by
        rw [rounds_succ, round_eq]
      rw [e]; exact hv₂
    · have := VG.Proof.Sha256.AArch64.pubRegs_ne hr
      rw [hr₂ r hr, hr₁ r this.1 this.2.1 this.2.2.1 this.2.2.2, hs.pub r hr]
    · rw [hm₂, hm₁]
      exact hs.frame.writeW (List.mem_singleton_self _) _ (VG.Proof.Sha256.AArch64.win_contains scr t)
    · intro j hj hj'
      rw [hm₂, hm₁]
      by_cases hjt : j = t
      · subst hjt; exact Mem.readW_writeW_self32 _ _ _
      · rw [Mem.readW_writeW_sep (VG.Proof.Sha256.AArch64.slot_sep scr (by omega)) (by decide)]
        exact hs.win j (by omega) (by omega)

end VG.Proof.Sha256.AArch64

/-!
# SHA-256 compression function on AArch64: the whole function
-/

namespace VG.Proof.Sha256.AArch64

open VG VG.AArch64 VG.Impl.Sha256.AArch64
open VG.Spec.Sha256 (HashValue Word Block K W stateAt blockAt compressBlocks compress parseBlock)

/-! The hash value in memory and offsets into regions (`Proof/Sha256/StateMem.lean`). -/
export VG.Proof.Sha256.StateMem (toNat_ofNat_lt contains_offset sub_offset word_sep
  readW_writeW_word stateAt_eq stateAt_get writeState stateAt_writeState)

/-! ## The precondition -/

section
variable (s₀ : State)

abbrev st : Addr := s₀.gpr .x0
abbrev bp : Addr := s₀.gpr .x1
abbrev nb : Nat := (s₀.gpr .x2).toNat
abbrev scr : Addr := s₀.gpr .x3
abbrev stR : Region := ⟨VG.Proof.Sha256.AArch64.st s₀, 32⟩
abbrev blR : Region := ⟨VG.Proof.Sha256.AArch64.bp s₀, 64 * VG.Proof.Sha256.AArch64.nb s₀⟩
abbrev scrR : Region := ⟨VG.Proof.Sha256.AArch64.scr s₀, 112⟩
abbrev H₀ : HashValue := stateAt s₀.mem (VG.Proof.Sha256.AArch64.st s₀)

/-- Block `i`, and where it starts. -/
abbrev blkAddr (i : Nat) : Addr := VG.Proof.Sha256.AArch64.bp s₀ + BitVec.ofNat 64 (64 * i)
abbrev blk (i : Nat) : Block := blockAt s₀.mem (VG.Proof.Sha256.AArch64.blkAddr s₀ i)

end

structure Pre (s₀ : State) : Prop where
  rd : s₀.rd = [VG.Proof.Sha256.AArch64.blR s₀]
  wr : s₀.wr = [VG.Proof.Sha256.AArch64.stR s₀, VG.Proof.Sha256.AArch64.scrR s₀]
  st_scr : (VG.Proof.Sha256.AArch64.stR s₀).Disjoint (VG.Proof.Sha256.AArch64.scrR s₀)
  blk_st : (VG.Proof.Sha256.AArch64.blR s₀).Disjoint (VG.Proof.Sha256.AArch64.stR s₀)
  blk_scr : (VG.Proof.Sha256.AArch64.blR s₀).Disjoint (VG.Proof.Sha256.AArch64.scrR s₀)

theorem pre_of (s₀ : State) (h : Proof.Sha256.compressAArch64.pre s₀) : VG.Proof.Sha256.AArch64.Pre s₀ := by
  obtain ⟨h1, h2, h3, h4, h5⟩ := h
  exact ⟨h1, h2, h3, h4, h5⟩

namespace Pre
variable {s₀ : State} (h : VG.Proof.Sha256.AArch64.Pre s₀)
include h

theorem nb_lt : 64 * VG.Proof.Sha256.AArch64.nb s₀ < 2 ^ 64 := by
  by_contra hn
  refine h.blk_st (VG.Proof.Sha256.AArch64.st s₀) ?_ (by simp [Region.Contains])
  simp only [Region.Contains]
  have := (VG.Proof.Sha256.AArch64.st s₀ - VG.Proof.Sha256.AArch64.bp s₀).isLt
  omega

theorem in_state {k : Nat} (hk : k < 8) :
    InRegions (s₀.rd ++ s₀.wr) (VG.Proof.Sha256.AArch64.st s₀ + BitVec.ofNat 64 (4 * k)) 4 :=
  ⟨VG.Proof.Sha256.AArch64.stR s₀, by simp [h.wr], VG.Proof.Sha256.StateMem.contains_offset (by omega) (by omega)⟩

theorem out_state {k : Nat} (hk : k < 8) :
    InRegions s₀.wr (VG.Proof.Sha256.AArch64.st s₀ + BitVec.ofNat 64 (4 * k)) 4 :=
  ⟨VG.Proof.Sha256.AArch64.stR s₀, by simp [h.wr], VG.Proof.Sha256.StateMem.contains_offset (by omega) (by omega)⟩

theorem in_slot (j : Nat) : InRegions (s₀.rd ++ s₀.wr) (VG.Proof.Sha256.AArch64.slotAddr (VG.Proof.Sha256.AArch64.scr s₀) j) 4 :=
  ⟨VG.Proof.Sha256.AArch64.scrR s₀, by simp [h.wr], VG.Proof.Sha256.StateMem.contains_offset (by simp only [slot]; omega) (by simp only [slot]; omega)⟩

theorem out_slot (j : Nat) : InRegions s₀.wr (VG.Proof.Sha256.AArch64.slotAddr (VG.Proof.Sha256.AArch64.scr s₀) j) 4 :=
  ⟨VG.Proof.Sha256.AArch64.scrR s₀, by simp [h.wr], VG.Proof.Sha256.StateMem.contains_offset (by simp only [slot]; omega) (by simp only [slot]; omega)⟩

theorem blk_contains {i t : Nat} (hi : i < VG.Proof.Sha256.AArch64.nb s₀) (ht : t < 16) :
    (VG.Proof.Sha256.AArch64.blR s₀).Contains (VG.Proof.Sha256.AArch64.blkAddr s₀ i + BitVec.ofNat 64 (4 * t)) 4 := by
  have := h.nb_lt
  rw [show VG.Proof.Sha256.AArch64.blkAddr s₀ i + BitVec.ofNat 64 (4 * t) =
    VG.Proof.Sha256.AArch64.bp s₀ + BitVec.ofNat 64 (64 * i + 4 * t) from Offset.add_add _ _ _]
  exact VG.Proof.Sha256.StateMem.contains_offset (by omega) (by omega)

theorem in_blk {i t : Nat} (hi : i < VG.Proof.Sha256.AArch64.nb s₀) (ht : t < 16) :
    InRegions (s₀.rd ++ s₀.wr) (VG.Proof.Sha256.AArch64.blkAddr s₀ i + BitVec.ofNat 64 (4 * t)) 4 :=
  ⟨VG.Proof.Sha256.AArch64.blR s₀, by simp [h.rd], h.blk_contains hi ht⟩

end Pre

/-! ## The loop invariant -/

/-- What holds between blocks, after `i` of them. -/
structure Common (s₀ : State) (i : Nat) (s : State) : Prop where
  x0 : s.gpr .x0 = VG.Proof.Sha256.AArch64.st s₀
  x3 : s.gpr .x3 = VG.Proof.Sha256.AArch64.scr s₀
  kept : ∀ r ∈ preserved, s.gpr r = s₀.gpr r
  rd : s.rd = s₀.rd
  wr : s.wr = s₀.wr
  frame : Frame [VG.Proof.Sha256.AArch64.stR s₀, VG.Proof.Sha256.AArch64.scrR s₀] s₀.mem s.mem
  state : stateAt s.mem (VG.Proof.Sha256.AArch64.st s₀) = compressBlocks (VG.Proof.Sha256.AArch64.H₀ s₀) s₀.mem (VG.Proof.Sha256.AArch64.bp s₀) i

/-- The loop invariant, at the start of block `i`. -/
structure LInv (s₀ : State) (i : Nat) (s : State) : Prop extends VG.Proof.Sha256.AArch64.Common s₀ i s where
  x1 : s.gpr .x1 = VG.Proof.Sha256.AArch64.blkAddr s₀ i
  x2 : s.gpr .x2 = BitVec.ofNat 64 (VG.Proof.Sha256.AArch64.nb s₀ - i)

theorem preserved_sub : ∀ r ∈ preserved, r ∈ VG.Proof.Sha256.AArch64.pubRegs := by decide

/-! ## One block -/

theorem load_eq : load = [
    .ldr .w .x4 .x0 (4 * 0), .ldr .w .x5 .x0 (4 * 1), .ldr .w .x6 .x0 (4 * 2),
    .ldr .w .x7 .x0 (4 * 3), .ldr .w .x8 .x0 (4 * 4), .ldr .w .x9 .x0 (4 * 5),
    .ldr .w .x10 .x0 (4 * 6), .ldr .w .x11 .x0 (4 * 7)] := by
  decide

theorem update_eq : update ++ advance = [
    .ldr .w .x12 .x0 (4 * 0), .ldr .w .x13 .x0 (4 * 1), .ldr .w .x14 .x0 (4 * 2),
    .ldr .w .x15 .x0 (4 * 3),
    .add .w .x4 .x4 .x12, .add .w .x5 .x5 .x13, .add .w .x6 .x6 .x14, .add .w .x7 .x7 .x15,
    .ldr .w .x12 .x0 (4 * (0 + 4)), .ldr .w .x13 .x0 (4 * (1 + 4)), .ldr .w .x14 .x0 (4 * (2 + 4)),
    .ldr .w .x15 .x0 (4 * (3 + 4)),
    .add .w .x8 .x8 .x12, .add .w .x9 .x9 .x13, .add .w .x10 .x10 .x14, .add .w .x11 .x11 .x15,
    .str .w .x4 .x0 (4 * 0), .str .w .x5 .x0 (4 * 1), .str .w .x6 .x0 (4 * 2),
    .str .w .x7 .x0 (4 * 3), .str .w .x8 .x0 (4 * 4), .str .w .x9 .x0 (4 * 5),
    .str .w .x10 .x0 (4 * 6), .str .w .x11 .x0 (4 * 7),
    .addImm .x .x1 .x1 64, .subImm .x .x2 .x2 1] := by
  decide

theorem vars0 (s : State) (v : HashValue) : VG.Proof.Sha256.AArch64.Vars 0 s v ↔
    s.gpr .x4 = v[0].setWidth 64 ∧ s.gpr .x5 = v[1].setWidth 64 ∧
    s.gpr .x6 = v[2].setWidth 64 ∧ s.gpr .x7 = v[3].setWidth 64 ∧
    s.gpr .x8 = v[4].setWidth 64 ∧ s.gpr .x9 = v[5].setWidth 64 ∧
    s.gpr .x10 = v[6].setWidth 64 ∧ s.gpr .x11 = v[7].setWidth 64 := Iff.rfl

set_option simprocs false in
theorem load_ok {s₀ : State} (hp : VG.Proof.Sha256.AArch64.Pre s₀) {s : State} (hx0 : s.gpr .x0 = VG.Proof.Sha256.AArch64.st s₀)
    (hrd : s.rd = s₀.rd) (hwr : s.wr = s₀.wr) :
    WP isa (.block load) s fun s₁ =>
      VG.Proof.Sha256.AArch64.Vars 0 s₁ (stateAt s.mem (VG.Proof.Sha256.AArch64.st s₀)) ∧ (∀ r ∈ VG.Proof.Sha256.AArch64.pubRegs, s₁.gpr r = s.gpr r) ∧
      s₁.rd = s.rd ∧ s₁.wr = s.wr ∧ s₁.mem = s.mem := by
  have hin : ∀ k : Nat, k < 8 → InRegions (s.rd ++ s.wr) (VG.Proof.Sha256.AArch64.st s₀ + BitVec.ofNat 64 (4 * k)) 4 := by
    rw [hrd, hwr]; exact fun k hk => hp.in_state hk
  have h0 := hin 0 (by decide); have h1 := hin 1 (by decide); have h2 := hin 2 (by decide)
  have h3 := hin 3 (by decide); have h4 := hin 4 (by decide); have h5 := hin 5 (by decide)
  have h6 := hin 6 (by decide); have h7 := hin 7 (by decide)
  apply WP.of_runBlock
  rw [VG.Proof.Sha256.AArch64.load_eq]
  simp (config := {decide := true}) only [VG.Proof.Sha256.AArch64.vars0, runBlock_cons, runStep_some,
    runBlock_nil, exec_ldr_w, isa, RegUpd.gpr_write_self, RegUpd.gpr_write_of_ne,
    RegUpd.mem_write, RegUpd.rd_write, RegUpd.wr_write, hx0,
    h0, h1, h2, h3, h4, h5, h6, h7, Option.some.injEq,
    exists_eq_left']
  simp only [VG.Proof.Sha256.StateMem.stateAt_get _ _ (show 0 < 8 by decide), VG.Proof.Sha256.StateMem.stateAt_get _ _ (show 1 < 8 by decide),
    VG.Proof.Sha256.StateMem.stateAt_get _ _ (show 2 < 8 by decide), VG.Proof.Sha256.StateMem.stateAt_get _ _ (show 3 < 8 by decide),
    VG.Proof.Sha256.StateMem.stateAt_get _ _ (show 4 < 8 by decide), VG.Proof.Sha256.StateMem.stateAt_get _ _ (show 5 < 8 by decide),
    VG.Proof.Sha256.StateMem.stateAt_get _ _ (show 6 < 8 by decide), VG.Proof.Sha256.StateMem.stateAt_get _ _ (show 7 < 8 by decide)]
  refine ⟨⟨trivial, trivial, trivial, trivial, trivial, trivial, trivial, trivial⟩, fun r hr => ?_, trivial⟩
  simp only [VG.Proof.Sha256.AArch64.pubRegs, List.mem_cons, List.not_mem_nil, or_false] at hr
  rcases hr with rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl |
    rfl | rfl <;>
    simp (config := {decide := true}) only [RegUpd.gpr_write_of_ne]

theorem frame_writeState {s₀ : State} {m m' : Mem} (h : Frame [VG.Proof.Sha256.AArch64.stR s₀] m m') (v : HashValue) :
    Frame [VG.Proof.Sha256.AArch64.stR s₀] m (VG.Proof.Sha256.StateMem.writeState m' (VG.Proof.Sha256.AArch64.st s₀) v) := by
  have c : ∀ k, k < 8 → (VG.Proof.Sha256.AArch64.stR s₀).Contains (VG.Proof.Sha256.AArch64.st s₀ + BitVec.ofNat 64 (4 * k)) (32 / 8) :=
    fun k hk => VG.Proof.Sha256.StateMem.contains_offset (by omega) (by omega)
  simp only [VG.Proof.Sha256.StateMem.writeState]
  refine (((((((h.writeW ?_ _ (c 0 ?_)).writeW ?_ _ (c 1 ?_)).writeW ?_ _ (c 2 ?_)).writeW ?_ _
    (c 3 ?_)).writeW ?_ _ (c 4 ?_)).writeW ?_ _ (c 5 ?_)).writeW ?_ _ (c 6 ?_)).writeW ?_ _ (c 7 ?_) <;>
  simp

set_option simprocs false in
theorem update_ok {s₀ : State} (hp : VG.Proof.Sha256.AArch64.Pre s₀) {s : State} (V H : HashValue) (hv : VG.Proof.Sha256.AArch64.Vars 0 s V)
    (hx0 : s.gpr .x0 = VG.Proof.Sha256.AArch64.st s₀) (hrd : s.rd = s₀.rd) (hwr : s.wr = s₀.wr)
    (hH : ∀ k : Nat, (hk : k < 8) → s.mem.readW (VG.Proof.Sha256.AArch64.st s₀ + BitVec.ofNat 64 (4 * k)) 32 = H[k]) :
    WP isa (.block (update ++ advance)) s fun s' =>
      s'.mem = VG.Proof.Sha256.StateMem.writeState s.mem (VG.Proof.Sha256.AArch64.st s₀) (Vector.zipWith (· + ·) V H) ∧
      s'.gpr .x1 = s.gpr .x1 + 64 ∧ s'.gpr .x2 = s.gpr .x2 - 1 ∧
      s'.gpr .x0 = s.gpr .x0 ∧ s'.gpr .x3 = s.gpr .x3 ∧
      (∀ r ∈ preserved, s'.gpr r = s.gpr r) ∧ s'.rd = s.rd ∧ s'.wr = s.wr := by
  have hin : ∀ k : Nat, k < 8 → InRegions (s.rd ++ s.wr) (VG.Proof.Sha256.AArch64.st s₀ + BitVec.ofNat 64 (4 * k)) 4 := by
    rw [hrd, hwr]; exact fun k hk => hp.in_state hk
  have hout : ∀ k : Nat, k < 8 → InRegions s.wr (VG.Proof.Sha256.AArch64.st s₀ + BitVec.ofNat 64 (4 * k)) 4 := by
    rw [hwr]; exact fun k hk => hp.out_state hk
  have i0 := hin 0 (by decide); have i1 := hin 1 (by decide); have i2 := hin 2 (by decide)
  have i3 := hin 3 (by decide); have i4 := hin (0 + 4) (by decide); have i5 := hin (1 + 4) (by decide)
  have i6 := hin (2 + 4) (by decide); have i7 := hin (3 + 4) (by decide)
  have o0 := hout 0 (by decide); have o1 := hout 1 (by decide); have o2 := hout 2 (by decide)
  have o3 := hout 3 (by decide); have o4 := hout 4 (by decide); have o5 := hout 5 (by decide)
  have o6 := hout 6 (by decide); have o7 := hout 7 (by decide)
  have m0 := hH 0 (by decide); have m1 := hH 1 (by decide); have m2 := hH 2 (by decide)
  have m3 := hH 3 (by decide); have m4 := hH (0 + 4) (by decide); have m5 := hH (1 + 4) (by decide)
  have m6 := hH (2 + 4) (by decide); have m7 := hH (3 + 4) (by decide)
  rw [VG.Proof.Sha256.AArch64.vars0] at hv
  obtain ⟨v0, v1, v2, v3, v4, v5, v6, v7⟩ := hv
  apply WP.of_runBlock
  rw [VG.Proof.Sha256.AArch64.update_eq]
  simp (config := {decide := true}) only [runBlock_cons, runStep_some,
    runBlock_nil, exec_ldr_w, exec_str_w, exec_add, exec_addImm_x, exec_subImm_x, State.read,
    RegUpd.gpr_write_self, RegUpd.gpr_write_of_ne, RegUpd.mem_write, RegUpd.rd_write, RegUpd.wr_write,
    RegUpd.sp_write, Size.bits, hx0,
    i0, i1, i2, i3, i4, i5, i6, i7, o0, o1, o2, o3, o4, o5, o6, o7,
    m0, m1, m2, m3, m4, m5, m6, m7, v0, v1, v2, v3, v4, v5, v6, v7,
    BitVec.setWidth_setWidth_of_le, BitVec.setWidth_eq,
    Option.some.injEq, exists_eq_left']
  refine ⟨?_, ?_⟩
  · simp only [VG.Proof.Sha256.StateMem.writeState, Vector.getElem_zipWith]
  and_intros
  all_goals first
    | trivial
    | rfl
    | (intro r hr
       simp only [preserved, List.mem_cons, List.not_mem_nil, or_false] at hr
       rcases hr with rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl | rfl <;>
       simp (config := {decide := true}) only [RegUpd.gpr_write_of_ne])

theorem compressBlocks_succ (H : HashValue) (m : Mem) (p : Addr) (i : Nat) :
    compressBlocks H m p (i + 1) =
      compress (compressBlocks H m p i) (blockAt m (p + BitVec.ofNat 64 (64 * i))) := by
  simp [compressBlocks, List.range_succ, List.foldl_append]

theorem blk_word {s₀ : State} (i t : Nat) (ht : t < 16) :
    rev32 (s₀.mem.readW (VG.Proof.Sha256.AArch64.blkAddr s₀ i + BitVec.ofNat 64 (4 * t)) 32) = W (VG.Proof.Sha256.AArch64.blk s₀ i) t := by
  rw [W_lt _ ht, rev32_readW]
  simp only [VG.Proof.Sha256.AArch64.blk, blockAt, parseBlock]
  rw [show VG.Proof.Sha256.AArch64.blkAddr s₀ i + BitVec.ofNat 64 (4 * t) + 1 = VG.Proof.Sha256.AArch64.blkAddr s₀ i + BitVec.ofNat 64 (4 * t + 1) from
      Offset.add_add _ _ 1,
    show VG.Proof.Sha256.AArch64.blkAddr s₀ i + BitVec.ofNat 64 (4 * t + 1) + 1 = VG.Proof.Sha256.AArch64.blkAddr s₀ i + BitVec.ofNat 64 (4 * t + 2) from
      Offset.add_add _ _ 1,
    show VG.Proof.Sha256.AArch64.blkAddr s₀ i + BitVec.ofNat 64 (4 * t + 2) + 1 = VG.Proof.Sha256.AArch64.blkAddr s₀ i + BitVec.ofNat 64 (4 * t + 3) from
      Offset.add_add _ _ 1]

theorem win_sub (p : Addr) : Region.Sub (VG.Proof.Sha256.AArch64.winRegion p) ⟨p, 112⟩ := Region.sub_prefix (by omega)

theorem body_ok {s₀ : State} (hp : VG.Proof.Sha256.AArch64.Pre s₀) {i : Nat} (hi : i < VG.Proof.Sha256.AArch64.nb s₀) {s : State}
    (hL : VG.Proof.Sha256.AArch64.LInv s₀ i s) :
    WP isa body s fun s' =>
      (eval (.nonzero .x .x2) s' = some false ∧ VG.Proof.Sha256.AArch64.Common s₀ (VG.Proof.Sha256.AArch64.nb s₀) s') ∨
      (eval (.nonzero .x .x2) s' = some true ∧ i + 1 < VG.Proof.Sha256.AArch64.nb s₀ ∧ VG.Proof.Sha256.AArch64.LInv s₀ (i + 1) s') := by
  refine WP.seq (WP.mono (VG.Proof.Sha256.AArch64.load_ok hp hL.x0 hL.rd hL.wr) fun s₁ ⟨hv₁, hpub₁, hrd₁, hwr₁, hm₁⟩ => ?_)
  have hwin : ∀ r' ∈ [VG.Proof.Sha256.AArch64.winRegion (VG.Proof.Sha256.AArch64.scr s₀)], (VG.Proof.Sha256.AArch64.blR s₀).Disjoint r' := by
    simpa using Region.Disjoint.sub_right hp.blk_scr (VG.Proof.Sha256.AArch64.win_sub _)
  have hblk : ∀ m, Frame [VG.Proof.Sha256.AArch64.winRegion (VG.Proof.Sha256.AArch64.scr s₀)] s₁.mem m → ∀ t : Nat, t < 16 →
      rev32 (m.readW (VG.Proof.Sha256.AArch64.blkAddr s₀ i + BitVec.ofNat 64 (4 * t)) 32) = W (VG.Proof.Sha256.AArch64.blk s₀ i) t := by
    intro m hm t ht
    rw [hm.readW (hp.blk_contains hi ht) hwin (by decide), hm₁,
      hL.frame.readW (hp.blk_contains hi ht) (by simpa using ⟨hp.blk_st, hp.blk_scr⟩) (by decide)]
    exact VG.Proof.Sha256.AArch64.blk_word i t ht
  have hx1₁ : s₁.gpr .x1 = VG.Proof.Sha256.AArch64.blkAddr s₀ i := (hpub₁ .x1 (by decide)).trans hL.x1
  have hx3₁ : s₁.gpr .x3 = VG.Proof.Sha256.AArch64.scr s₀ := (hpub₁ .x3 (by decide)).trans hL.x3
  refine WP.seq (WP.mono (VG.Proof.Sha256.AArch64.rounds_ok _ (VG.Proof.Sha256.AArch64.blk s₀ i) _ (VG.Proof.Sha256.AArch64.scr s₀) s₁ hx1₁ hx3₁
    (by rw [hrd₁, hwr₁, hL.rd, hL.wr]; exact hp.in_slot)
    (by rw [hwr₁, hL.wr]; exact hp.out_slot)
    (fun t ht => by rw [hrd₁, hwr₁, hL.rd, hL.wr]; exact hp.in_blk hi ht) hblk hv₁ 64 (Nat.le_refl _))
    fun s₂ hR => ?_)
  have hst : ∀ r' ∈ [VG.Proof.Sha256.AArch64.winRegion (VG.Proof.Sha256.AArch64.scr s₀)], (VG.Proof.Sha256.AArch64.stR s₀).Disjoint r' := by
    simpa using Region.Disjoint.sub_right hp.st_scr (VG.Proof.Sha256.AArch64.win_sub _)
  have pub₂ : ∀ r ∈ VG.Proof.Sha256.AArch64.pubRegs, s₂.gpr r = s.gpr r := fun r hr => by
    rw [hR.pub r hr, hpub₁ r hr]
  have hx0₂ : s₂.gpr .x0 = VG.Proof.Sha256.AArch64.st s₀ := by rw [pub₂ .x0 (by decide), hL.x0]
  refine WP.mono (VG.Proof.Sha256.AArch64.update_ok hp _ (stateAt s.mem (VG.Proof.Sha256.AArch64.st s₀)) hR.vars hx0₂
    (by rw [hR.rd, hrd₁, hL.rd]) (by rw [hR.wr, hwr₁, hL.wr]) fun k hk => ?_) fun s₃ h₃ => ?_
  · rw [hR.frame.readW (VG.Proof.Sha256.StateMem.contains_offset (by omega) (by omega)) hst (by decide), hm₁,
      VG.Proof.Sha256.StateMem.stateAt_get _ _ hk]
  obtain ⟨hm₃, hx1₃, hx2₃, hx0₃, hx3₃, hkept₃, hrd₃, hwr₃⟩ := h₃
  have hx2 : s₂.gpr .x2 - 1 = BitVec.ofNat 64 (VG.Proof.Sha256.AArch64.nb s₀ - (i + 1)) := by
    rw [pub₂ .x2 (by decide), hL.x2]
    rw [show (1 : BitVec 64) = BitVec.ofNat 64 1 from rfl, Offset.ofNat_sub_ofNat (by omega), Nat.sub_sub]
  have hframe : Frame [VG.Proof.Sha256.AArch64.stR s₀, VG.Proof.Sha256.AArch64.scrR s₀] s₀.mem s₃.mem := by
    refine hL.frame.trans ?_
    rw [← hm₁]
    refine Frame.trans (hR.frame.sub fun r hr => ⟨VG.Proof.Sha256.AArch64.scrR s₀, by simp, by simp at hr; subst hr; exact VG.Proof.Sha256.AArch64.win_sub _⟩) ?_
    rw [hm₃]
    exact (VG.Proof.Sha256.AArch64.frame_writeState (Frame.refl _ _) _).sub fun r hr => ⟨r, by simp at hr; simp [hr], fun _ h => h⟩
  have hcommon : ∀ j, j = i + 1 → VG.Proof.Sha256.AArch64.Common s₀ j s₃ := by
    rintro j rfl
    refine ⟨by rw [hx0₃, hx0₂], by rw [hx3₃, pub₂ .x3 (by decide), hL.x3],
      fun r hr => by rw [hkept₃ r hr, pub₂ r (VG.Proof.Sha256.AArch64.preserved_sub r hr), hL.kept r hr],
      by rw [hrd₃, hR.rd, hrd₁, hL.rd], by rw [hwr₃, hR.wr, hwr₁, hL.wr], hframe, ?_⟩
    rw [hm₃, VG.Proof.Sha256.StateMem.stateAt_writeState, VG.Proof.Sha256.AArch64.compressBlocks_succ, ← hL.state]
    rfl
  have hev : eval (.nonzero .x .x2) s₃ = some (BitVec.ofNat 64 (VG.Proof.Sha256.AArch64.nb s₀ - (i + 1)) != 0) := by
    simp only [eval, State.read, Size.bits, BitVec.setWidth_eq, hx2₃, hx2]
  have := hp.nb_lt
  by_cases hlast : i + 1 = VG.Proof.Sha256.AArch64.nb s₀
  · left
    refine ⟨by rw [hev, hlast]; simp, hlast ▸ hcommon _ rfl⟩
  · right
    have hne : VG.Proof.Sha256.AArch64.nb s₀ - (i + 1) ≠ 0 := by omega
    have h0 : BitVec.ofNat 64 (VG.Proof.Sha256.AArch64.nb s₀ - (i + 1)) ≠ 0 := by
      intro h
      have h' := congrArg BitVec.toNat h
      rw [BitVec.toNat_ofNat, Nat.mod_eq_of_lt (by omega)] at h'
      exact hne h'
    refine ⟨by rw [hev]; simpa using h0, by omega, { hcommon _ rfl with x1 := ?_, x2 := ?_ }⟩
    · rw [hx1₃, pub₂ .x1 (by decide), hL.x1]
      exact (Offset.add_add _ _ 64).trans
        (congrArg (VG.Proof.Sha256.AArch64.bp s₀ + ·) (congrArg (BitVec.ofNat 64) (by omega)))
    · rw [hx2₃, hx2]

/-! ## The whole function -/

theorem correct {s₀ : State} (hp : VG.Proof.Sha256.AArch64.Pre s₀) :
    WP isa compress s₀ fun s' =>
      (∀ r ∈ preserved, s'.gpr r = s₀.gpr r) ∧ Proof.Sha256.compressAArch64.post s₀ s' := by
  have hc₀ : VG.Proof.Sha256.AArch64.Common s₀ 0 s₀ :=
    ⟨rfl, rfl, fun _ _ => rfl, rfl, rfl, Frame.refl _ _, rfl⟩
  refine WP.mono (Q := VG.Proof.Sha256.AArch64.Common s₀ (VG.Proof.Sha256.AArch64.nb s₀)) ?_ fun s' hc => ⟨hc.kept, hc.state⟩
  refine WP.ite (s₀.gpr .x2 == 0) (by simp [eval, State.read]) (fun h => ?_) (fun h => ?_)
  · have h0 : VG.Proof.Sha256.AArch64.nb s₀ = 0 := by simp at h; simp [VG.Proof.Sha256.AArch64.nb, h]
    exact WP.block_nil (M := isa) (h0 ▸ hc₀)
  · have hpos : 0 < VG.Proof.Sha256.AArch64.nb s₀ := by
      simp only [beq_eq_false_iff_ne, ne_eq] at h
      exact Nat.pos_of_ne_zero fun h' => h (BitVec.eq_of_toNat_eq (by simpa using h'))
    let Inv : Nat → State → Prop := fun m s => ∃ i, m = VG.Proof.Sha256.AArch64.nb s₀ - i ∧ i < VG.Proof.Sha256.AArch64.nb s₀ ∧ VG.Proof.Sha256.AArch64.LInv s₀ i s
    have hstep : ∀ m s, Inv m s → WP isa body s (fun s' =>
        (eval (.nonzero .x .x2) s' = some false ∧ VG.Proof.Sha256.AArch64.Common s₀ (VG.Proof.Sha256.AArch64.nb s₀) s') ∨
        (eval (.nonzero .x .x2) s' = some true ∧ ∃ m' < m, Inv m' s')) := by
      rintro m s ⟨i, rfl, hi, hL⟩
      refine WP.mono (VG.Proof.Sha256.AArch64.body_ok hp hi hL) fun s' h => ?_
      rcases h with ⟨he, hc⟩ | ⟨he, hi', hL'⟩
      · exact .inl ⟨he, hc⟩
      · exact .inr ⟨he, VG.Proof.Sha256.AArch64.nb s₀ - (i + 1), by omega, i + 1, rfl, hi', hL'⟩
    have hL₀ : VG.Proof.Sha256.AArch64.LInv s₀ 0 s₀ :=
      { hc₀ with
        x1 := by simp [VG.Proof.Sha256.AArch64.blkAddr]
        x2 := by simp [VG.Proof.Sha256.AArch64.nb] }
    exact WP.loop (M := isa) Inv hstep (VG.Proof.Sha256.AArch64.nb s₀) s₀ ⟨0, rfl, hpos, hL₀⟩

/-- A state satisfying the precondition (with no blocks). -/
def satState : State where
  gpr r := match r with
    | .x0 => 0x1000 | .x1 => 0x2000 | .x3 => 0x3000 | _ => 0
  sp := 0x4000
  mem _ := 0
  rd := [⟨0x2000, 0⟩]
  wr := [⟨0x1000, 32⟩, ⟨0x3000, 112⟩]

theorem compress_verified :
    Verified AArch64.target Impl.Sha256.AArch64.compress Proof.Sha256.compressAArch64 := by
  refine ⟨fun s hs => ?_, ?_, ?_⟩
  · obtain ⟨t, s', he, h₁, h₂⟩ := VG.Proof.Sha256.AArch64.correct (VG.Proof.Sha256.AArch64.pre_of s hs)
    exact ⟨t, s', he, ⟨h₁, Exec.sp he, Exec.preservedV he⟩, h₂⟩
  · refine VG.Taint.constantTime (A := taint) (Taint.ofRegs [.x0, .x1, .x2, .x3]) ?_ (by taint_decide)
    intro s₁ s₂ _ _ ⟨h1, h2, h3, h4, hsp⟩
    refine ⟨hsp, fun r hr => ?_⟩
    simp only [VG.AArch64.Taint.mem_ofRegs, List.mem_cons, List.not_mem_nil, or_false] at hr
    rcases hr with rfl | rfl | rfl | rfl <;> assumption
  · refine ⟨VG.Proof.Sha256.AArch64.satState, rfl, rfl, ?_, ?_, ?_⟩ <;>
    first
    | exact Offset.disjoint_of_le (by decide) (by decide)
    | exact Region.Disjoint.symm (Offset.disjoint_of_le (by decide) (by decide))

end VG.Proof.Sha256.AArch64

end
