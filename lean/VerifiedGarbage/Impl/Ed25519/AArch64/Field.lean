module

public import VerifiedGarbage.Impl.Ed25519.AArch64.Word
public import VerifiedGarbage.Spec.Ed25519

/-! Extended Edwards formulas on the four-word field representation.
Slots 0–21 occupy bytes 64–767 of the eight-KiB workspace. -/

@[expose] public section

namespace VG.Impl.Ed25519.AArch64
open VG.AArch64

abbrev Slot := Fin 22

def offset (s : Slot) : Nat := 64 + 32 * s.val

def constWords (v : Spec.X25519.Fe) : List Instr :=
  const64 .x4 (BitVec.ofNat 64 v.val) ++
  const64 .x5 (BitVec.ofNat 64 (v.val / 2 ^ 64)) ++
  const64 .x6 (BitVec.ofNat 64 (v.val / 2 ^ 128)) ++
  const64 .x7 (BitVec.ofNat 64 (v.val / 2 ^ 192))

def constField (o : Slot) (v : Spec.X25519.Fe) : List Instr := constWords v ++ store4 (offset o)

def copyField (o a : Slot) : List Instr := loads (offset a) .x4 .x5 .x6 .x7 ++ store4 (offset o)

/-- Small field programs, lowered to the existing verified integer code. -/
inductive FieldOp where
  | copy (out a : Slot)
  | const (out : Slot) (v : Spec.X25519.Fe)
  | mul (out a b : Slot)
  | sqr (out a : Slot)
  | add (out a b : Slot)
  | sub (out a b : Slot)
  deriving DecidableEq

def FieldOp.code : FieldOp → List Instr
  | .copy o a => copyField o a
  | .const o v => constField o v
  | .mul o a b => fieldMul (offset o) (offset a) (offset b)
  | .sqr o a => fieldSqr (offset o) (offset a)
  | .add o a b => fieldAdd (offset o) (offset a) (offset b)
  | .sub o a b => fieldSub (offset o) (offset a) (offset b)

def fieldCode (ops : List FieldOp) : List Instr := ops.flatMap FieldOp.code

/-- Add the points in slots 0–3 and 4–7 into slots 0–3. The coordinates
are X,Y,Z,T. Slot 16 holds d; slots 8–15 are temporary. Both points are
read before the result overwrites the first. -/
def pointAddOps : List FieldOp := [
  .sub 8 1 0, .sub 9 5 4, .mul 8 8 9,
  .add 9 1 0, .add 10 5 4, .mul 9 9 10,
  .mul 10 3 16, .add 10 10 10, .mul 10 10 7,
  .add 11 2 2, .mul 11 11 6,
  .sub 12 9 8, .sub 13 11 10, .add 14 11 10, .add 15 9 8,
  .mul 0 12 13, .mul 1 14 15, .mul 2 13 14, .mul 3 12 15]

def pointAdd : List Instr := fieldCode pointAddOps

/-- Double the first point: the complete addition formula on two equal
inputs, with its products of equal factors as squarings. -/
def pointDoubleOps : List FieldOp := [
  .sub 8 1 0, .sqr 8 8,
  .add 9 1 0, .sqr 9 9,
  .sqr 10 3, .mul 10 10 16, .add 10 10 10,
  .sqr 11 2, .add 11 11 11,
  .sub 12 9 8, .sub 13 11 10, .add 14 11 10, .add 15 9 8,
  .mul 0 12 13, .mul 1 14 15, .mul 2 13 14, .mul 3 12 15]

def pointDouble : List Instr := fieldCode pointDoubleOps

end VG.Impl.Ed25519.AArch64
