import VerifiedGarbage.Impl.Sha512.X86_64
import VerifiedGarbage.Impl.Sha512.X86_64.Avx2
import VerifiedGarbage.Impl.Sha512.X86_64.ShaNi
import VerifiedGarbage.Impl.MdStream.X86_64

/-!
# Streaming SHA-512: x86-64 implementation

The streaming state (192 bytes at `state`) is the hash value followed by a
128-byte buffer (see `VG.Spec.Sha512.Repr`).

* `init iv (state = rdi)` stores the initial hash value `iv`.
* `update(state = rdi, count = rsi, data = rdx, len = rcx, scratch = r8)`
  processes one block per iteration: straight from `data` while the buffer is
  empty and a whole block remains, otherwise by copying bytes into the buffer,
  compressing it once it is full.
* `finalize(state = rdi, count = rsi, out = rdx, scratch = rcx)` pads the
  buffered bytes (one or two blocks), compresses them and writes the final
  hash value; `finalizeDigest` writes only the first 48, 32 or 28 bytes of
  it, the digest of SHA-384, SHA-512/256 or SHA-512/224.

`update` and `finalize` are the generic streaming code of
`Impl/MdStream/X86_64.lean`. They take the compression function they call (a
`Callee`, e.g. `vg_sha512_compress` or `vg_sha512_compress_avx2`), and are emitted once for each
implementation (`Generic/MdHash/X86_64/Stream.lean`). It is called
with `scratch[0..1328)` as its scratch space (as much as the shared
contract gives the compression function, which the AVX2 implementation
uses); our caller's callee-saved registers are saved in `scratch[1328..1376)`. The
length field is the length in bits as a 128-bit big-endian integer:
`count >> 61`, then `count << 3` (modulo 2⁶⁴); the words of the final hash
value are big-endian.
-/

namespace VG.Impl.Sha512.X86_64.Stream

open VG.X86_64
open VG.Impl.Sha512.X86_64 (at_ compress)
open VG.Impl.MdStream.X86_64 (Params len64 out64)

/-- A compression function to call: its symbol and its code. -/
structure Callee where
  name : String
  code : Prog isa

def Callee.scalar : Callee := ⟨"vg_sha512_compress", compress⟩
def Callee.avx2 : Callee := ⟨"vg_sha512_compress_avx2", Avx2.compress⟩
def Callee.shani : Callee := ⟨"vg_sha512_compress_shani", ShaNi.compress⟩

def init (iv : Spec.Sha512.HashValue) : Prog isa :=
  .block ((List.range 8).flatMap fun k => [.movImm64 .rax iv[k]!, .store (at_ .rdi (8 * k)) .rax])

/-- The sizes, the length field and the digest. -/
def params : Params where
  N := 64
  B := 128
  L := 16
  so := 1328
  len := ([.mov .rax (.reg .r12), .shift .shr .rax 61, .bswap .rax, .store (at_ .rbx 176) .rax] : List Instr) ++
    len64 184 true
  out := out64 8

def update (f : Callee) : Prog isa := MdStream.X86_64.update params f.name f.code

def finalize (f : Callee) : Prog isa := MdStream.X86_64.finalize params f.name f.code

/-! ## The truncated digests

SHA-384, SHA-512/256 and SHA-512/224 output the first 48, 32 and 28 bytes of
the final hash value: `params` with a digest of 6 or 4 words, or of 3 words
and the high half of the fourth (`outHi`). -/

/-- The high half of word `k` of the hash value at `rbx`, big-endian, written
to `rbp + 8 k`. -/
def outHi (k : Nat) : List Instr :=
  [.mov32 .rax (.mem (at_ .rbx (8 * k + 4))), .bswap32 .rax, .store32 (at_ .rbp (8 * k)) .rax]

def params384 : Params := { params with out := out64 6 }
def params512_256 : Params := { params with out := out64 4 }
def params512_224 : Params := { params with out := out64 3 ++ outHi 3 }

/-- `finalize`, writing the digest `P.out` writes (`params384`, …). -/
def finalizeDigest (P : Params) (f : Callee) : Prog isa := MdStream.X86_64.finalize P f.name f.code

end VG.Impl.Sha512.X86_64.Stream
